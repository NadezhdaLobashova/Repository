
#Область ОбработчикиСобытийФормы
    
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
    
    Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
    
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		
		Если ТипЗнч(Параметры.Основание) = Тип("Структура") Тогда
			Если Параметры.Основание.Свойство("Сервис") Тогда
				НоваяСтрока = Объект.Сервисы.Добавить();
				НоваяСтрока.Сервис = Параметры.Основание.Сервис;
			КонецЕсли; 
			
			Если Параметры.Основание.Свойство("Компоненты") Тогда
				Для каждого Компонент Из Параметры.Основание.Компоненты Цикл
					НоваяСтрока = Объект.Компоненты.Добавить();
					НоваяСтрока.Компонент = Компонент;
				КонецЦикла; 
			КонецЕсли; 
		КонецЕсли;
		
    	ЗаполнитьСпискиПоТабличнымЧастям(Объект);
       
		Если ЗначениеЗаполнено(Параметры.ЗначениеКопирования) Тогда
			ОбщегоНазначенияУСП.УстановитьФорматированноеОписаниеИзХранилища(Текст, Параметры.ЗначениеКопирования.ТекстHTML);
		КонецЕсли;
        
	КонецЕсли;
        
    ПараметрыДляВыбора = Новый Массив();
	ПараметрыДляВыбора.Добавить(Новый ПараметрВыбора("Отбор.Владелец", СервисСписок.ВыгрузитьЗначения()));
	ПараметрыДляВыбора.Добавить(Новый ПараметрВыбора("Отбор.ИспользоватьВЦентреИдей", Истина));
	Элементы.КомпонентСписок.ПараметрыВыбора = Новый ФиксированныйМассив(ПараметрыДляВыбора);
    
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ЗаполнитьСпискиПоТабличнымЧастям(ТекущийОбъект);
	РедактируемыйОбъект = РеквизитФормыВЗначение("Объект");
	ОбщегоНазначенияУСП.УстановитьФорматированноеОписаниеИзХранилища(Текст, РедактируемыйОбъект.ТекстHTML);
    
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ЗаполнитьТабличныеЧастиПоСпискам(ТекущийОбъект);
	ОбщегоНазначенияУСП.ПоместитьФорматированноеОписаниеВХранилище(Текст, ТекущийОбъект.ТекстHTML);
	ТекущийОбъект.Текст = Текст.ПолучитьТекст();
    
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовФормы
    
&НаКлиенте
Процедура ОбъектПриИзменении(Элемент)
    
    Если ЗначениеЗаполнено(Объект.Объект) Тогда
        Оповещение = Новый ОписаниеОповещения("ОбъектПриИзмененииЗавершение", ЭтотОбъект);
        ПоказатьВопрос(Оповещение, НСтр("ru='Заполнить сервисы и компоненты из выбранного объекта?'"), РежимДиалогаВопрос.ДаНет); 
    КонецЕсли; 
    
КонецПроцедуры

&НаКлиенте
Процедура СервисСписокПриИзменении(Элемент)
	
	ПриИзмененииСервиса();
	
КонецПроцедуры

&НаКлиенте
Процедура СервисСписокНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ДанныеПодбора = СервисСписок.ВыгрузитьЗначения();
	ПараметрыФормы = Новый Структура("ДанныеПодбора", ДанныеПодбора);
	
	Оповещение = Новый ОписаниеОповещения("СервисСписокНачалоВыбораЗавершение", ЭтотОбъект);
	 
	ОткрытьФорму("Справочник.Сервисы.Форма.ФормаПодбора", ПараметрыФормы, Элементы.СервисСписок, УникальныйИдентификатор,,, Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура КомпонентСписокНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ДанныеПодбора = КомпонентСписок.ВыгрузитьЗначения();
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ДанныеПодбора", ДанныеПодбора);
	ПараметрыФормы.Вставить("Отбор", Новый Структура("Владелец", Новый ФиксированныйМассив(СервисСписок.ВыгрузитьЗначения())));
	
	Оповещение = Новый ОписаниеОповещения("КомпонентСписокНачалоВыбораЗавершение", ЭтотОбъект);
	 
	ОткрытьФорму("Справочник.КомпонентыСервиса.Форма.ФормаПодбора", ПараметрыФормы, Элементы.КомпонентСписок, УникальныйИдентификатор,,, Оповещение);
	
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции
    
&НаКлиенте
Процедура ПриИзмененииСервиса()
	
	ПараметрыДляВыбора = Новый Массив();
	ПараметрыДляВыбора.Добавить(Новый ПараметрВыбора("Отбор.Владелец", СервисСписок.ВыгрузитьЗначения()));
	ПараметрыДляВыбора.Добавить(Новый ПараметрВыбора("Отбор.ИспользоватьВЦентреИдей", Истина));
	Элементы.КомпонентСписок.ПараметрыВыбора = Новый ФиксированныйМассив(ПараметрыДляВыбора);
	
КонецПроцедуры

&НаКлиенте 
Процедура ОбъектПриИзмененииЗавершение(Результат, ПараметрыОповещения) Экспорт
    
    Если Результат = КодВозвратаДиалога.Да Тогда
        ОбъектПриИзмененииЗавершениеНаСервере();
    КонецЕсли; 	
	
КонецПроцедуры

&НаСервере
Процедура ОбъектПриИзмененииЗавершениеНаСервере()
    
    Запрос = Новый Запрос;
    
     
    Запрос.Текст = 
    	"ВЫБРАТЬ
        |   ПожеланияСервисы.Сервис
        |ИЗ
        |   Справочник.Пожелания.Сервисы КАК ПожеланияСервисы
        |ГДЕ
        |   ПожеланияСервисы.Ссылка = &Объект";
    
    Запрос.УстановитьПараметр("Объект", Объект.Объект);
    
    Результат = Запрос.Выполнить();
    Выборка = Результат.Выбрать();
    
    Объект.Сервисы.Очистить();
    Пока Выборка.Следующий() Цикл
        НоваяСтрока = Объект.Сервисы.Добавить();
        НоваяСтрока.Сервис = Выборка.Сервис;
    КонецЦикла;
    
    Запрос.Текст = 
    	"ВЫБРАТЬ
        |	ПожеланияКомпоненты.Компонент
        |ИЗ
        |	Справочник.Пожелания.Компоненты КАК ПожеланияКомпоненты
        |ГДЕ
        |	ПожеланияКомпоненты.Ссылка = &Объект";
    
    Запрос.УстановитьПараметр("Объект", Объект.Объект);
    
    Результат = Запрос.Выполнить();
    Выборка = Результат.Выбрать();
    
    Объект.Компоненты.Очистить();
    Пока Выборка.Следующий() Цикл
        НоваяСтрока = Объект.Компоненты.Добавить();
        НоваяСтрока.Компонент = Выборка.Компонент;
    КонецЦикла;
    
    ЗаполнитьСпискиПоТабличнымЧастям(Объект);
    
КонецПроцедуры

&НаКлиенте
Процедура СервисСписокНачалоВыбораЗавершение(Результат, ПараметрыДляВыбора) Экспорт
	
	Если Результат <> Неопределено Тогда
		СервисСписок.ЗагрузитьЗначения(Результат);
        Модифицированность = Истина;
	КонецЕсли;
	
	ПриИзмененииСервиса();
	
КонецПроцедуры

&НаКлиенте
Процедура КомпонентСписокНачалоВыбораЗавершение(Результат, ПараметрыДляВыбора) Экспорт
	
	Если Результат <> Неопределено Тогда
		КомпонентСписок.ЗагрузитьЗначения(Результат);
        Модифицированность = Истина;
	КонецЕсли; 
	
КонецПроцедуры
 
&НаСервере
Процедура ЗаполнитьТабличныеЧастиПоСпискам(ТекущийОбъект)
	
	ОбщегоНазначенияУСП.ЗаполнитьТаблицуИзСписка(ТекущийОбъект.Сервисы, СервисСписок, "Сервис");
	ОбщегоНазначенияУСП.ЗаполнитьТаблицуИзСписка(ТекущийОбъект.Компоненты, КомпонентСписок, "Компонент");
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСпискиПоТабличнымЧастям(Знач ТекущийОбъект)
	
	ОбщегоНазначенияУСП.ЗаполнитьСписокИзТаблицы(СервисСписок, ТекущийОбъект.Сервисы, "Сервис");
	ОбщегоНазначенияУСП.ЗаполнитьСписокИзТаблицы(КомпонентСписок, ТекущийОбъект.Компоненты, "Компонент");
	
КонецПроцедуры

#КонецОбласти 


