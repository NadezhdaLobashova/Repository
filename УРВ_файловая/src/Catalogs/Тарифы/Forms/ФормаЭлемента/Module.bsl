
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.ОграниченияКоличествоДопЛицензийРасширяющейПодписки.Видимость = ЗначениеЗаполнено(Объект.ПериодДействияРасширяющейПодписки);
	Элементы.ПериодДействияРасширяющейПодписки.Видимость = Не Объект.РасширениеТарифа;
	Элементы.ГруппаДоступныеТарифы.Видимость = Объект.РасширениеТарифа;
    УстановитьКартинкуКомментария(Объект.Комментарий, Элементы.ГруппаКомментарий);
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ЗначениеОписанияДляАбонента = ТекущийОбъект.ОписаниеДляАбонентов.Получить();
	Если ТипЗнч(ЗначениеОписанияДляАбонента) = Тип("ФорматированныйДокумент") Тогда 
		ОписаниеДляАбонентов = ЗначениеОписанияДляАбонента;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ТекущийОбъект.ОписаниеДляАбонентов = Новый ХранилищеЗначения(ОписаниеДляАбонентов);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПериодДействияРасширяющейПодпискиПриИзменении(Элемент)
	
	Элементы.ОграниченияКоличествоДопЛицензийРасширяющейПодписки.Видимость = ЗначениеЗаполнено(Объект.ПериодДействияРасширяющейПодписки);
	
КонецПроцедуры

&НаКлиенте
Процедура РасширениеТарифаПриИзменении(Элемент)
	
	Элементы.ПериодДействияРасширяющейПодписки.Видимость = Не Объект.РасширениеТарифа;
	Элементы.ГруппаДоступныеТарифы.Видимость = Объект.РасширениеТарифа;
	
КонецПроцедуры

&НаКлиенте
Процедура КомментарийПриИзменении(Комментарий, СтраницаКомментарий)
    
    УстановитьКартинкуКомментария(Объект.Комментарий, Элементы.ГруппаКомментарий);
        
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьКартинкуКомментария(Комментарий, СтраницаКомментария)
    
    Если ПустаяСтрока(Комментарий) Тогда
        СтраницаКомментария.Картинка = Новый Картинка;
    Иначе
        СтраницаКомментария.Картинка = БиблиотекаКартинок.Комментарий;
    КонецЕсли;

КонецПроцедуры

#КонецОбласти 
