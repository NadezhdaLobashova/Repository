
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Если Параметры.Отбор.Свойство("Владелец") Тогда
		Элементы.Владелец.Видимость = Ложь;
	КонецЕсли;

    УстановитьУсловноеОформление();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Элементы.Список.Обновить();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	НовоеУсловноеОформление = Список.УсловноеОформление.Элементы.Добавить();
	ОтборУсловногоОформления = НовоеУсловноеОформление.Отбор;
	
	ТипЭлементаОтбора = Тип("ЭлементОтбораКомпоновкиДанных");
	ТипГруппыОтбора = Тип("ГруппаЭлементовОтбораКомпоновкиДанных");
	
	ГруппаИ           = ОтборУсловногоОформления.Элементы.Добавить(ТипГруппыОтбора);
	ГруппаИ.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
	ПерваяГруппаИЛИ   = ГруппаИ.Элементы.Добавить(ТипГруппыОтбора);
	ПерваяГруппаИЛИ.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;
	ПерваяНачальнаяДата = ПерваяГруппаИЛИ.Элементы.Добавить(ТипЭлементаОтбора);
	ПерваяНачальнаяДата.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ДатаНачалаДействия");
	ПерваяНачальнаяДата.ВидСравнения = ВидСравненияКомпоновкиДанных.НеРавно;
	ПерваяНачальнаяДата.ПравоеЗначение = '00010101';
	
	ВтораяНачальнаяДата = ПерваяГруппаИЛИ.Элементы.Добавить(ТипЭлементаОтбора);
	ВтораяНачальнаяДата.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ДатаНачалаДействия");
	ВтораяНачальнаяДата.ВидСравнения = ВидСравненияКомпоновкиДанных.БольшеИлиРавно;
	ВтораяНачальнаяДата.ПравоеЗначение = ТекущаяДатаСеанса();
	
	ВтораяГруппаИЛИ = ГруппаИ.Элементы.Добавить(ТипГруппыОтбора);
	ВтораяГруппаИЛИ.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;
	ПерваяНачальнаяДата = ВтораяГруппаИЛИ.Элементы.Добавить(ТипЭлементаОтбора);
	ПерваяНачальнаяДата.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ДатаОкончанияДействия");
	ПерваяНачальнаяДата.ВидСравнения = ВидСравненияКомпоновкиДанных.НеРавно;
	ПерваяНачальнаяДата.ПравоеЗначение = '00010101';
	
	ВтораяНачальнаяДата = ВтораяГруппаИЛИ.Элементы.Добавить(ТипЭлементаОтбора);
	ВтораяНачальнаяДата.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ДатаОкончанияДействия");
	ВтораяНачальнаяДата.ВидСравнения = ВидСравненияКомпоновкиДанных.МеньшеИлиРавно;
	ВтораяНачальнаяДата.ПравоеЗначение = ТекущаяДатаСеанса();
	
	НовоеОформление = НовоеУсловноеОформление.Оформление;
	ПараметрЦветаТекста = НовоеОформление.Элементы.Найти("ЦветТекста");
	Если ПараметрЦветаТекста <> Неопределено Тогда 
		ПараметрЦветаТекста.Значение = WebЦвета.СветлоСерый;
		ПараметрЦветаТекста.Использование = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
