
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
    ИспользоватьНесколькоВитрин = ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоВитринСервисов");
    
    Если ИспользоватьНесколькоВитрин Тогда
        Элементы.ОтборСервис.Видимость = Ложь;
        Элементы.Сервис.Видимость = Ложь;
    	Если Параметры.Свойство("Витрина") Тогда
    		Витрина = Параметры.Витрина;
    	КонецЕсли;
    Иначе
        Если Параметры.Свойство("Сервис") Тогда
    		Сервис = Параметры.Сервис;
        КонецЕсли;
    КонецЕсли; 
	
	УстановитьОтборСписка();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ОтборСервисПриИзменении(Элемент)
	
	УстановитьОтборСписка();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборВитринаПриИзменении(Элемент)
	
	УстановитьОтборСписка();
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
    
    ПодключитьОбработчикОжидания("ОтобразитьПредварительныйПросмотр", 0.1, Истина);
    
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьОтборСписка()
	
    Список.Параметры.УстановитьЗначениеПараметра("ОтборСервис", ЗначениеЗаполнено(Сервис) И Не ИспользоватьНесколькоВитрин);
    Список.Параметры.УстановитьЗначениеПараметра("Сервис", Сервис);
    Список.Параметры.УстановитьЗначениеПараметра("ОтборВитрина", ЗначениеЗаполнено(Витрина) И ИспользоватьНесколькоВитрин);
    Список.Параметры.УстановитьЗначениеПараметра("Витрина", Витрина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтобразитьПредварительныйПросмотр()
    
    Если Элементы.Список.ТекущаяСтрока <> Неопределено Тогда
        ОтобразитьПредварительныйПросмотрНаСервере(Элементы.Список.ТекущаяСтрока);    
    КонецЕсли;
	
КонецПроцедуры
 
&НаСервере
Процедура ОтобразитьПредварительныйПросмотрНаСервере(Ссылка)
    
    ОбщегоНазначенияУСП.УстановитьФорматированноеОписаниеИзХранилища(ПредварительныйПросмотр, Ссылка.ТекстХранилище);
    
КонецПроцедуры

#КонецОбласти
