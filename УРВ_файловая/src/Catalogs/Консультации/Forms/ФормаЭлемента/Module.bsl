
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
    ИсходнаяАктуальность = Объект.Актуальна;
    БазаЗнаний.ПриСозданииНаСервере(ЭтаФорма);
    
    Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
        ПриСозданииЧтенииНаСервере();
    КонецЕсли; 
	
    ЗаполнитьШаблоныДляАвтоЗапуска();
    ЗаполнитьПоказатели();
    
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
    ПриСозданииЧтенииНаСервере()
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ЗаполнитьТабличныеЧастиПоСпискам(ТекущийОбъект);
	
	ОбщегоНазначенияУСП.ПоместитьФорматированноеОписаниеВХранилище(Описание, ТекущийОбъект.ОписаниеХранилище);
	ТекущийОбъект.Описание = Описание.ПолучитьТекст();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)

	Оповестить("КарточкаБазыЗнаний_Запись", Объект.Ссылка, ЭтаФорма);
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Комментарии, "Владелец", Объект.Ссылка);
    
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Не ЗавершениеРаботы И ТолькоЧтоСозданнаяКарточка И БылПоказанДиалогИнтерактивногоЗапускаПроцесса <> Истина Тогда
		ИнтерактивныйЗапускБизнесПроцессовКлиент.ВыполнитьИнтерактивныйЗапускБизнесПроцесса(
			ШаблоныДляАвтоЗапускаЗакрытиеКарточки, 
			Объект.Ссылка, 
			"ЗакрытиеКарточки",
			ЭтаФорма,
			Отказ,
			Истина);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОписаниеПриИзменении(Элемент)
	
	Объект.ОписаниеДатаИзменения = ТекущаяДата();
	Объект.ОписаниеАвторИзменения = ТекущийПользователь;
	
КонецПроцедуры

&НаКлиенте
Процедура СостояниеПриИзменении(Элемент)

	Отказ = Не Записать();
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Объект.Актуальна = (Объект.Состояние = ПредопределенноеЗначение("Перечисление.СостоянияКонсультаций.Актуальна"));
	
	Если ИсходнаяАктуальность Тогда
		ИнтерактивныйЗапускБизнесПроцессовКлиент.ВыполнитьИнтерактивныйЗапускБизнесПроцесса(
			ШаблоныДляАвтоЗапускаСнятиеАктуальностиКарточки, 
			Объект.Ссылка, 
			"СнятиеАктуальностиКарточки",
			ЭтаФорма,
			Отказ,
			Ложь);
	КонецЕсли;
		
	ИсходнаяАктуальность = Не ИсходнаяАктуальность;
	
КонецПроцедуры

&НаКлиенте
Процедура СервисСписокПриИзменении(Элемент)
	
	ПриИзмененииСервиса();
	
КонецПроцедуры

&НаКлиенте
Процедура СервисСписокНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ДанныеПодбора = СервисСписок.ВыгрузитьЗначения();
	ПараметрыФормы = Новый Структура("ДанныеПодбора", ДанныеПодбора);
	
	Оповещение = Новый ОписаниеОповещения("СервисСписокНачалоВыбораЗавершение", ЭтотОбъект);
	 
	ОткрытьФорму("Справочник.Сервисы.Форма.ФормаПодбора", ПараметрыФормы, Элементы.СервисСписок, УникальныйИдентификатор,,, Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура ВитринаСписокПриИзменении(Элемент)
    
    ВитринаСписокПриИзмененииНаСервере();
    
КонецПроцедуры

&НаКлиенте
Процедура ВитринаСписокНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ДанныеПодбора = ВитринаСписок.ВыгрузитьЗначения();
	ПараметрыФормы = Новый Структура("ДанныеПодбора", ДанныеПодбора);
	
	Оповещение = Новый ОписаниеОповещения("ВитринаСписокНачалоВыбораЗавершение", ЭтотОбъект);
	 
	ОткрытьФорму("Справочник.Витрины.Форма.ФормаПодбора", ПараметрыФормы, Элементы.ВитринаСписок, УникальныйИдентификатор,,, Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура КомпонентСписокНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ДанныеПодбора = КомпонентСписок.ВыгрузитьЗначения();
	
	Оповещение = Новый ОписаниеОповещения("КомпонентСписокНачалоВыбораЗавершение", ЭтотОбъект);
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ДанныеПодбора", ДанныеПодбора);
	ПараметрыФормы.Вставить("Отбор", Новый Структура("Владелец", СервисСписок.ВыгрузитьЗначения()));
	 
	ОткрытьФорму("Справочник.КомпонентыСервиса.Форма.ФормаПодбора", ПараметрыФормы, Элементы.КомпонентСписок, УникальныйИдентификатор,,, Оповещение);
	
КонецПроцедуры
 
&НаКлиенте
Процедура РазделСписокНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ДанныеПодбора = РазделСписок.ВыгрузитьЗначения();
	
	Оповещение = Новый ОписаниеОповещения("РазделСписокНачалоВыбораЗавершение", ЭтотОбъект);
	ПараметрыФормы = Новый Структура("ДанныеПодбора", ДанныеПодбора);
	ПараметрыФормы.Вставить("Сервисы", СервисСписок.ВыгрузитьЗначения());
	ПараметрыФормы.Вставить("Компоненты", КомпонентСписок.ВыгрузитьЗначения());
	 
	ОткрытьФорму("Справочник.Разделы.Форма.ФормаПодбора", ПараметрыФормы, Элементы.РазделСписок, УникальныйИдентификатор,,, Оповещение);
	
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыКомментарии

&НаКлиенте
Процедура КомментарииВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	                                                                                    	
	СтандартнаяОбработка = Ложь;
	Если НЕ ЗначениеЗаполнено(Элемент.ТекущиеДанные.Ссылка) Тогда
		ПараметрыФормы = Новый Структура("Дата, Автор, ЛинияПоддержки, Комментарий, ВнутренняяПереписка");
		ЗаполнитьЗначенияСвойств(ПараметрыФормы, Объект.Комментарии.НайтиПоИдентификатору(ВыбраннаяСтрока));
		ПараметрыФормы.Вставить("ВыбраннаяСтрока", ВыбраннаяСтрока);
		ОткрытьФорму("ОбщаяФорма.ФормаКомментария", ПараметрыФормы, Элементы.Комментарии);
	Иначе
		ПараметрыФормы = Новый Структура("Ключ", Элемент.ТекущиеДанные.Ссылка);
		ОткрытьФорму("Справочник.Комментарии.Форма.ФормаЭлемента", ПараметрыФормы, Элементы.Комментарии);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КомментарииОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Элементы.Комментарии.Обновить();
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура УдалитьКомментарий(Команда)
	
	ТекущиеДанные = Элементы.Комментарии.ТекущиеДанные;
	Если ТекущиеДанные <>Неопределено И ЗначениеЗаполнено(ТекущиеДанные.Ссылка) Тогда
		УдалитьКомментарийНаСервере(ТекущиеДанные.Ссылка);
		Элементы.Комментарии.Обновить();
	Иначе
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура УдалитьКомментарийНаСервере(СсылкаНаКомментарий)
	КомментарийОбъект = СсылкаНаКомментарий.ПолучитьОбъект();
	КомментарийОбъект.ПометкаУдаления = Истина;
	КомментарийОбъект.Записать();
КонецПроцедуры

&НаКлиенте
Процедура ВставитьКартинкуИзБуфера(Команда)
	
	КомпонентаУстановлена = РаботаСКартинкамиКлиент.ПроинициализироватьКомпоненту();
	Если Не КомпонентаУстановлена Тогда
		РаботаСКартинкамиКлиент.УстановитьКомпоненту();
	КонецЕсли;
	
	ПутьКФайлу = КомпонентаПолученияКартинкиИзБуфера.ПолучитьКартинкуИзБуфера();
	
	Если Не ПустаяСтрока(ПутьКФайлу) Тогда
		Картинка = Новый Картинка(ПутьКФайлу);
		АдресКартинки = ПоместитьВоВременноеХранилище(Картинка, АдресКартинки);
		ВставитьКартинкуИзБуфераНаСервере(АдресКартинки);
		Модифицированность = Истина;
	Иначе
		ПоказатьПредупреждение(,НСтр("ru = 'Буфер обмена не содержит картинки'"));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьКомментарий(Команда)
	
	Если ПустаяСтрока(СокрЛП(НовыйКомментарий)) Тогда
		Возврат;
	КонецЕсли;
	
	Если Объект.Ссылка.Пустая() Тогда
		Оповещение = Новый ОписаниеОповещения("ДобавитьКомментарийПродолжение", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, НСтр("ru = 'Консультация не записана. Комментарии можно писать только для записанных консультаций. Записать консультацию?'"), 
			РежимДиалогаВопрос.ДаНет, 30, КодВозвратаДиалога.Нет, , КодВозвратаДиалога.Нет);
	Иначе
		ДобавитьКомментарийЗавершение();
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьКомментарийПродолжение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		РезультатЗаписи = Записать();
		Если РезультатЗаписи Тогда
			Комментарии.Параметры.УстановитьЗначениеПараметра("Владелец", Объект.Ссылка);
			ДобавитьКомментарийЗавершение();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьКомментарийЗавершение()
	
	ДобавитьКомментарийНаСервере();
	Элементы.Комментарии.Обновить();
	НовыйКомментарий = "";
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьКомментарийНаСервере()
	НоваяСтрока = Справочники.Комментарии.СоздатьЭлемент();
	НоваяСтрока.ВладелецКомментария = Объект.Ссылка;
	НоваяСтрока.Дата = ТекущаяДата();
	НоваяСтрока.Автор = ТекущийПользователь;
	НоваяСтрока.ЛинияПоддержки = ЛинияПоддержки;
	НоваяСтрока.Комментарий = НовыйКомментарий;
	НоваяСтрока.ВнутренняяПереписка = ВнутренняяПереписка;
	НоваяСтрока.Записать();
КонецПроцедуры

&НаКлиенте
Процедура ПринятьВРаботу(Команда)
	
	Если Объект.Ответственный = ТекущийПользователь Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru='Консультация уже принята в работу.'"); ;
		Сообщение.Сообщить(); 
		Возврат;
	КонецЕсли; 
	
	Результат = ПринятьВРаботуНаСервере();
	Если Результат = Истина Тогда
		ОповеститьОбИзменении(Объект.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПеревестиНаЛинию(Команда)
	
	Если Не Записать() Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Результат = ПеревестиНаЛиниюНаСервере();
		Если Результат = Истина Тогда
			ОповеститьОбИзменении(Объект.Ссылка);
		КонецЕсли;
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура Перенаправить(Команда)
	
	Если Не Записать() Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		СписокКонсультаций = Новый Массив;
		СписокКонсультаций.Добавить(Объект.Ссылка);
		Оповещение = Новый ОписаниеОповещения("ПеренаправитьЗавершение", ЭтотОбъект);
		ОбслуживаниеКлиент.ПеренаправитьОбъекты(СписокКонсультаций, Тип("СправочникСсылка.Консультации"), Оповещение);
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ПеренаправитьЗавершение(Адресация, Параметры) Экспорт
	
	Если Адресация <> Неопределено Тогда
		Если Адресация.Ответственный <> ТекущийПользователь Тогда
			Закрыть();
		Иначе
			Прочитать();
			Элементы.Комментарии.Обновить();
		КонецЕсли;
		ОповеститьОбИзменении(Объект.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УказатьТрудозатраты(Команда)
	
	Оповещение = Новый ОписаниеОповещения("УказатьТрудозатратыЗавершение", ЭтотОбъект);
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		
		ПоказатьВопрос(Оповещение, НСтр("ru='Данные еще не записаны.
			|Указать трудозатраты можно только после записи данных.
			|Данные будут записаны.'"), РежимДиалогаВопрос.ОКОтмена, 60, КодВозвратаДиалога.ОК,, КодВозвратаДиалога.Отмена);  
	Иначе
	    ВыполнитьОбработкуОповещения(Оповещение, КодВозвратаДиалога.ОК);
	КонецЕсли; 
		
КонецПроцедуры

&НаКлиенте
Процедура УказатьТрудозатратыЗавершение(Результат, ПараметрыОповещения) Экспорт
	
	Отказ = Истина;
	
	Если Результат = КодВозвратаДиалога.ОК Тогда
		Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
			Отказ = Не Записать();
		Иначе
			Отказ = Ложь;
		КонецЕсли; 
	КонецЕсли;
			
	Если Не Отказ Тогда
		УчетВремениКлиент.ДобавитьВОтчетКлиент(ТекущаяДата(), Объект.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтветственныйСтрокойНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	СписокВыбора = Новый СписокЗначений;
	Если ЗначениеЗаполнено(Объект.ОбслуживающаяОрганизация) Тогда
		СписокВыбора.Добавить(Объект.ОбслуживающаяОрганизация);
	КонецЕсли; 
	Если ЗначениеЗаполнено(Объект.Ответственный) Тогда
		СписокВыбора.Добавить(Объект.Ответственный);
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ОтветственныйСтрокойНажатиеЗавершение", ЭтотОбъект);
	
	ПоказатьВыборИзМеню(Оповещение, СписокВыбора, Элементы.ГруппаОтветственный);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПриСозданииЧтенииНаСервере()
    
    ЗаполнитьСпискиПоТабличнымЧастям(Объект);
	РедактируемыйОбъект = РеквизитФормыВЗначение("Объект");
	Если Параметры.Свойство("ЗначениеКопирования") И ЗначениеЗаполнено(Параметры.ЗначениеКопирования) Тогда
		ОбщегоНазначенияУСП.УстановитьФорматированноеОписаниеИзХранилища(Описание, Параметры.ЗначениеКопирования.ОписаниеХранилище);
	ИначеЕсли ЗначениеЗаполнено(Объект.Ссылка) Тогда
    	ОбщегоНазначенияУСП.УстановитьФорматированноеОписаниеИзХранилища(Описание, РедактируемыйОбъект.ОписаниеХранилище);
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Функция ПринятьВРаботуНаСервере()
	
	Возврат БазаЗнаний.ПринятьВРаботу(ЭтотОбъект);
	
КонецФункции

&НаСервере
Функция ПеревестиНаЛиниюНаСервере()
	
	Возврат БазаЗнаний.ПеревестиНаЛинию(ЭтотОбъект);
	
КонецФункции

&НаСервере
Процедура ВставитьКартинкуИзБуфераНаСервере(АдресКартинки)
	
    БазаЗнаний.ВставитьКартинкуИзБуфера(ЭтотОбъект, АдресКартинки, "Описание");
    
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТабличныеЧастиПоСпискам(ТекущийОбъект)
	
    БазаЗнаний.ЗаполнитьТабличныеЧастиПоСпискам(ТекущийОбъект, ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСпискиПоТабличнымЧастям(Знач ТекущийОбъект)
	
    БазаЗнаний.ЗаполнитьСпискиПоТабличнымЧастям(ЭтотОбъект, ТекущийОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьШаблоныДляАвтоЗапуска()
	
	ШаблоныДляАвтоЗапускаЗакрытиеКарточки = ИнтерактивныйЗапускБизнесПроцессов.ПолучитьШаблоныДляАвтоЗапуска(
		Перечисления.ВидыИнтерактивныхДействий.ЗакрытиеКарточкиТолькоЧтоСозданнойКонсультации, Объект.Ссылка);
		
	ШаблоныДляАвтоЗапускаСнятиеАктуальностиКарточки = ИнтерактивныйЗапускБизнесПроцессов.ПолучитьШаблоныДляАвтоЗапуска(
		Перечисления.ВидыИнтерактивныхДействий.ИнтерактивноеСнятиеАктуальностиКонсультации,Объект.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииСервиса()
	
	ПараметрыДляВыбора = Новый Массив();
	ПараметрыДляВыбора.Добавить(Новый ПараметрВыбора("Отбор.Владелец", СервисСписок.ВыгрузитьЗначения()));
	Элементы.КомпонентСписок.ПараметрыВыбора = Новый ФиксированныйМассив(ПараметрыДляВыбора);
	
КонецПроцедуры

&НаКлиенте
Процедура СервисСписокНачалоВыбораЗавершение(Результат, ПараметрыДляВыбора) Экспорт
	
	Если Результат <> Неопределено Тогда
		СервисСписок.ЗагрузитьЗначения(Результат);
	КонецЕсли;
	
	ПриИзмененииСервиса();
	
КонецПроцедуры

&НаКлиенте
Процедура ВитринаСписокНачалоВыбораЗавершение(Результат, ПараметрыДляВыбора) Экспорт
	
	Если Результат <> Неопределено Тогда
		ВитринаСписок.ЗагрузитьЗначения(Результат);
        ВитринаСписокПриИзмененииНаСервере();
	КонецЕсли;
    
    Модифицированность = Истина;
    
КонецПроцедуры

&НаКлиенте
Процедура РазделСписокНачалоВыбораЗавершение(Результат, ПараметрыДляВыбора) Экспорт
	
	Если Результат <> Неопределено Тогда
		РазделСписок.ЗагрузитьЗначения(Результат);
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура КомпонентСписокНачалоВыбораЗавершение(Результат, ПараметрыДляВыбора) Экспорт
	
	Если Результат <> Неопределено Тогда
		КомпонентСписок.ЗагрузитьЗначения(Результат);
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ОтветственныйСтрокойНажатиеЗавершение(Результат, ПараметрыОповещения) Экспорт
	
	Если Результат <> Неопределено Тогда
		ПоказатьЗначение(, Результат.Значение);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ВитринаСписокПриИзмененииНаСервере()
    
    Запрос = Новый Запрос;
    Запрос.Текст = 
    	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
        |	Витрины.Владелец КАК Сервис
        |ИЗ
        |	Справочник.Витрины КАК Витрины
        |ГДЕ
        |	Витрины.Ссылка В (&Витрины)";
    
    Запрос.УстановитьПараметр("Витрины", ВитринаСписок.ВыгрузитьЗначения());
    
    Результат = Запрос.Выполнить();
    СервисСписок.ЗагрузитьЗначения(Результат.Выгрузить().ВыгрузитьКолонку("Сервис"));
        
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПоказатели()
	
	Запрос = Новый Запрос;
    Запрос.Текст = 
    	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
        |   ЗначенияПоказателей.ЗначениеОборот КАК КоличествоОбращений
        |ИЗ
        |   РегистрНакопления.ЗначенияПоказателей.Обороты(
        |           ,
        |           ,
        |           ,
        |           Объект = &СсылкаНаОбъект
        |               И Показатель = &КоличествоОбращений) КАК ЗначенияПоказателей";
    
    Запрос.УстановитьПараметр("СсылкаНаОбъект", Объект.Ссылка);
    Запрос.УстановитьПараметр("КоличествоОбращений", Перечисления.ПоказателиКарточек.КоличествоОбращений);
    
    Результат = Запрос.Выполнить();
    Выборка = Результат.Выбрать();
    
    Если Выборка.Следующий() Тогда
        КоличествоОбращений = Выборка.КоличествоОбращений;
    КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
