
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Запускает процедуру расчета сроков обращений по соглашению в фоне
//
// Параметры:
//  ОбъектРасчета СправочникСсылка.Сервисы, СправочникСсылка.Соглашения - объект расчета сроков обращений.
//
Процедура ЗапуститьРасчетСроковОбращенийВФоне(ОбъектРасчета) Экспорт
    
	УстановитьПривилегированныйРежим(Истина);
	Ключ = ОбъектРасчета.УникальныйИдентификатор();
	
	Отбор = Новый Структура();
	Отбор.Вставить("Ключ", Ключ);
	Отбор.Вставить("Состояние", СостояниеФоновогоЗадания.Активно);
	
	АктивныеЗадания = ФоновыеЗадания.ПолучитьФоновыеЗадания(Отбор);
	
	Если АктивныеЗадания.Количество() > 0 Тогда
		Возврат;
	КонецЕсли;
	
    Если ТипЗнч(ОбъектРасчета) = Тип("СправочникСсылка.Сервисы") Тогда
        НаименованиеЗадания = СтрШаблон(НСтр("ru = 'Расчет сроков обращений по сервису: %1'"), ОбъектРасчета);
    Иначе
        НаименованиеЗадания = СтрШаблон(НСтр("ru = 'Расчет сроков обращений по соглашению: %1'"), ОбъектРасчета);
    КонецЕсли;
	
	ИмяЭкспортнойПроцедуры = "Справочники.Соглашения.ВыполнитьРасчетСроковОбращений";
	
	ПараметрыЭкспортнойПроцедуры = Новый Массив();
	ПараметрыЭкспортнойПроцедуры.Добавить(Новый Структура("ОбъектРасчета", ОбъектРасчета));
	
	ПараметрыЗадания = Новый Массив();
	ПараметрыЗадания.Добавить(ИмяЭкспортнойПроцедуры);
	ПараметрыЗадания.Добавить(ПараметрыЭкспортнойПроцедуры);
	
	ФоновыеЗадания.Выполнить("ОбщегоНазначения.ВыполнитьВБезопасномРежиме", ПараметрыЗадания, Ключ, НаименованиеЗадания);
    
КонецПроцедуры

// Выполняет расчет сроков обращений, у которых рассчитан срок по заданному соглашению.
//
Процедура ВыполнитьРасчетСроковОбращений(ПараметрыОбработки) Экспорт
    
    Запрос = Новый Запрос;
    
    Если ТипЗнч(ПараметрыОбработки.ОбъектРасчета) = Тип("СправочникСсылка.Соглашения") Тогда
        Если ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПараметрыОбработки.ОбъектРасчета, "ПометкаУдаления") Тогда
            Запрос.Текст = 
                "ВЫБРАТЬ
                |   Сроки.Обращение,
                |   Сроки.Период
                |ИЗ
                |   РегистрСведений.СрокиОбращенийПоСоглашениям.СрезПоследних КАК Сроки
                |ГДЕ
                |   Сроки.Соглашение = &Соглашение
                |   И НЕ Сроки.Обращение.ПометкаУдаления
                |   И Сроки.Обращение.Состояние <> &СостояниеЗакрыто";
        Иначе
            Запрос.Текст =
                "ВЫБРАТЬ РАЗЛИЧНЫЕ
                |   СоглашенияСервисов.Сервис
                |ПОМЕСТИТЬ Сервисы
                |ИЗ
                |   РегистрСведений.СоглашенияСервисов КАК СоглашенияСервисов
                |ГДЕ
                |   СоглашенияСервисов.Соглашение = &Соглашение
                |;
                |
                |////////////////////////////////////////////////////////////////////////////////
                |ВЫБРАТЬ
                |   Сроки.Обращение,
                |   Сроки.Период
                |ИЗ
                |   РегистрСведений.СрокиОбращенийПоСоглашениям.СрезПоследних КАК Сроки
                |ГДЕ
                |   Сроки.Обращение.Сервис В
                |           (ВЫБРАТЬ
                |               Сервисы.Сервис
                |           ИЗ
                |               Сервисы КАК Сервисы)
                |   И НЕ Сроки.Обращение.ПометкаУдаления
                |   И Сроки.Обращение.Состояние <> &СостояниеЗакрыто";
            
        КонецЕсли;
        
        Запрос.УстановитьПараметр("Соглашение", ПараметрыОбработки.ОбъектРасчета);
    Иначе
        Запрос.Текст =
            "ВЫБРАТЬ
            |   Сроки.Обращение,
            |   Сроки.Период
            |ИЗ
            |   РегистрСведений.СрокиОбращенийПоСоглашениям.СрезПоследних КАК Сроки
            |ГДЕ
            |   Сроки.Обращение.Сервис = &Сервис
            |   И НЕ Сроки.Обращение.ПометкаУдаления
            |   И Сроки.Обращение.Состояние <> &СостояниеЗакрыто";
        
        Запрос.УстановитьПараметр("Сервис", ПараметрыОбработки.ОбъектРасчета);
    КонецЕсли;
    Запрос.УстановитьПараметр("СостояниеЗакрыто", Перечисления.СостоянияОбращений.Закрыто);
    
    Результат = Запрос.Выполнить();
    Выборка = Результат.Выбрать();
    
    Пока Выборка.Следующий() Цикл
        Соглашения.УстановитьСрокОбращения(Выборка.Обращение, Выборка.Период, Истина);
    КонецЦикла;
    
    Комментарий = СтрШаблон(НСтр("ru='Выполнен фоновый расчет сроков для %1 обращений.'"), Выборка.Количество()); 
    ЗаписьЖурналаРегистрации(Соглашения.СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Информация,,, Комментарий);
    
КонецПроцедуры

#КонецОбласти

#КонецЕсли
