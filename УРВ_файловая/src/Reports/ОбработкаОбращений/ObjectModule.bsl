#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	ПараметрПериод = ЭтотОбъект.КомпоновщикНастроек.Настройки.ПараметрыДанных.Элементы.Найти("Период");
	ПользовательскийПараметрПериод = Неопределено;
	ПараметрКомпоновкиДанныхПериод = Новый ПараметрКомпоновкиДанных("Период");
	
	Для каждого ЭлементНастройки Из ЭтотОбъект.КомпоновщикНастроек.ПользовательскиеНастройки.Элементы Цикл
		Если ЭлементНастройки.Параметр = ПараметрКомпоновкиДанныхПериод Тогда
			ПользовательскийПараметрПериод = ЭлементНастройки;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если ПараметрПериод <> Неопределено Тогда
		
		ДатаНачала = Неопределено;
		ДатаОкончания = Неопределено;
		
		ОтборыПериодДень = ОбщегоНазначенияКлиентСервер.НайтиЭлементыИГруппыОтбора(ЭтотОбъект.КомпоновщикНастроек.Настройки.Отбор, "ПериодДень");
		
		Если ОтборыПериодДень.Количество() > 0 Тогда
			Для каждого ЭлементОтбора Из ОтборыПериодДень Цикл
				Если ЭлементОтбора.Использование = Истина И (ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно Или ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.БольшеИлиРавно) Тогда
					ДатаНачала = ЭлементОтбора.ПравоеЗначение;
					ДатаОкончания = КонецДня(ЭлементОтбора.ПравоеЗначение);
					ЭтотОбъект.КомпоновщикНастроек.Настройки.Отбор.Элементы.Удалить(ЭлементОтбора);
				КонецЕсли;
			КонецЦикла;
		Иначе
			ОтборыПериодНеделя = ОбщегоНазначенияКлиентСервер.НайтиЭлементыИГруппыОтбора(ЭтотОбъект.КомпоновщикНастроек.Настройки.Отбор, "ПериодНеделя");
			Если ОтборыПериодНеделя.Количество() > 0 Тогда
				Для каждого ЭлементОтбора Из ОтборыПериодНеделя Цикл
					Если ЭлементОтбора.Использование = Истина И (ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно Или ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.БольшеИлиРавно) Тогда
						ДатаНачала = ЭлементОтбора.ПравоеЗначение;
						ДатаОкончания = КонецНедели(ЭлементОтбора.ПравоеЗначение);
						ЭтотОбъект.КомпоновщикНастроек.Настройки.Отбор.Элементы.Удалить(ЭлементОтбора);
					КонецЕсли;
				КонецЦикла;
			Иначе
				ОтборыПериодМесяц = ОбщегоНазначенияКлиентСервер.НайтиЭлементыИГруппыОтбора(ЭтотОбъект.КомпоновщикНастроек.Настройки.Отбор, "ПериодМесяц");
				Если ОтборыПериодМесяц.Количество() > 0 Тогда
					Для каждого ЭлементОтбора Из ОтборыПериодМесяц Цикл
						Если ЭлементОтбора.Использование = Истина И (ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно Или ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.БольшеИлиРавно) Тогда
							ДатаНачала = ЭлементОтбора.ПравоеЗначение;
							ДатаОкончания = КонецМесяца(ЭлементОтбора.ПравоеЗначение);
							ЭтотОбъект.КомпоновщикНастроек.Настройки.Отбор.Элементы.Удалить(ЭлементОтбора);
						КонецЕсли;
					КонецЦикла;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		Если ДатаНачала <> Неопределено И ДатаОкончания <> Неопределено Тогда
			ПараметрПериод.Значение.ДатаНачала = ДатаНачала;
			ПараметрПериод.Значение.ДатаОкончания = ДатаОкончания;
			Если ПользовательскийПараметрПериод <> Неопределено Тогда
				ПользовательскийПараметрПериод.Значение.ДатаНачала = ДатаНачала;
				ПользовательскийПараметрПериод.Значение.ДатаОкончания = ДатаОкончания;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли