
&НаКлиенте
Процедура РезультатОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)
	ЭтоГруппа = Ложь;
	Задача = ПолучитьРасшифровкуНаСервере(Расшифровка, ЭтоГруппа);
	
	Если Задача = Неопределено Тогда 
		Возврат; 
	КонецЕсли;

	Если ЭтоГруппа Тогда 
		Возврат; 
	КонецЕсли; //Группа стандартной обработкой проверяется на равенство, поэтому при выборе группы используем стандартную расшифровку.
    СтандартнаяОбработка = Ложь;

	КоллекцияЗдесь = ЭтаФорма.Отчет.КомпоновщикНастроек.ПользовательскиеНастройки.Элементы;
    НастройкиЗдесь = ЭтаФорма.Отчет.КомпоновщикНастроек.Настройки;
	
    //Получаем Период, указанный в настройках текущего отчета:
    ИДНастройкиПериодЗдесь = НастройкиЗдесь.ПараметрыДанных.Элементы.Найти("Период").ИдентификаторПользовательскойНастройки;
    КонецПериодаЗдесь = КоллекцияЗдесь.Найти(ИДНастройкиПериодЗдесь).Значение.ДатаОкончания;

    //"Классическая" схема расшифровки одного отчета используя другой отчет:
    //Настраиваем отбор будующего отчета
    ОтборХ = Новый Структура("Задача, Период",
                            Задача,
                            Новый СтандартныйПериод(ДобавитьМесяц(КонецПериодаЗдесь, -12),КонецПериодаЗдесь));
    
    //В параметрах должен фигурировать вариант, в котором НЕТ установки параметров пользовательских настроек и пользовательских отборов,
    //т.к. это вызовет конфликт настроек и отчет не будет сформирован.
    ПараметрыФормыХ = Новый Структура("СформироватьПриОткрытии, Отбор, КлючВарианта", Истина, ОтборХ, "ДляРасшифровки");

    ОткрытьФорму("Отчет.ПланФактПоВыполненнымРаботам.Форма", ПараметрыФормыХ, ЭтаФорма, Задача);
КонецПроцедуры

&НаСервере   
Функция ПолучитьРасшифровкуНаСервере(Расшифровка, ЭтоГруппа = Ложь)

	Данные = ПолучитьИзВременногоХранилища(ДанныеРасшифровки); 

	Поля = Данные.Элементы.Получить(Расшифровка).ПолучитьПоля(); 

	ПолеЗадача = Поля.Найти("Задача");

	Если ПолеЗадача = Неопределено Тогда
		Возврат Неопределено;
	Иначе
		ЭтоГруппа = ПолеЗадача.Значение.ЭтоГруппа;
		
		Возврат ПолеЗадача.Значение;
	КонецЕсли;

КонецФункции