
#Область ПрограммныйИнтерфейс
    
// Выполнить интерактивный запуск бизнес-процесса
//
Процедура ВыполнитьИнтерактивныйЗапускБизнесПроцесса(ШаблоныДляАвтоЗапуска, Предмет, ВидСобытияСтрока, ФормаДокумента, Отказ, ЗакрытьФорму) Экспорт
	
	Если Не ЗначениеЗаполнено(Предмет) Тогда
		Возврат;
	КонецЕсли;	
	
	ПараметрыОбработчикаОповещения = Новый Структура;
	ПараметрыОбработчикаОповещения.Вставить("ФормаДокумента", ФормаДокумента);
	ПараметрыОбработчикаОповещения.Вставить("Предмет", Предмет);
	ПараметрыОбработчикаОповещения.Вставить("ЗакрытьФорму", ЗакрытьФорму);
	ПараметрыОбработчикаОповещения.Вставить("Отказ", Отказ);
		
	ОписаниеОповещения = Новый ОписаниеОповещения("ЗавершениеИнтерактивногоЗапускаПроцесса",
		ЭтотОбъект, ПараметрыОбработчикаОповещения);
			
	Если ШаблоныДляАвтоЗапуска.Количество() <> 0 Тогда
		
		ПараметрыФормы = Новый Структура("ВидСобытияСтрока, СписокШаблонов", ВидСобытияСтрока, ШаблоныДляАвтоЗапуска);
		Шаблон = Неопределено;
		
		Если ШаблоныДляАвтоЗапуска.Количество() = 1 Тогда
			 ОткрытьФорму("ОбщаяФорма.ОповещениеПриИнтерактивномАвтоЗапускеБизнесПроцессов",
				ПараметрыФормы,,,,,	ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		Иначе	
			 ОткрытьФорму("ОбщаяФорма.ВыборПриИнтерактивномАвтоЗапускеБизнесПроцессов",
				ПараметрыФормы,,,,, ОписаниеОповещения,	РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		КонецЕсли;	
		
		Отказ = Истина; 
	Иначе
		ВыполнитьОбработкуОповещения(ОписаниеОповещения, Неопределено);
	КонецЕсли;	
	
КонецПроцедуры

// Обработчик события завершения интерактивного запуска процесса.
//
// Параметры:
//  Шаблон    - СправочникСсылка.ШаблоныПоручений - выбранный шаблон бизнес-процесса.
//  Параметры - Структура - параметры обработчика оповещения:
//  * ФормаДокумента - УправляемаяФорма - форма объекта, из которой интерактивно запускается бизнес-процесс.
//  * Предмет - СправочникСсылка, ДокументСсылка - предмет бизнес-процесса.
//  * ЗакрытьФорму - Булево - признак закрытия формы после старта бизнес-процесса.
//
Процедура ЗавершениеИнтерактивногоЗапускаПроцесса(Шаблон, Параметры) Экспорт
	
	Параметры.ФормаДокумента.БылПоказанДиалогИнтерактивногоЗапускаПроцесса = Истина;
	Если Шаблон <> Неопределено И Шаблон <> КодВозвратаДиалога.Отмена Тогда
		Если Параметры.ЗакрытьФорму = Истина И Параметры.ФормаДокумента.Открыта() Тогда
			Параметры.ФормаДокумента.Закрыть();
		КонецЕсли;
        Основание = Новый Структура("Шаблон, Предмет", Шаблон, Параметры.Предмет);
        #Если ВебКлиент Тогда
            ПараметрыФормы = Новый Структура("Основание", Основание);
            ОткрытьФорму("БизнесПроцесс.Поручение.ФормаОбъекта", ПараметрыФормы, Параметры.ФормаДокумента.ВладелецФормы);
        #Иначе
            ПараметрыФормы = Новый Структура("Основание", Основание);
            ОткрытьФорму("БизнесПроцесс.Поручение.ФормаОбъекта", ПараметрыФормы, Параметры.ФормаДокумента.ВладелецФормы,,,,,
                РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
        #КонецЕсли
		Возврат;
	КонецЕсли;
	
	Если Параметры.ЗакрытьФорму = Истина И Параметры.ФормаДокумента.Открыта() И Параметры.Отказ = Истина Тогда
		Параметры.ФормаДокумента.Закрыть();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти 
