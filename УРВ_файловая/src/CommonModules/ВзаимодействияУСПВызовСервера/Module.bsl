
#Область ПрограммныйИнтерфейс

// Возвращает параметры для создания электронного письма по обращению.
//
// Параметры:
//  ДокументСсылка.Обращение - обращение, по которому нужно получить параметры.
// 
// Возвращаемое значение:
//  Структура - структура параметров письма по обращению:
//  * Номер - номер обращения. 
//  * Тема - тема обращения.
//  * Инициатор - инициатор обращения.
//  * УчетнаяЗаписьСлужбыПоддержкиПоУмолчанию - учетная запись электронной почты службы поддержки сервиса по умолчанию.
//  * УчетныеЗаписиСлужбыПоддержки - учетные записи электронной почты службы поддержки сервиса.
//  * КарточкаБазыЗнаний - карточка базы знаний в обращении.
//
Функция ПараметрыПисьмаПоОбращению(Обращение) Экспорт
	
	РеквизитыОбращения = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Обращение, 
		Новый Структура("Номер, Тема, Инициатор, УчетнаяЗаписьСлужбыПоддержкиПоУмолчанию, УчетныеЗаписиСлужбыПоддержки, КарточкаБазыЗнаний",
			"Номер", "Тема", "Инициатор", "Сервис.УчетнаяЗаписьСлужбыПоддержкиПоУмолчанию", "Сервис.УчетныеЗаписиСлужбыПоддержки", "КарточкаБазыЗнаний"));
			
	ПараметрыПисьма = Новый Структура;
	
	ПараметрыПисьма.Вставить("Основание", Обращение);
	ПараметрыПисьма.Вставить("Тема", СтрШаблон(НСтр("ru='%1 %2%3)'"), 
		РеквизитыОбращения.Тема, 
		ВзаимодействияУСП.НачалоКлючаОбращения(), 
		ПрефиксацияОбъектов.УдалитьЛидирующиеНулиИзНомераОбъекта(СокрП(РеквизитыОбращения.Номер))));
	
	Если ЗначениеЗаполнено(РеквизитыОбращения.УчетнаяЗаписьСлужбыПоддержкиПоУмолчанию) Тогда
		ПараметрыПисьма.Вставить("УчетнаяЗапись", РеквизитыОбращения.УчетнаяЗаписьСлужбыПоддержкиПоУмолчанию);
	Иначе
		ВыборкаУчетныхЗаписей = РеквизитыОбращения.УчетныеЗаписиСлужбыПоддержки.Выбрать();
		Если ВыборкаУчетныхЗаписей.Следующий() Тогда
			ПараметрыПисьма.Вставить("УчетнаяЗапись", ВыборкаУчетныхЗаписей.УчетнаяЗапись);
		КонецЕсли;
	КонецЕсли;
	
	Вложения = Новый Массив;
	ПрисоединенныеФайлы.ПолучитьПрикрепленныеФайлыКОбъекту(РеквизитыОбращения.КарточкаБазыЗнаний, Вложения);
	ВложенияДляПисьма = Новый СписокЗначений;
	
	Для каждого Вложение Из Вложения Цикл
		ДанныеФайла = ПрисоединенныеФайлы.ПолучитьДанныеФайла(Вложение);
		ВложенияДляПисьма.Добавить(ДанныеФайла.СсылкаНаДвоичныеДанныеФайла, ДанныеФайла.ИмяФайла);
	КонецЦикла;
	
	Вложения = Новый Массив;
	ПрисоединенныеФайлы.ПолучитьПрикрепленныеФайлыКОбъекту(Обращение, Вложения);
	
	Для каждого Вложение Из Вложения Цикл
		ДанныеФайла = ПрисоединенныеФайлы.ПолучитьДанныеФайла(Вложение);
		ВложенияДляПисьма.Добавить(ДанныеФайла.СсылкаНаДвоичныеДанныеФайла, ДанныеФайла.ИмяФайла);
	КонецЦикла;
	
	ПараметрыПисьма.Вставить("Вложения", ВложенияДляПисьма);
	
	ПараметрыПисьма.Вставить("Кому", КонтактнаяИнформацияУСП.АктуальныеАдресаОтправкиПользователяСервиса(РеквизитыОбращения.Инициатор));
	
	Возврат ПараметрыПисьма;
	
КонецФункции

#КонецОбласти 
