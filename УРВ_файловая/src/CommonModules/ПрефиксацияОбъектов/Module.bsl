
#Область ПрограммныйИнтерфейс
    
// Удаляет лидирующие нули из номера объекта
// Переменная НомерОбъекта должна соответствовать шаблону: ССС-ХХХ...ХХ , где:
// ССС - префикс сервиса;
// "-" - разделитель;
// ХХХ...ХХ - номер/код объекта.
//
// Параметры:
//  НомерОбъекта - Строка - номер или код объекта из которого требуется лидирующие нули.
// 
Функция УдалитьЛидирующиеНулиИзНомераОбъекта(Знач НомерОбъекта) Экспорт
	
	ПользовательскийПрефикс = ПолучитьПользовательскийПрефикс(НомерОбъекта);
	
	Если НомерСодержитСтандартныйПрефикс(НомерОбъекта) Тогда
		
		Если НомерСодержитЧетырехзначныйПрефикс(НомерОбъекта) Тогда
			Префикс = Лев(НомерОбъекта, 4);
			Номер = Сред(НомерОбъекта, 5 + СтрДлина(ПользовательскийПрефикс));
		Иначе
			Префикс = Лев(НомерОбъекта, 3);
			Номер = Сред(НомерОбъекта, 4 + СтрДлина(ПользовательскийПрефикс));
		КонецЕсли;
		
	Иначе
		
		Префикс = "";
		Номер = Сред(НомерОбъекта, 1 + СтрДлина(ПользовательскийПрефикс));
		
	КонецЕсли;
	
	// удаляем лидирующие нули слева в номере
	Номер = СтроковыеФункцииКлиентСервер.УдалитьПовторяющиесяСимволы(Номер, "0");
	
	Возврат Префикс + ПользовательскийПрефикс + Номер;
	
КонецФункции

// Удаляет все пользовательские префиксы из номера объекта (все нецифровые символы)
// Переменная НомерОбъекта должна соответствовать шаблону: ССС-ХХХ...ХХ, где:
// ССС - префикс сервиса;
// "-" - разделитель;
// ХХХ...ХХ - номер/код объекта.
//
// Параметры:
//  НомерОбъекта - Строка - номер или код объекта из которого требуется лидирующие нули.
// 
Функция УдалитьПользовательскиеПрефиксыИзНомераОбъекта(Знач НомерОбъекта) Экспорт
	
	СтрокаЦифровыхСимволов = "0123456789";
	
	Если НомерСодержитСтандартныйПрефикс(НомерОбъекта) Тогда
		
		Если НомерСодержитЧетырехзначныйПрефикс(НомерОбъекта) Тогда
			Префикс     = Лев(НомерОбъекта, 4);
			НомерПолный = Сред(НомерОбъекта, 5);
		Иначе
			Префикс     = Лев(НомерОбъекта, 3);
			НомерПолный = Сред(НомерОбъекта, 4);
		КонецЕсли;
		
	Иначе
		
		Префикс     = "";
		НомерПолный = НомерОбъекта;
		
	КонецЕсли;
	
	Номер = "";
	
	Для Индекс = 1 По СтрДлина(НомерПолный) Цикл
		
		Символ = Сред(НомерПолный, Индекс, 1);
		
		Если Найти(СтрокаЦифровыхСимволов, Символ) > 0 Тогда
			
			Номер = Номер + Символ;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Префикс + Номер;
	
КонецФункции

// Получает пользовательский префикс номера/кода объекта
// Переменная НомерОбъекта должна соответствовать шаблону: ООГГ-ХХХ...ХХ или ГГ-ХХХ...ХХ, где:
// ОО - префикс организации;
// ГГ - префикс информационной базы;
// "-" - разделитель;
// АА - пользовательский префикс;
// ХХ..ХХ - номер/код объекта.
//
// Параметры:
//  НомерОбъекта - Строка - номер или код объекта из которого требуется получить пользовательский префикс.
// 
Функция ПолучитьПользовательскийПрефикс(Знач НомерОбъекта) Экспорт
	
	// возвращаемое значение функции (пользовательский префикс)
	Результат = "";
	
	Если НомерСодержитСтандартныйПрефикс(НомерОбъекта) Тогда
		
		Если НомерСодержитЧетырехзначныйПрефикс(НомерОбъекта) Тогда
			НомерОбъекта = Сред(НомерОбъекта, 5);
		Иначе
			НомерОбъекта = Сред(НомерОбъекта, 4);
		КонецЕсли;
		
	КонецЕсли;
	
	СтрокаЦифровыхСимволов = "0123456789";
	
	Для Индекс = 1 По СтрДлина(НомерОбъекта) Цикл
		
		Символ = Сред(НомерОбъекта, Индекс, 1);
		
		Если Найти(СтрокаЦифровыхСимволов, Символ) > 0 Тогда
			Прервать;
		КонецЕсли;
		
		Результат = Результат + Символ;
		
	КонецЦикла;
	
	Возврат Результат;
КонецФункции

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

Функция НомерСодержитСтандартныйПрефикс(Знач НомерОбъекта)
	
	ПозицияРазделителя = Найти(НомерОбъекта, "-");
	
	Возврат ПозицияРазделителя = 3 ИЛИ ПозицияРазделителя = 4;
	
КонецФункции

Функция НомерСодержитЧетырехзначныйПрефикс(Знач НомерОбъекта)
	
	Возврат Найти(НомерОбъекта, "-") = 4;
	
КонецФункции

#КонецОбласти
