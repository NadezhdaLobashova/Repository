
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РазрешениеДоступаДляКлиентовКомпании.Абонент КАК Абонент,
		|	РазрешениеДоступаДляКлиентовКомпании.Абонент.Представление КАК АбонентПредставление
		|ИЗ
		|	РегистрСведений.РазрешениеДоступаДляКлиентовКомпании КАК РазрешениеДоступаДляКлиентовКомпании
		|ГДЕ
		|	РазрешениеДоступаДляКлиентовКомпании.Пользователь = &Пользователь
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Обращение.Ссылка) КАК Количество
		|ИЗ
		|	Документ.Обращение КАК Обращение
		|ГДЕ
		|	НЕ Обращение.ПометкаУдаления
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПользователиКонтактнаяИнформация.Вид КАК Вид,
		|	ПользователиКонтактнаяИнформация.Тип КАК Тип,
		|	ПользователиКонтактнаяИнформация.Представление КАК Представление
		|ИЗ
		|	Справочник.Пользователи.КонтактнаяИнформация КАК ПользователиКонтактнаяИнформация
		|ГДЕ
		|	ПользователиКонтактнаяИнформация.Ссылка = &Пользователь";
	
	Запрос.УстановитьПараметр("Пользователь", ПараметрыСеанса.ТекущийПользователь);
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
		
	ВыборкаДетальныеЗаписи = РезультатЗапроса[0].Выбрать();
	
	Абонент = "";
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Абонент = ?(ЗначениеЗаполнено(Абонент), Абонент + ", " + ВыборкаДетальныеЗаписи.АбонентПредставление, ВыборкаДетальныеЗаписи.АбонентПредставление);
	КонецЦикла;
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса[1].Выбрать();
	
	КоличествоОбращений = 0;
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		КоличествоОбращений = ВыборкаДетальныеЗаписи.Количество;
	КонецЕсли;

	ВыборкаДетальныеЗаписи = РезультатЗапроса[2].Выбрать();
	
	СтрокаКИ = "";
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		СтрокаКИ = СтрокаКИ + Строка(ВыборкаДетальныеЗаписи.Вид) + ": " + Строка(ВыборкаДетальныеЗаписи.Представление) + "<br>"
	КонецЦикла;
	
	Если ЗначениеЗаполнено(СтрокаКИ) Тогда	
		КонтактнаяИнформация = СтрокаКИ;	
	Иначе	
		КонтактнаяИнформация = НСтр("ru = 'не указана.'");	
	КонецЕсли;
		
	ПриветственнаяИнформация = Обработки.СамообслуживаниеПартнеров.ПолучитьМакет("ГлавнаяСтраница").ПолучитьТекст();

	Обработки.СамообслуживаниеПартнеров.УстановитьЦветФонаИнформацииНTML(ПриветственнаяИнформация);

	ПриветственнаяИнформация = СтрЗаменить(ПриветственнаяИнформация,"#ПолноеНаименованиеПартнера#", Строка(ПараметрыСеанса.ТекущийПользователь));
	
	ПриветственнаяИнформация = СтрЗаменить(ПриветственнаяИнформация,"#НаименованиеАбонентов#", Абонент);

	ПриветственнаяИнформация = СтрЗаменить(ПриветственнаяИнформация,"#КоличествоОбращений#", КоличествоОбращений);
	
	ПриветственнаяИнформация = СтрЗаменить(ПриветственнаяИнформация,"#КонтактнаяИнформация#", КонтактнаяИнформация);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьСписокОбращений(Команда)
	
	ПараметрыФормы = Новый Структура;

	ОткрытьФорму("Документ.Обращение.Форма.ФормаСпискаКлиентаКомпании", ПараметрыФормы, ЭтаФорма);
		
КонецПроцедуры

&НаКлиенте
Процедура ПриветственнаяИнформацияПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если СтрЧислоВхождений(ДанныеСобытия.Href, "incident") > 0 Тогда
		ОткрытьФорму("Документ.Обращение.Форма.ФормаСпискаКлиентаКомпании",
		,
		ПолучитьОкна()[0],
		,
		ОкноПриложения());
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ОкноПриложения()
	
	Возврат ?(ТекущийВариантИнтерфейсаКлиентскогоПриложения() = ВариантИнтерфейсаКлиентскогоПриложения.Версия8_2, ПолучитьОкна()[0], Неопределено);
	
КонецФункции
