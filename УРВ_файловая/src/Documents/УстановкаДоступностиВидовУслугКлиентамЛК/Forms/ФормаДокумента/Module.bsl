
&НаКлиенте
Процедура ДобавитьВидыУслугКонтрагента(Команда)
	ОписаниеОповещения = Новый ОписаниеОповещения("ДобавитьВидыУслугКонтрагентаЗавершение", ЭтаФорма);
	ОткрытьФорму("Справочник.Абоненты.Форма.ФормаВыбораУправляемая", , ЭтаФорма,,,, ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьВидыУслугКонтрагентаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		ВозВрат;
	КонецЕсли;
	Если ТипЗнч(Результат) <> Тип("СправочникСсылка.Абоненты") Тогда
		ВозВрат;
	КонецЕсли;
	Если Результат.ЭтоГруппа Тогда // мало ли
		ВозВрат;
	КонецЕсли;
	
	ДобавитьВидыУслугКонтрагентаНаСервере(Результат);

КонецПроцедуры

&НаСервере
Процедура ДобавитьВидыУслугКонтрагентаНаСервере(Контрагент)
	
	ТекущаяДата = ТекущаяДата();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Ограничения.Контрагент,
	|	Ограничения.ВидУслуги,
	|	Ограничения.ДатаНачала,
	|	Ограничения.ДатаОкончания,
	|	Ограничения.Отменить,
	|	Ограничения.НомерСтроки
	|ПОМЕСТИТЬ ВТ_Ограничения
	|ИЗ
	|	&Ограничения КАК Ограничения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДоступностьВидовУслугКлиентамЛКСрезПоследних.Контрагент,
	|	ДоступностьВидовУслугКлиентамЛКСрезПоследних.ВидУслуги,
	|	ДоступностьВидовУслугКлиентамЛКСрезПоследних.ДатаНачала,
	|	ДоступностьВидовУслугКлиентамЛКСрезПоследних.ДатаОкончания
	|ПОМЕСТИТЬ ВТ_ОграниченияКонтрагента
	|ИЗ
	|	РегистрСведений.ДоступностьВидовУслугКлиентамЛК.СрезПоследних(
	|			&МоментВремени,
	|			Контрагент = &Контрагент
	|				И (ДатаОкончания >= &ДатаАктуальности
	|					ИЛИ ДатаОкончания = ДАТАВРЕМЯ(1, 1, 1))) КАК ДоступностьВидовУслугКлиентамЛКСрезПоследних
	|ГДЕ
	|	НЕ ДоступностьВидовУслугКлиентамЛКСрезПоследних.НеДействует
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Ограничения.Контрагент,
	|	ВТ_Ограничения.ВидУслуги,
	|	ВТ_Ограничения.ДатаНачала,
	|	ВТ_Ограничения.ДатаОкончания,
	|	ВТ_Ограничения.Отменить,
	|	ВТ_Ограничения.НомерСтроки КАК НомерСтроки
	|ИЗ
	|	ВТ_Ограничения КАК ВТ_Ограничения
	|ГДЕ
	|	ВТ_Ограничения.Контрагент <> &Контрагент
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТ_ОграниченияКонтрагента.Контрагент,
	|	ВТ_ОграниченияКонтрагента.ВидУслуги,
	|	ВТ_ОграниченияКонтрагента.ДатаНачала,
	|	ВТ_ОграниченияКонтрагента.ДатаОкончания,
	|	ЛОЖЬ,
	|	999999
	|ИЗ
	|	ВТ_ОграниченияКонтрагента КАК ВТ_ОграниченияКонтрагента
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	
	Если Не ЗначениеЗаполнено(Объект.Дата) Тогда
		МоментВремени = Неопределено;
	ИначеЕсли Не ЗначениеЗаполнено(Объект.Ссылка) И НачалоДня(Объект.Дата) = НачалоДня(ТекущаяДата) Тогда
		МоментВремени = Неопределено;
	Иначе
		МоментВремени = Новый МоментВремени(Объект.Дата, Объект.Ссылка);
	КонецЕсли;
	
	ДатаАктуальности = НачалоДня(Объект.Дата);
	
	Запрос.УстановитьПараметр("ДатаАктуальности"		, ДатаАктуальности);
	Запрос.УстановитьПараметр("Контрагент"				, Контрагент);
	Запрос.УстановитьПараметр("Ограничения"				, Объект.Ограничения.Выгрузить());
	Запрос.УстановитьПараметр("МоментВремени"			, МоментВремени);
	
	РезультатЗапроса = Запрос.Выполнить();
	Объект.Ограничения.Загрузить(РезультатЗапроса.Выгрузить());
	
КонецПроцедуры

//--------------------------------------------------------------------------------------------------
#Область КомандаМножественнаяУстановка


&НаКлиенте
Процедура КомандаМассоваяУстановка(Команда)
	
	ОткрытьФорму(
		"Документ.УстановкаДоступностиВидовУслугКлиентамЛК.Форма.ФормаМассоваяУстановка", , 
		ЭтаФорма,,,, 
		Новый ОписаниеОповещения("КомандаМножественнаяУстановка_Завершение", ЭтаФорма), 
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаМножественнаяУстановка_Завершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;

	Для каждого Контрагент Из Результат.Контрагенты Цикл
		Для каждого ВидУслуги Из Результат.ВидыУслуг Цикл
			строкаТЧ = Объект.Ограничения.Добавить();
			ЗаполнитьЗначенияСвойств(строкаТЧ, Результат, "ДатаНачала,ДатаОкончания,Отменить");
			строкаТЧ.Контрагент	= Контрагент;
			строкаТЧ.ВидУслуги	= ВидУслуги;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
