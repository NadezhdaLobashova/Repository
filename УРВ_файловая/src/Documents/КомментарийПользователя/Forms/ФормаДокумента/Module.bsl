
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Если Объект.Ссылка.Пустая() Тогда
		Взаимодействия.УстановитьПредметПоДаннымЗаполнения(Параметры, Предмет);
        Рассмотрено = Истина;
		ЗаполнитьПоПереданнымПараметрам();
    КонецЕсли;
    
	Если ЗначениеЗаполнено(Объект.Дата) Тогда
		МестнаяДата = МестноеВремя(Объект.Дата);
	КонецЕсли;
    
    Взаимодействия.ЗаполнитьСписокВыбораДляРассмотретьПосле(Элементы.РассмотретьПосле.СписокВыбора);
    
    Если Объект.Ссылка.Пустая() Тогда
		ПриСозданииИПриЧтенииНаСервере();
	КонецЕсли;
	
	ТекущийПользователь = Пользователи.АвторизованныйПользователь();
	
	// Определим типы контактов, которые можно создать.
	СписокИнтерактивноСоздаваемыхКонтактов = Взаимодействия.СоздатьСписокЗначенийИнтерактивноСоздаваемыхКонтактов();

	// Подготовить оповещения взаимодействий.
	Взаимодействия.ПодготовитьОповещения(ЭтотОбъект,Параметры);
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, Новый Структура("ИмяЭлементаДляРазмещения", "СтраницаДополнительныеРеквизиты"));
	// Конец СтандартныеПодсистемы.Свойства
	
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ВерсионированиеОбъектов") Тогда
		МодульВерсионированиеОбъектов = ОбщегоНазначения.ОбщийМодуль("ВерсионированиеОбъектов");
		МодульВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	КонецЕсли;
	
	ВзаимодействияКлиентСервер.ПроверитьЗаполнениеКонтактов(Объект, ЭтотОбъект, "КомментарийПользователя");
    Элементы.Тема.Видимость = (ТипЗнч(Предмет) = Тип("ДокументСсылка.Обращение"));
	
    Если ТипЗнч("Предмет") = Тип("ДокументСсылка.Обращение") Тогда
        Рассмотрено = Истина;
    КонецЕсли;
    
    Элементы.ФормаСоздатьНовоеОбращение.Видимость = (ТипЗнч(Предмет) = Тип("ДокументСсылка.Обращение") И Объект.Входящий);
	Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("%1 от %2", Объект.Ссылка.Метаданные().Синоним, МестнаяДата); 
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	ПриСозданииИПриЧтенииНаСервере();
	
	РедактируемыйОбъект = РеквизитФормыВЗначение("Объект");
	ОбщегоНазначенияУСП.УстановитьФорматированноеОписаниеИзХранилища(Описание, РедактируемыйОбъект.ОписаниеХранилище);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
    
    ПодключитьОбработчикОжидания("УстановитьПризнакРассмотрено", 0.1, Истина);
    
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, РежимЗаписи, РежимПроведения)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	ОбщегоНазначенияУСП.ПоместитьФорматированноеОписаниеВХранилище(Описание, ТекущийОбъект.ОписаниеХранилище);
	ТекущийОбъект.Описание = Описание.ПолучитьТекст();
    ТекущийОбъект.ДополнительныеСвойства.Вставить("Предмет", Предмет);
    
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Взаимодействия.ПриЗаписиВзаимодействияИзФормы(ТекущийОбъект, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)

	ВзаимодействияКлиент.ВзаимодействиеПредметПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи, "КомментарийПользователя");
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)

	// СтандартныеПодсистемы.Свойства
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства
	
	ВзаимодействияКлиент.ОтработатьОповещение(ЭтотОбъект,ИмяСобытия, Параметр, Источник);
	ВзаимодействияКлиентСервер.ПроверитьЗаполнениеКонтактов(Объект, ЭтотОбъект, "КомментарийПользователя");
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПредметПриИзменении(Элемент)
    
    ПредметПриИзмененииНаСервере();
    
КонецПроцедуры

&НаКлиенте
Процедура РассмотретьПослеОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ВзаимодействияКлиент.ОбработатьВыборВПолеРассмотретьПосле(РассмотретьПосле, 
		ВыбранноеЗначение, СтандартнаяОбработка, Модифицированность);
	
КонецПроцедуры

&НаКлиенте
Процедура РассмотреноПриИзменении(Элемент)
	
	Элементы.РассмотретьПосле.Доступность = НЕ Рассмотрено;
	
КонецПроцедуры

&НаКлиенте
Процедура ОписаниеПриИзменении(Элемент)
	
	Объект.ОписаниеДатаИзменения = ТекущаяДата();
	Объект.ОписаниеАвторИзменения = ТекущийПользователь;
    Если Не ЗначениеЗаполнено(Объект.Тема) Тогда
        Объект.Тема = Описание.ПолучитьТекст();
    КонецЕсли;
    
КонецПроцедуры

&НаКлиенте
Процедура СервисПриИзменении(Элемент)
    СервисПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ВитринаПриИзменении(Элемент)
    ВитринаПриИзмененииНаСервере();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СоздатьНовоеОбращение(Команда)
    
    НовоеОбращение = НовоеОбращениеНаСервере();
    Если ЗначениеЗаполнено(НовоеОбращение) Тогда
        ПоказатьЗначение(, НовоеОбращение);
        Закрыть();
    КонецЕсли;
    
КонецПроцедуры

&НаКлиенте
Процедура СвязанныеВзаимодействияВыполнить(Команда)

	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("Предмет", Объект.Предмет);

	ОткрытьФорму("ЖурналДокументов.Взаимодействия.ФормаСписка", ПараметрыОтбора, ЭтотОбъект, , Окно);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура Подключаемый_СвойстваВыполнитьКоманду(ЭлементИлиКоманда, НавигационнаяСсылка = Неопределено, СтандартнаяОбработка = Неопределено)

	УправлениеСвойствамиКлиент.ВыполнитьКоманду(ЭтотОбъект, ЭлементИлиКоманда, СтандартнаяОбработка);

КонецПроцедуры

&НаКлиенте
Процедура ВопросОЗаписиФайлаПослеЗакрытия(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		Записать();
	Иначе
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПоПереданнымПараметрам()
	
	Если Параметры.Свойство("ЗначенияЗаполнения") Тогда
		Если Параметры.ЗначенияЗаполнения.Свойство("Тема") Тогда
			Объект.Тема = Параметры.ЗначенияЗаполнения.Тема;
		КонецЕсли; 
    КонецЕсли;
    
    УстановитьПринадлежностьОбъектаПоПредмету();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПринадлежностьОбъектаПоПредмету()
    
    Если ТипЗнч(Предмет) = Тип("ДокументСсылка.Обращение") Тогда
        ПринадлежностьОбращения = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Предмет, "Сервис, Витрина");
        Объект.Сервис = ПринадлежностьОбращения.Сервис;
        Объект.Витрина = ПринадлежностьОбращения.Витрина;
    ИначеЕсли ТипЗнч(Предмет) = Тип("СправочникСсылка.Пожелания") Тогда
        ПринадлежностьПожелания = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Предмет, "Сервисы");
        Если ПринадлежностьПожелания.Сервисы <> Неопределено Тогда
            ВыборкаСервисы = ПринадлежностьПожелания.Сервисы.Выбрать();
            Если ВыборкаСервисы.Следующий() Тогда
                Объект.Сервис = ВыборкаСервисы.Сервис;
                Объект.Витрина = ВыборкаСервисы.Витрина;
            КонецЕсли; 
        КонецЕсли;
    КонецЕсли; 
	
КонецПроцедуры
 
&НаСервере
Процедура ПриСозданииИПриЧтенииНаСервере()
	
	Если Не Объект.Ссылка.Пустая() Тогда
		Взаимодействия.УстановитьРеквизитыФормыВзаимодействияПоДаннымРегистра(ЭтотОбъект);
	Иначе
		ИзменилисьКонтакты = Истина;
	КонецЕсли;
	Элементы.РассмотретьПосле.Доступность = НЕ Рассмотрено;
	
КонецПроцедуры

&НаСервере
Процедура ПредметПриИзмененииНаСервере()
	
    Элементы.Тема.Видимость = (ТипЗнч(Предмет) = Тип("ДокументСсылка.Обращение"));
    УстановитьПринадлежностьОбъектаПоПредмету();
	
КонецПроцедуры
 
// СтандартныеПодсистемы.Свойства
&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
	
	УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтотОбъект);
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

&НаСервере
Процедура СервисПриИзмененииНаСервере()
	
    Если БазаЗнанийПовтИсп.СервисВитрины(Объект.Витрина) <> Объект.Сервис Тогда
        Объект.Витрина = БазаЗнанийПовтИсп.ОсновнаяВитринаСервиса(Объект.Сервис);
    КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ВитринаПриИзмененииНаСервере()
    
    Объект.Сервис = БазаЗнанийПовтИсп.СервисВитрины(Объект.Витрина);
    
КонецПроцедуры

&НаСервере
Функция НовоеОбращениеНаСервере()
    
    НовоеОбращение = Документы.Обращение.СоздатьДокумент();
    НовоеОбращение.Дата = ТекущаяУниверсальнаяДата();
    СвойстваПредмета = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Предмет, "Сервис, Витрина");
    НовоеОбращение.Сервис = СвойстваПредмета.Сервис;
    НовоеОбращение.Витрина = СвойстваПредмета.Витрина;
    НовоеОбращение.Инициатор = Объект.Автор;
    КонтактыПользователя = КонтактнаяИнформацияУСП.АктуальныеАдресаОтправкиПользователяСервиса(Объект.Автор);
    Если КонтактыПользователя.Количество() > 0 Тогда
        НовоеОбращение.АдресДляПереписки = КонтактыПользователя[0].Адрес;
    КонецЕсли;
    НовоеОбращение.Тема = Объект.Тема;
    НовоеОбращение.ОписаниеХранилище = Новый ХранилищеЗначения(Описание);
    НовоеОбращение.Абонент = Обслуживание.АбонентПользователяСервиса(Объект.Автор);
    НовоеОбращение.АбонентОбслуживающейОрганизации = Обслуживание.АбонентОбслуживающейОрганизацииАбонента(НовоеОбращение.Абонент);
    НовоеОбращение.Исполнитель = Пользователи.АвторизованныйПользователь();
    Сведения = РегистрыСведений.СведенияОПользователях.СведенияОПользователе();
    НовоеОбращение.ЛинияПоддержки = Сведения.ЛинияПоддержки; 
    НовоеОбращение.ОбслуживающаяОрганизация = Сведения.ОбслуживающаяОрганизация; 
    НовоеОбращение.КаналПолучения = Справочники.КаналыПолученияОбращений.ИнформационныйЦентр;
    НовоеОбращение.Записать(РежимЗаписиДокумента.Проведение);
    Взаимодействия.УстановитьПредмет(Объект.Ссылка, НовоеОбращение.Ссылка, Истина);
    Предмет = НовоеОбращение.Ссылка;
    
    Возврат НовоеОбращение.Ссылка;
    
КонецФункции

&НаКлиенте
Процедура УстановитьПризнакРассмотрено()
	
    Если ЗначениеЗаполнено(Объект.Ссылка) И Не Рассмотрено Тогда
        УстановитьПризнакРассмотреноНаСервере();    
        ОповеститьОбИзменении(Объект.Ссылка);
    КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПризнакРассмотреноНаСервере()
    
    Объекты = Новый Массив;
    Объекты.Добавить(Объект.Ссылка);
    Взаимодействия.УстановитьПризнакРассмотрено(Объекты, Истина, Истина);
    Рассмотрено = Истина;
	
КонецПроцедуры

#КонецОбласти
