#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ДополнительныеСвойства.Вставить("СтруктураЗаписи", СтруктураЗаписи());
	
	Если НЕ Взаимодействия.РассчитыватьРассмотрено(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	СтараяЗапись     = ДополнительныеСвойства.СтруктураЗаписи;
	НоваяЗапись      = СтруктураЗаписи();
	ДанныеДляРасчета = Новый Структура("НоваяЗапись, СтараяЗапись", НоваяЗапись, СтараяЗапись);
	
	// Обслуживание
	СтарыйПредмет = Неопределено;
	Если СтараяЗапись.Свойство("Предмет", СтарыйПредмет) Тогда 
		НовыйПредмет = НоваяЗапись.Предмет;
        Если ТипЗнч(НовыйПредмет) = Тип("ДокументСсылка.Обращение") Тогда
		    РегистрыСведений.СостоянияОбращений.ДобавитьЗапись(НовыйПредмет, СтарыйПредмет);
        КонецЕсли;
	КонецЕсли;
	
	Если ЭтотОбъект.Количество() > 0 Тогда
		Взаимодействие = ЭтотОбъект.Получить(0).Взаимодействие;
		Если ТипЗнч(НоваяЗапись.Предмет) = Тип("ДокументСсылка.Обращение") Тогда 
			
			Если ТипЗнч(СтарыйПредмет) = Тип("ДокументСсылка.Обращение") И СтарыйПредмет <> НовыйПредмет Тогда
				СтарыйИнициатор = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтарыйПредмет, "Инициатор");
				ВзаимодействияУСП.УдалитьПризнакНеПросмотренности(Взаимодействие, СтарыйИнициатор);
			КонецЕсли;
			
		КонецЕсли;
	КонецЕсли;
	// Конец Обслуживание
	
	Если НЕ Взаимодействия.РассчитыватьРассмотрено(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;
	
	Если НоваяЗапись.Рассмотрено <> СтараяЗапись.Рассмотрено Тогда
		
		Взаимодействия.РассчитатьРассмотреноПоПапкам(Взаимодействия.ТаблицаДанныхДляРасчетаРассмотрено(ДанныеДляРасчета, "Папка"));
		Взаимодействия.РассчитатьРассмотреноПоПредметам(Взаимодействия.ТаблицаДанныхДляРасчетаРассмотрено(ДанныеДляРасчета, "Предмет"));
		
		Возврат;

	КонецЕсли;
	
	Если НоваяЗапись.Папка <> СтараяЗапись.Папка Тогда
		
		Взаимодействия.РассчитатьРассмотреноПоПапкам(Взаимодействия.ТаблицаДанныхДляРасчетаРассмотрено(ДанныеДляРасчета, "Папка"));
		
		Возврат;
		
	КонецЕсли;
	
	Если НоваяЗапись.Предмет <> СтараяЗапись.Предмет Тогда
		
		Взаимодействия.РассчитатьРассмотреноПоПредметам(Взаимодействия.ТаблицаДанныхДляРасчетаРассмотрено(ДанныеДляРасчета, "Предмет"));
		Возврат;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция СтруктураЗаписи()

	СтруктураВозврата = Новый Структура;
	СтруктураВозврата.Вставить("Предмет", Неопределено);
	СтруктураВозврата.Вставить("Папка", Справочники.ПапкиЭлектронныхПисем.ПустаяСсылка());
	СтруктураВозврата.Вставить("Рассмотрено", Неопределено);
	
	Если ЭтотОбъект.Отбор.Количество() = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ПредметыПапкиВзаимодействий.Предмет,
	|	ПредметыПапкиВзаимодействий.ПапкаЭлектронногоПисьма КАК Папка,
	|	ПредметыПапкиВзаимодействий.Рассмотрено
	|ИЗ
	|	РегистрСведений.ПредметыПапкиВзаимодействий КАК ПредметыПапкиВзаимодействий
	|ГДЕ
	|	ПредметыПапкиВзаимодействий.Взаимодействие = &Взаимодействие";
	
	Запрос.УстановитьПараметр("Взаимодействие", ЭтотОбъект.Отбор.Взаимодействие.Значение);
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат СтруктураВозврата;
	КонецЕсли;
	
	Выборка = Результат.Выбрать();
	Выборка.Следующий();
	ЗаполнитьЗначенияСвойств(СтруктураВозврата, Выборка);
	Возврат СтруктураВозврата;
	
КонецФункции

#КонецОбласти

#КонецЕсли
