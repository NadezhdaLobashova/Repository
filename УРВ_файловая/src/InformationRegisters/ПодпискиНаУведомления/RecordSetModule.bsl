#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриЗаписи(Отказ, Замещение) 
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Количество() = 1 Тогда
		
		ЭлементЗаписи = Получить(0);
		ВидСобытия = ЭлементЗаписи.ВидСобытия;
		ПодпискаАктивна = ЭлементЗаписи.ПодпискаАктивна;
		Если ТипЗнч(ВидСобытия) <> Тип("СправочникСсылка.ВидыБизнесСобытий")
			ИЛИ ТипЗнч(ПодпискаАктивна) <> Тип("Булево")
			ИЛИ ПодпискаАктивна <> Истина Тогда
			Возврат;
		КонецЕсли;
		
		БизнесСобытия.СохранитьПодпискуНаБизнесСобытия(ВидСобытия, 
			Перечисления.ПотребителиБизнесСобытий.РассылкаУведомлений);
		
	ИначеЕсли Количество() = 0 Тогда // удаление
		
		Если ТипЗнч(ЭтотОбъект.Отбор.ВидСобытия.Значение) <> Тип("СправочникСсылка.ВидыБизнесСобытий") Тогда
			Возврат;
		КонецЕсли;
		
		КоличествоПодписчиков = ПолучитьКоличествоПодписчиков(ЭтотОбъект.Отбор.ВидСобытия.Значение);
		
		Если КоличествоПодписчиков = 0 Тогда
			БизнесСобытия.УдалитьПодпискуНаБизнесСобытия(ЭтотОбъект.Отбор.ВидСобытия.Значение, 
				Перечисления.ПотребителиБизнесСобытий.РассылкаУведомлений);
		КонецЕсли;	
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьКоличествоПодписчиков(ВидСобытия)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ПодпискиНаУведомления.ВидСобытия КАК ВидСобытия
		|ИЗ
		|	РегистрСведений.ПодпискиНаУведомления КАК ПодпискиНаУведомления
		|ГДЕ
		|	ПодпискиНаУведомления.ВидСобытия = &ВидСобытия
		|	И ПодпискиНаУведомления.ПодпискаАктивна = ИСТИНА
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	НастройкиУведомленийПоУмолчанию.ВидСобытия
		|ИЗ
		|	РегистрСведений.НастройкиУведомленийПоУмолчанию КАК НастройкиУведомленийПоУмолчанию
		|ГДЕ
		|	НастройкиУведомленийПоУмолчанию.Настройка = ЗНАЧЕНИЕ(Перечисление.НастройкиУведомлений.Подписка)
		|	И НастройкиУведомленийПоУмолчанию.ВидСобытия = &ВидСобытия
		|	И НастройкиУведомленийПоУмолчанию.Значение = ИСТИНА";
	
	Запрос.УстановитьПараметр("ВидСобытия", ВидСобытия);
	
	Таблица = Запрос.Выполнить().Выгрузить();
	Возврат Таблица.Количество();
	
КонецФункции

#КонецОбласти

#КонецЕсли
