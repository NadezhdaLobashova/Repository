
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
////////////////////////////////////////////////////////////////////////////////////////
// БоР : Для работы в толстом клиенте модуль выполнять на сервере. 01.04.2015 16:37:54

// БоР : Получение массива сотрудников (пользователей), управление которыми доступно указанному пользователю, с учетом "уровней доступности" 27.06.2017 15:05:23
&НаСервере
Функция ПолучитьМассивДоступныхСотрудников(ДоступностьПодчиненныхПодразделенийЛК, ДоступностьСвоегоПодразделенияЛК, ТекущийПользователь = Неопределено) Экспорт
	
	Если ТекущийПользователь = Неопределено Тогда
		ТекущийПользователь = ПользователиКлиентСервер.ТекущийПользователь();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	
	#Область Запрос
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РолиСотрудниковЛК.Подразделение КАК Подразделение
	|ПОМЕСТИТЬ ВТПодразделения
	|ИЗ
	|	РегистрСведений.РолиСотрудниковЛК КАК РолиСотрудниковЛК
	|ГДЕ
	|	&Условие_Подразделения_Руководитель
	|	И РолиСотрудниковЛК.Сотрудник = &Сотрудник
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РолиСотрудниковЛК.Подразделение
	|ИЗ
	|	РегистрСведений.РолиСотрудниковЛК КАК РолиСотрудниковЛК
	|ГДЕ
	|	&Условие_Подразделения_Координатор
	|	И РолиСотрудниковЛК.Сотрудник = &Сотрудник
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РолиСотрудниковЛК.Подразделение
	|ИЗ
	|	РегистрСведений.РолиСотрудниковЛК КАК РолиСотрудниковЛК
	|ГДЕ
	|	&Условие_Подразделения_Сотрудник
	|	И РолиСотрудниковЛК.Сотрудник = &Сотрудник
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Подразделение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РолиСотрудниковЛК.Сотрудник
	|ИЗ
	|	ВТПодразделения КАК ВТПодразделения
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.РолиСотрудниковЛК КАК РолиСотрудниковЛК
	|		ПО ВТПодразделения.Подразделение = РолиСотрудниковЛК.Подразделение
	|ГДЕ
	|	РолиСотрудниковЛК.Роль = ЗНАЧЕНИЕ(Перечисление.РолиСотрудниковЛК.Сотрудник)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	&Сотрудник";
	#КонецОбласти
	
	Если ДоступностьПодчиненныхПодразделенийЛК = Истина Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Условие_Подразделения_Руководитель"	, "РолиСотрудниковЛК.Роль = ЗНАЧЕНИЕ(Перечисление.РолиСотрудниковЛК.Руководитель)");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Условие_Подразделения_Координатор"	, "РолиСотрудниковЛК.Роль = ЗНАЧЕНИЕ(Перечисление.РолиСотрудниковЛК.Координатор)");
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Условие_Подразделения_Руководитель"	, "ЛОЖЬ");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Условие_Подразделения_Координатор"	, "ЛОЖЬ");
	КонецЕсли;
	
	Если ДоступностьСвоегоПодразделенияЛК = Истина Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Условие_Подразделения_Сотрудник"			, "РолиСотрудниковЛК.Роль = ЗНАЧЕНИЕ(Перечисление.РолиСотрудниковЛК.Сотрудник)");
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Условие_Подразделения_Сотрудник"		, "ЛОЖЬ");
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Сотрудник", ТекущийПользователь);
	РезультатЗапроса = Запрос.Выполнить();
	МассивСотрудников = РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Сотрудник");
	ВозВрат МассивСотрудников;
	
КонецФункции

&НаСервере
Функция ПолучитьДоступныеДействия(ТекущийПользователь = Неопределено) Экспорт
	
	Если ТекущийПользователь = Неопределено Тогда
		ТекущийПользователь = ПользователиКлиентСервер.ТекущийПользователь();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	
	#Область Запрос
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВЫБОР
	|		КОГДА РС.Роль <> ЗНАЧЕНИЕ(Перечисление.РолиСотрудниковЛК.Сотрудник)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ДоступныВсеДействия,
	|	МАКСИМУМ(РС.ДоступноСоздание) КАК ДоступноСоздание,
	|	МАКСИМУМ(РС.ДоступноИзменение) КАК ДоступноИзменение,
	|	МАКСИМУМ(РС.ДоступноПерепланирование) КАК ДоступноПерепланирование,
	|	МАКСИМУМ(РС.ДоступноБлокировка) КАК ДоступноБлокировка,
	|	МАКСИМУМ(РС.ДоступноОтменаБлокировки) КАК ДоступноОтменаБлокировки
	//Лобашова 30.05.2019 87280 +
	|   ,
	|   МАКСИМУМ(РС.ДоступноПросмотрДругихСотрудников) КАК ДоступноПросмотрДругихСотрудников
	//Лобашова 30.05.2019 87280 -
	|ПОМЕСТИТЬ ВТ_РС
	|ИЗ
	|	РегистрСведений.РолиСотрудниковЛК КАК РС
	|ГДЕ
	|	РС.Сотрудник = &Сотрудник
	|
	|СГРУППИРОВАТЬ ПО
	|	ВЫБОР
	|		КОГДА РС.Роль <> ЗНАЧЕНИЕ(Перечисление.РолиСотрудниковЛК.Сотрудник)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ,
	|	РС.Сотрудник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТ_РС.ДоступныВсеДействия,
	|	ИСТИНА КАК ДоступноСоздание,
	|	ИСТИНА КАК ДоступноИзменение,
	|	ИСТИНА КАК ДоступноПерепланирование,
	|	ИСТИНА КАК ДоступноБлокировка,
	|	ИСТИНА КАК ДоступноОтменаБлокировки
	//Лобашова 30.05.2019 87280 +
	|   ,
	|   ИСТИНА КАК ДоступноПросмотрДругихСотрудников
	//Лобашова 30.05.2019 87280 -
	|ИЗ
	|	ВТ_РС КАК ВТ_РС
	|ГДЕ
	|	ВТ_РС.ДоступныВсеДействия
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТ_РС.ДоступныВсеДействия,
	|	ВТ_РС.ДоступноСоздание,
	|	ВТ_РС.ДоступноИзменение,
	|	ВТ_РС.ДоступноПерепланирование,
	|	ВТ_РС.ДоступноБлокировка,
	|	ВТ_РС.ДоступноОтменаБлокировки
	//Лобашова 30.05.2019 87280 +
	|   ,
	|   ВТ_РС.ДоступноПросмотрДругихСотрудников
	//Лобашова 30.05.2019 87280 -
	|ИЗ
	|	ВТ_РС КАК ВТ_РС
	|ГДЕ
	|	НЕ ВТ_РС.ДоступныВсеДействия";
	#КонецОбласти
	
	Запрос.УстановитьПараметр("Сотрудник", ТекущийПользователь);
	Выборка = Запрос.Выполнить().Выбрать();
	//Лобашова 30.05.2019 87280 +	
	//Результат = Новый Структура("ДоступныВсеДействия,ДоступноСоздание,ДоступноИзменение,ДоступноОтменаБлокировки,ДоступноБлокировка,ДоступноПерепланирование", 
	//				Ложь, Ложь, Ложь, Ложь, Ложь, Ложь);
	Результат = Новый Структура("ДоступныВсеДействия,ДоступноСоздание,ДоступноИзменение,ДоступноОтменаБлокировки,ДоступноБлокировка,ДоступноПерепланирование, ДоступноПросмотрДругихСотрудников", 
					Ложь, Ложь, Ложь, Ложь, Ложь, Ложь, Ложь);
	//Лобашова 30.05.2019 87280 -	
	Если Выборка.Следующий() Тогда // Берем из первой записи на случай если пользователь и сотрудник и координатор/руководитель 
		ЗаполнитьЗначенияСвойств(Результат, Выборка);
	КонецЕсли;
	
	ВозВрат Результат;
	
КонецФункции

#КонецЕсли                                                                      
