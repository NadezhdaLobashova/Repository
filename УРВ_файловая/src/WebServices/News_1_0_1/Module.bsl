
#Область ПрограммныйИнтерфейс

Функция ПолучитьНовости(ПрефиксСервиса, ИмяКонфигурации, ДатаНачала)

	УстановитьПривилегированныйРежим(Истина);
    
    Сервис = Справочники.Сервисы.НайтиПоРеквизиту("Префикс", ПрефиксСервиса);
    Если Не ЗначениеЗаполнено(Сервис) Тогда
        ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='Не найден сервис по полученному префиксу - %1'"), ПрефиксСервиса); 
    КонецЕсли; 
    Конфигурация = Справочники.Конфигурации.НайтиПоРеквизиту("Имя", ИмяКонфигурации,, Сервис);
    Если Не ЗначениеЗаполнено(Конфигурация) Тогда
        ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='Не найдена конфигурация по имени - %1'"), ИмяКонфигурации); 
    КонецЕсли;
    
    Запрос = Новый Запрос;
    Запрос.Текст = 
    	"ВЫБРАТЬ РАЗЛИЧНЫЕ
        |   Новости.Ссылка КАК Новость
        |ИЗ
        |   Справочник.Новости КАК Новости
        |       ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Новости.Компоненты КАК НовостиКомпоненты
        |       ПО Новости.Ссылка = НовостиКомпоненты.Ссылка
        |ГДЕ
        |   Новости.Публиковать
        |   И Новости.ДатаСоздания > &ДатаНачала
        |   И Новости.Сервисы.Сервис = &Сервис
        |   И (НовостиКомпоненты.Компонент.Конфигурация = &Конфигурация
        |           ИЛИ НовостиКомпоненты.Компонент ЕСТЬ NULL)";
    
    Запрос.УстановитьПараметр("ДатаНачала", ДатаНачала);
    Запрос.УстановитьПараметр("Сервис", Сервис);
    Запрос.УстановитьПараметр("Конфигурация", Конфигурация);
    
    Результат = Запрос.Выполнить();
    Выборка = Результат.Выбрать();
	СписокНовостейТип = ФабрикаXDTO.Тип(ПространствоИмен(), "NewsList");
    НовостьТип = ФабрикаXDTO.Тип(ПространствоИмен(), "News");
    СписокНовостей = ФабрикаXDTO.Создать(СписокНовостейТип);
    
    Пока Выборка.Следующий() Цикл
        Новость = Выборка.Новость;
        НовостьXDTO = ФабрикаXDTO.Создать(НовостьТип);
        НовостьXDTO.name = Новость.Наименование;
        НовостьXDTO.startDate = Новость.ДатаСоздания;
    	НовостьXDTO.endDate = Новость.ДатаАктуальности; 
    	НовостьXDTO.id = Строка(Новость.Объект.УникальныйИдентификатор());
    	НовостьXDTO.criticality = ?(Новость.Приоритет = 0, 1, Новость.Приоритет);
        НовостьXDTO.textHTML = ВнешнийИнтерфейс.ОбъектHTMLObjectИзФорматированногоДокумента(Новость.ТекстHTML.Получить(), ПространствоИмен());
        СписокНовостей.News.Добавить(НовостьXDTO);
    КонецЦикла;
    
    Возврат СписокНовостей;
    
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПространствоИмен()
	
    Возврат "http://www.1cfresh.com/sd/news/1.0";	
	
КонецФункции
 
#КонецОбласти 
