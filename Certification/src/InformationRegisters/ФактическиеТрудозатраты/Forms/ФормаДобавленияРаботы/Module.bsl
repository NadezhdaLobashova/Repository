
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("ДатаОтчета") И ЗначениеЗаполнено(Параметры.ДатаОтчета) Тогда  
		ДатаРаботы = Параметры.ДатаОтчета;
	Иначе	
		ДатаРаботы = ТекущаяДатаСеанса();
	КонецЕсли;	
	
	Если ЗначениеЗаполнено(Параметры.Источник) Тогда  
		Источник = Параметры.Источник;
	КонецЕсли;	
	
	Если Параметры.Свойство("Пользователь") И ЗначениеЗаполнено(Параметры.Пользователь) Тогда  
		Пользователь = Параметры.Пользователь;
	Иначе	
		Пользователь = ПользователиКлиентСервер.ТекущийПользователь();
	КонецЕсли;
	
	Если Параметры.Свойство("ДлительностьРаботы") Тогда 
		Длительность = Параметры.ДлительностьРаботы;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Источник) Тогда 
		ОписаниеРаботы = СтрШаблон(НСтр("ru = 'Работа над ""%1""'"), Источник);
	КонецЕсли;	
	
	ДлительностьРаботы = УчетВремениКлиентСервер.ЧислоВСтроку(Длительность);
	
	Элементы.ДлительностьРаботы.СписокВыбора.ЗагрузитьЗначения(МассивВыбораВремени());
	
	ПоказатьДобавить1 = Истина;
	ПоказатьДобавить2 = Ложь;
	ПоказатьДобавить3 = Ложь;
	ПоказатьДобавить5 = Истина;
	ПоказатьДобавить10 = Истина;
	ПоказатьДобавить15 = Ложь;
	ПоказатьДобавить20 = Ложь;
	ПоказатьДобавить25 = Ложь;
	ПоказатьДобавить30 = Истина;
	ПоказатьДобавить60 = Ложь;
	
	УстановитьВидимостьБыстрыхКнопок();
	
	//{Рарус kruser 2018.12.21 78092
	ВывестиРеквизитыНаФорму();
	
	ЭтаФорма.ВремяОкончания = ТекущаяДата();
	//}Рарус kruser 2018.12.21 78092

КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	//Лобашова 26.04.2019 85443 +
	Если Не ЗначениеЗаполнено(ЭтаФорма.Работа) Тогда
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Не заполнено поле ""Работа""'"),,
			"ВремяНачала",, 
			Отказ);	
	
	КонецЕсли;
	//Лобашова 26.04.2019 85443 -
	
	//{Рарус kruser 2018.12.21 78092
	Если ЭтаФорма.ВыборРежимаЗаполнения = "Ручное заполнение" И ЗначениеЗаполнено(ЭтаФорма.ВремяНачала) И ЗначениеЗаполнено(ЭтаФорма.ВремяОкончания) И ЭтаФорма.ВремяНачала > ЭтаФорма.ВремяОкончания Тогда 	
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Время окончания не может быть меньше времени начала.'"),,
			"ВремяОкончания",, 
			Отказ);
	ИначеЕсли ЭтаФорма.ВыборРежимаЗаполнения = "Ручное заполнение" И (Не ЗначениеЗаполнено(ЭтаФорма.ВремяНачала) Или Не ЗначениеЗаполнено(ЭтаФорма.ВремяОкончания)) Тогда
		
		Если Не ЗначениеЗаполнено(ЭтаФорма.ВремяНачала) Тогда
		
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				НСтр("ru = 'Не заполнено поле ""Временя начала""'"),,
				"ВремяНачала",, 
				Отказ);	
		
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(ЭтаФорма.ВремяОкончания) Тогда
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				НСтр("ru = 'Не заполнено поле ""Временя окончания""'"),,
				"ВремяОкончания",, 
				Отказ);	
			
		КонецЕсли;
			
	ИначеЕсли ЭтаФорма.ВыборРежимаЗаполнения = "Быстрое заполнение" Тогда
	//}Рарус kruser 2018.12.21 78092	
		Если Не ЗначениеЗаполнено(ДлительностьРаботы) Или ДлительностьРаботы = "00:00" Тогда 
		
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				НСтр("ru = 'Не заполнено поле ""Длительность""'"),,
				"ДлительностьРаботы",, 
				Отказ);
		КонецЕсли;
	//{Рарус kruser 2018.12.21 78092
	КонецЕсли;
	//}Рарус kruser 2018.12.21 78092
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	Интервалы = СтрРазделить("1,2,3,5,10,15,20,25,30,60",",");
	
	Для каждого Интервал Из Интервалы Цикл
		ЗначениеНастройки = Настройки.Получить(СтрШаблон("ПоказатьДобавить%1", Интервал));
		Если ЗначениеНастройки <> Неопределено Тогда
			ЭтаФорма[СтрШаблон("ПоказатьДобавить%1", Интервал)] = ЗначениеНастройки;
			Настройки.Удалить(СтрШаблон("ПоказатьДобавить%1", Интервал));
		КонецЕсли;
	КонецЦикла;
		
	УстановитьВидимостьБыстрыхКнопок();
	
КонецПроцедуры

//{Рарус kruser 2018.12.21 78092
&НаСервере
Процедура ВывестиРеквизитыНаФорму()
	
	ДобавляемыеРеквизиты = Новый Массив; 
	
	Массив = Новый Массив;
	Массив.Добавить(Тип("СправочникСсылка.Работы"));
	
	Реквизит = Новый РеквизитФормы("Работа", Новый ОписаниеТипов(Массив)); 
	ДобавляемыеРеквизиты.Добавить(Реквизит);
	
	Массив = Новый Массив;
	Массив.Добавить(Тип("Дата"));

	Реквизит = Новый РеквизитФормы("ВремяНачала", Новый ОписаниеТипов(Массив,,,,, Новый КвалификаторыДаты(ЧастиДаты.Время))); 
	ДобавляемыеРеквизиты.Добавить(Реквизит);

	Реквизит = Новый РеквизитФормы("ВремяОкончания", Новый ОписаниеТипов(Массив,,,,, Новый КвалификаторыДаты(ЧастиДаты.Время))); 
	ДобавляемыеРеквизиты.Добавить(Реквизит);
		
	Массив = Новый Массив;
	Массив.Добавить(Тип("Строка"));
	
	Реквизит = Новый РеквизитФормы("ВыборРежимаЗаполнения", Новый ОписаниеТипов(Массив)); 
	ДобавляемыеРеквизиты.Добавить(Реквизит);

    ИзменитьРеквизиты(ДобавляемыеРеквизиты); 
	
	МассивРежимовВыбора = Новый Массив;
	МассивРежимовВыбора.Добавить("Быстрое заполнение"); 
	МассивРежимовВыбора.Добавить("Ручное заполнение"); 
	
	НовыйЭлемент = Элементы.Вставить("ВыборРежимаЗаполнения", Тип("ПолеФормы"), ЭтаФорма, Элементы.Группа1);
	НовыйЭлемент.ПутьКДанным					= "ВыборРежимаЗаполнения";
	НовыйЭлемент.Вид							= ВидПоляФормы.ПолеПереключателя;
	НовыйЭлемент.ТолькоПросмотр					= Ложь;
	НовыйЭлемент.ПоложениеЗаголовка				= ПоложениеЗаголовкаЭлементаФормы.Нет;
	НовыйЭлемент.УстановитьДействие("ПриИзменении", "ВыборРежимаЗаполненияПриИзменении");

	НовыйЭлемент.СписокВыбора.ЗагрузитьЗначения(МассивРежимовВыбора);
	
	ГруппаВремяВидимость = Истина;
	
	Попытка
	
		ДополнительныеНастройки = ХранилищеНастроекДанныхФорм.Загрузить("РегистрСведений.ФактическиеТрудозатраты", "ВыборРежимаЗаполнения");
		
		Если ЗначениеЗаполнено(ДополнительныеНастройки) Тогда		
			ЭтаФорма.ВыборРежимаЗаполнения = ДополнительныеНастройки;
			ГруппаВремяВидимость = ДополнительныеНастройки = "Быстрое заполнение";
		Иначе
			ЭтаФорма.ВыборРежимаЗаполнения = НовыйЭлемент.СписокВыбора[0];
		КонецЕсли;
		
	Исключение
		ЭтаФорма.ВыборРежимаЗаполнения 	= НовыйЭлемент.СписокВыбора[0];
	КонецПопытки;
	
	Элементы.Группа1.Видимость = ГруппаВремяВидимость;
	
	НовыйЭлемент = Элементы.Вставить("ГруппаВремя", Тип("ГруппаФормы"), ЭтаФорма, Элементы.Группа1);
	НовыйЭлемент.Вид							= ВидГруппыФормы.ОбычнаяГруппа;
	НовыйЭлемент.ТолькоПросмотр					= Ложь;
	НовыйЭлемент.ОтображатьЗаголовок			= Ложь;
	НовыйЭлемент.Отображение					= ОтображениеОбычнойГруппы.Нет;
	НовыйЭлемент.Группировка					= ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
	НовыйЭлемент.Видимость						= Не ГруппаВремяВидимость;
	НовыйЭлемент.ЦветФона                       = ЦветаСтиля.ФонУправляющегоПоля;
	
	НовыйЭлемент = Элементы.Вставить("ДекорацияГруппаВремя", Тип("ДекорацияФормы"), Элементы.ГруппаВремя);
	НовыйЭлемент.Вид							= ВидДекорацииФормы.Надпись;
	НовыйЭлемент.Заголовок						= "Укажите время:";
	НовыйЭлемент.Ширина 						= 12;
		
	НовыйЭлемент = Элементы.Вставить("ВремяНачала", Тип("ПолеФормы"), Элементы.ГруппаВремя);
	НовыйЭлемент.ПутьКДанным					= "ВремяНачала";
	НовыйЭлемент.Вид							= ВидПоляФормы.ПолеВвода;
	НовыйЭлемент.ТолькоПросмотр					= Ложь;
	НовыйЭлемент.ПоложениеЗаголовка				= ПоложениеЗаголовкаЭлементаФормы.Лево;
	НовыйЭлемент.Заголовок						= "Время начала";
	НовыйЭлемент.ФорматРедактирования			= "ДФ=HH:mm";
	
	НовыйЭлемент = Элементы.Вставить("ВремяОкончания", Тип("ПолеФормы"), Элементы.ГруппаВремя);
	НовыйЭлемент.ПутьКДанным					= "ВремяОкончания";
	НовыйЭлемент.Вид							= ВидПоляФормы.ПолеВвода;
	НовыйЭлемент.ТолькоПросмотр					= Ложь;
	НовыйЭлемент.ПоложениеЗаголовка				= ПоложениеЗаголовкаЭлементаФормы.Лево;
	НовыйЭлемент.Заголовок						= "Время окончания";
	НовыйЭлемент.ФорматРедактирования			= "ДФ=HH:mm";
	
	НовыйЭлемент = Элементы.Вставить("Работа", Тип("ПолеФормы"), ЭтаФорма, Элементы.ОписаниеРаботы);
	НовыйЭлемент.ПутьКДанным					= "Работа";
	НовыйЭлемент.Вид							= ВидПоляФормы.ПолеВвода;
	НовыйЭлемент.ТолькоПросмотр					= Ложь;
	НовыйЭлемент.ПоложениеЗаголовка				= ПоложениеЗаголовкаЭлементаФормы.Лево;
	НовыйЭлемент.Заголовок						= "Работа";
	НовыйЭлемент.КнопкаВыбора					= Истина;
	НовыйЭлемент.КнопкаОткрытия					= Истина;
	НовыйЭлемент.РежимВыбораИзСписка			= Истина;

	НовыйЭлемент.СписокВыбора.ЗагрузитьЗначения(УРВСервер.ЗаполнитьСписокВыборкаВидовРабот(Пользователь, ДатаРаботы));

КонецПроцедуры
//}Рарус kruser 2018.12.21 78092

//{Рарус kruser 2018.12.29 78092
&НаКлиенте
Процедура ВыборРежимаЗаполненияПриИзменении(Элемент)
	
	Если ЭтаФорма.ВыборРежимаЗаполнения = "Ручное заполнение" Тогда	
		Элементы.Группа1.Видимость 		= Ложь;
		Элементы.ГруппаВремя.Видимость 	= Истина;	
	Иначе	
		Элементы.Группа1.Видимость 		= Истина;
		Элементы.ГруппаВремя.Видимость 	= Ложь;	
	КонецЕсли;
	
	ВыборРежимаЗаполненияПриИзмененииНаСервере();
	
КонецПроцедуры
//}Рарус kruser 2018.12.29 78092

//{Рарус kruser 2018.12.29 78092
&НаСервере
Процедура ВыборРежимаЗаполненияПриИзмененииНаСервере()

	ХранилищеНастроекДанныхФорм.Сохранить("РегистрСведений.ФактическиеТрудозатраты", "ВыборРежимаЗаполнения", ЭтаФорма.ВыборРежимаЗаполнения);
		
КонецПроцедуры
//}Рарус kruser 2018.12.29 78092

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Добавить1(Команда)
	
	БыстроеДобавление(60);
	
КонецПроцедуры

&НаКлиенте
Процедура Добавить2(Команда)
	
	БыстроеДобавление(120);
	
КонецПроцедуры

&НаКлиенте
Процедура Добавить3(Команда)
	
	БыстроеДобавление(180);
	
КонецПроцедуры

&НаКлиенте
Процедура Добавить5(Команда)
	
	БыстроеДобавление(300);
	
КонецПроцедуры

&НаКлиенте
Процедура Добавить10(Команда)
	
	БыстроеДобавление(600);
	
КонецПроцедуры

&НаКлиенте
Процедура Добавить15(Команда)
	
	БыстроеДобавление(900);
	
КонецПроцедуры

&НаКлиенте
Процедура Добавить20(Команда)
	
	БыстроеДобавление(1200);
	
КонецПроцедуры

&НаКлиенте
Процедура Добавить25(Команда)
	
	БыстроеДобавление(1500);
	
КонецПроцедуры

&НаКлиенте
Процедура Добавить30(Команда)
	
	БыстроеДобавление(1800);
	
КонецПроцедуры

&НаКлиенте
Процедура Добавить60(Команда)
	
	БыстроеДобавление(3600);
	
КонецПроцедуры

&НаКлиенте
Процедура Добавить120(Команда)
	
	БыстроеДобавление(7200);
	
КонецПроцедуры

&НаКлиенте
Процедура Добавить180(Команда)
	
	БыстроеДобавление(10800);
	
КонецПроцедуры

&НаКлиенте
Процедура Добавить(Команда)
	
	ОчиститьСообщения();
	
	Если Не ПроверитьЗаполнение() Тогда 
		Возврат;
	КонецЕсли;
	
	//{Рарус kruser 2018.12.21 78092
	Если ЗначениеЗаполнено(ЭтаФорма.ВремяНачала) И ЗначениеЗаполнено(ЭтаФорма.ВремяОкончания) Тогда
		Длительность = ЭтаФорма.ВремяОкончания - ЭтаФорма.ВремяНачала; 
	Иначе 
	//}Рарус kruser 2018.12.21 78092
	Длительность = УчетВремениКлиентСервер.ЧислоИзСтроки(ДлительностьРаботы);
	//{Рарус kruser 2018.12.21 78092	
	КонецЕсли;
	//}Рарус kruser 2018.12.21 78092
	
	БыстроеДобавление(Длительность);
	
КонецПроцедуры

&НаКлиенте
Процедура НеДобавлять(Команда)
	
	Закрыть(Неопределено);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Функция МассивВыбораВремени()
	
	МассивВыбора = Новый Массив;
	
	МассивВыбора.Добавить("01:30");
	МассивВыбора.Добавить("02:00");
	МассивВыбора.Добавить("03:00");
	МассивВыбора.Добавить("04:00");
	МассивВыбора.Добавить("05:00");
	
	Возврат МассивВыбора;
	
КонецФункции

&НаСервере
Процедура УстановитьВидимостьБыстрыхКнопок()
	
	Интервалы = СтрРазделить("1,2,3,5,10,15,20,25,30,60",",");
	
	Если Элементы.ФормаНастроитьКнопки.Пометка Тогда
		Элементы.Группа3.Видимость = Истина;
		Для каждого Интервал Из Интервалы Цикл
			Элементы[СтрШаблон("ПоказатьДобавить%1", Интервал)].Видимость = Истина;
			Элементы[СтрШаблон("Добавить%1", Интервал)].Видимость = Истина;
		КонецЦикла; 
	Иначе
		Элементы.Группа3.Видимость = Ложь;
		
		Для каждого Интервал Из Интервалы Цикл
			Элементы[СтрШаблон("ПоказатьДобавить%1", Интервал)].Видимость = Ложь;
			Элементы[СтрШаблон("Добавить%1", Интервал)].Видимость = ЭтаФорма[СтрШаблон("ПоказатьДобавить%1", Интервал)];
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НастроитьКнопки(Команда)
	
	Элементы.ФормаНастроитьКнопки.Пометка = Не Элементы.ФормаНастроитьКнопки.Пометка;
	УстановитьВидимостьБыстрыхКнопок();
	
КонецПроцедуры

&НаКлиенте
Процедура БыстроеДобавление(Длительность)
	//Лобашова 20.05.2019 85443 +
	Если Не ЗначениеЗаполнено(ЭтаФорма.Работа) Тогда
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Не заполнено поле ""Работа""'"),,
			"ВремяНачала",,);	
	     Возврат;
	КонецЕсли;
	//Лобашова 20.05.2019 85443 -

	ПараметрыОтчета = Новый Структура();
	ПараметрыОтчета.Вставить("ДатаРаботы", ДатаРаботы);
	ПараметрыОтчета.Вставить("ОписаниеРаботы", ОписаниеРаботы);
	ПараметрыОтчета.Вставить("Длительность", Длительность);
	ПараметрыОтчета.Вставить("Источник", Источник);
	//{Рарус kruser 2018.12.21 78092		
	Если ЗначениеЗаполнено(ЭтаФорма.ВремяНачала) И ЗначениеЗаполнено(ЭтаФорма.ВремяОкончания) Тогда		
		ПараметрыОтчета.Вставить("ВремяНачала", 	ЭтаФорма.ВремяНачала);
		ПараметрыОтчета.Вставить("ВремяОкончания", 	НачалоМинуты(ЭтаФорма.ВремяОкончания));
	Иначе 
		ПараметрыОтчета.Вставить("ВремяНачала", 	Дата(1,1,1));
		ПараметрыОтчета.Вставить("ВремяОкончания",	Дата(1,1,1));		
	КонецЕсли;

	ПараметрыОтчета.Вставить("Работа", ЭтаФорма.Работа);
	//}Рарус kruser 2018.12.21 78092
	
	Закрыть(ПараметрыОтчета);
	
КонецПроцедуры

#КонецОбласти
