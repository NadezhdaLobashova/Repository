#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Добавляет новую запись в регистр
//
Процедура ДобавитьЗапись(НовоеОбращение, СтароеОбращение = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если НовоеОбращение <> Неопределено Тогда 
		Если ТипЗнч(НовоеОбращение) = Тип("ДокументСсылка.Обращение") Тогда 
			ДатаВзаимодействия = ДатаПоследнегоВзаимодействия(НовоеОбращение);
		Иначе
			ДатаВзаимодействия = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(НовоеОбращение, "Дата");
		КонецЕсли;
		
		Если ДатаВзаимодействия <> Неопределено Или ДатаВзаимодействия <> Дата(1, 1, 1) Тогда 
			НоваяЗапись = РегистрыСведений.СостоянияОбращений.СоздатьМенеджерЗаписи();
			НоваяЗапись.Обращение = НовоеОбращение;
			НоваяЗапись.ДатаПоследнегоВзаимодействия = ДатаВзаимодействия;
			НоваяЗапись.Записать();
		КонецЕсли;
	КонецЕсли;
	
	Если СтароеОбращение <> Неопределено Тогда 
		ДобавитьЗапись(СтароеОбращение);
	КонецЕсли;
	
КонецПроцедуры

// Возвращает дату последнего взаимодействия
//  по данным регистра сведений ПредметыПапкиВзаимодействий.
//
// Параметры:
//  Обращение - ДокументСсылка.Обращение - обращение, по которому нужно вернуть дату последнего взаимодействия.
// 
// Возвращаемое значение:
//  Дата - дата последнего взаимодействия.
//
Функция ДатаПоследнегоВзаимодействия(Обращение)
	
	Если Обращение.Пустая() Тогда 
		Возврат Дата(1, 1, 1);
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Обращение", Обращение);
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ПредметыПапкиВзаимодействий.Взаимодействие.Дата КАК ДатаВзаимодействия
		|ИЗ
		|	РегистрСведений.ПредметыПапкиВзаимодействий КАК ПредметыПапкиВзаимодействий
		|ГДЕ
		|	ПредметыПапкиВзаимодействий.Предмет = &Обращение
		|	И НЕ ПредметыПапкиВзаимодействий.НеПоказыватьПользователю
		|
		|УПОРЯДОЧИТЬ ПО
		|	ДатаВзаимодействия УБЫВ";
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл 
		Возврат Выборка.ДатаВзаимодействия;
	КонецЦикла;
	
	Возврат Обращение.Дата;
	
КонецФункции

#КонецОбласти

#КонецЕсли 