
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
    
    ОтображатьПревышение =  ПолучитьФункциональнуюОпцию("ИспользоватьСоглашения");
    Элементы.Превышение.Видимость = ОтображатьПревышение;
    Элементы.ПревышениеВРабочееВремя.Видимость = ОтображатьПревышение;

    Если Параметры.Отбор.Свойство("Обращение") Тогда
		Обращение = Параметры.Отбор.Обращение;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Обращение) Тогда
		АвтоЗаголовок = Ложь;
		Заголовок = НСтр("ru='Длительность обработки'"); 
		Элементы.Обращение.Видимость = Ложь;
	КонецЕсли;

    УстановитьУсловноеОформление();
    
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
    
	УсловноеОформление.Элементы.Очистить();
    
    ПоляДлительности = СтрРазделить("Длительность,ДлительностьВРабочееВремя,Превышение,ПревышениеВРабочееВремя", ",", Ложь);
    
    Для каждого ИмяПоля Из ПоляДлительности Цикл
    	Элемент = УсловноеОформление.Элементы.Добавить();
    	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
    	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(ИмяПоля);
    	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
    	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Список." + ИмяПоля);
    	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
    	ОтборЭлемента.ПравоеЗначение = '00010102';
    	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", "24:00:00");
    КонецЦикла; 
	
КонецПроцедуры
 
#КонецОбласти 