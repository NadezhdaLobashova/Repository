#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает структуру, содержащую сведения о пользователе.
// *Подразделение
// *Должность
Функция СведенияОПользователе(Пользователь = Неопределено) Экспорт
    
    Если Пользователь = Неопределено Тогда
        Пользователь = Пользователи.АвторизованныйПользователь();
    КонецЕсли;
    
	СведенияОПользователе = Новый Структура;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
        |   СведенияОПользователях.ОбслуживающаяОрганизация,
        |   СведенияОПользователях.ЛинияПоддержки,
        |   СведенияОПользователях.ПотребоватьСменуПароляПриВходе
        |ИЗ
        |   РегистрСведений.СведенияОПользователях КАК СведенияОПользователях
        |ГДЕ
        |   СведенияОПользователях.Пользователь = &Пользователь";

	Запрос.УстановитьПараметр("Пользователь", Пользователь);

	Результат = Запрос.Выполнить();

	ВыборкаДетальныеЗаписи = Результат.Выбрать();

	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		СведенияОПользователе.Вставить("ОбслуживающаяОрганизация", ВыборкаДетальныеЗаписи.ОбслуживающаяОрганизация);
		СведенияОПользователе.Вставить("ПотребоватьСменуПароляПриВходе", ВыборкаДетальныеЗаписи.ПотребоватьСменуПароляПриВходе);
		СведенияОПользователе.Вставить("ЛинияПоддержки", ВыборкаДетальныеЗаписи.ЛинияПоддержки);
	Иначе
		СведенияОПользователе.Вставить("ОбслуживающаяОрганизация", Справочники.ОбслуживающиеОрганизации.ПустаяСсылка());
		СведенияОПользователе.Вставить("ПотребоватьСменуПароляПриВходе", Ложь);
		СведенияОПользователе.Вставить("ЛинияПоддержки", Справочники.ЛинииПоддержки.ПустаяСсылка());
	КонецЕсли;

	Возврат СведенияОПользователе;
	
КонецФункции

// Возвращает массив пользователей Обслуживающей организации
//
Функция ПользователиОбслуживающейОрганизации(ОбслуживающаяОрганизация) Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СведенияОПользователях.Пользователь
		|ИЗ
		|	РегистрСведений.СведенияОПользователях КАК СведенияОПользователях
		|ГДЕ
		|	СведенияОПользователях.ОбслуживающаяОрганизация = &ОбслуживающаяОрганизация";
	
	Запрос.УстановитьПараметр("ОбслуживающаяОрганизация", ОбслуживающаяОрганизация );
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат Новый Массив;
	Иначе
		Возврат Результат.Выгрузить().ВыгрузитьКолонку("Пользователь");
	КонецЕсли; 
	
КонецФункции

// Возвращает массив пользователей Линии поддержки
//
Функция ПользователиЛинииПоддержки(ЛинияПоддержки) Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СведенияОПользователях.Пользователь
		|ИЗ
		|	РегистрСведений.СведенияОПользователях КАК СведенияОПользователях
		|ГДЕ
		|	СведенияОПользователях.ЛинияПоддержки = &ЛинияПоддержки";
	
	Запрос.УстановитьПараметр("ЛинияПоддержки", ЛинияПоддержки );
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат Новый Массив;
	Иначе
		Возврат Результат.Выгрузить().ВыгрузитьКолонку("Пользователь");
	КонецЕсли; 
	
КонецФункции

#КонецОбласти

#КонецЕсли 