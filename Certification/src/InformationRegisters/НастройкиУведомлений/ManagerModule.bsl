#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает настройку
Функция ПолучитьНастройку(Пользователь, Настройка, ВидСобытия) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	НастройкиУведомлений.Значение
		|ИЗ
		|	РегистрСведений.НастройкиУведомлений КАК НастройкиУведомлений
		|ГДЕ
		|	НастройкиУведомлений.Настройка = &Настройка
		|	И НастройкиУведомлений.ВидСобытия = &ВидСобытия
		|	И НастройкиУведомлений.Пользователь = &Пользователь";
	
	Запрос.УстановитьПараметр("ВидСобытия", ВидСобытия);
	Запрос.УстановитьПараметр("Настройка", Настройка);
	Запрос.УстановитьПараметр("Пользователь", Пользователь);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Значение;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

// Записывает настройку
Процедура УстановитьНастройку(Пользователь, Настройка, ВидСобытия, Значение) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запись = РегистрыСведений.НастройкиУведомлений.СоздатьМенеджерЗаписи();
	Запись.Пользователь = Пользователь;
	Запись.Настройка = Настройка;
	Запись.ВидСобытия = ВидСобытия;
	Запись.Значение = Значение;
	Запись.Записать(Истина);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
