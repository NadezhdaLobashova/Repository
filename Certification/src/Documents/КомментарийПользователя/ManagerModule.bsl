#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс


// Возвращает пользователя сервиса
// 
Функция ПолучитьКонтакты(Ссылка) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
    	"ВЫБРАТЬ
        |   КомментарийПользователя.Автор КАК Контакт,
        |   КомментарийПользователя.Автор.СтрокаEmail Адрес,
        |   КомментарийПользователя.Автор.Наименование Представление
        |ИЗ
        |   Документ.КомментарийПользователя КАК КомментарийПользователя
        |ГДЕ
        |   КомментарийПользователя.Ссылка = &Предмет
        |   И НЕ КомментарийПользователя.Автор = ЗНАЧЕНИЕ(Справочник.ПользователиСервисов.ПустаяСсылка)";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Взаимодействия.ПолучитьУчастникаПоПолям(Выборка.Контакт, Выборка.Адрес, Выборка.Представление);
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Групповое изменение объектов

// Возвращает список реквизитов, которые разрешается редактировать
// с помощью обработки группового изменения объектов.
//
Функция РеквизитыРедактируемыеВГрупповойОбработке() Экспорт
	
	Результат = Новый Массив;
	Результат.Добавить("Автор");
	Результат.Добавить("Важность");
	Результат.Добавить("Ответственный");
	Результат.Добавить("ВзаимодействиеОснование");
	Результат.Добавить("Входящий");
	Результат.Добавить("Комментарий");
	Результат.Добавить("АбонентКонтакт");
	Результат.Добавить("АбонентПредставление");
	Результат.Добавить("АбонентКакСвязаться");
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область ОбработчикиСобытий
    
Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	ВзаимодействияВызовСервера.ОбработкаПолученияДанныхВыбора(
		ДанныеВыбора,
		Параметры,
		СтандартнаяОбработка, 
		"КомментарийПользователя");
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьПризнакВходящийИПринадлежностьОбъектов_1_0_11_1(Параметры) Экспорт
    
    Запрос = Новый Запрос;
    
    Запрос.Текст = 
    	"ВЫБРАТЬ
        |   КомментарийПользователя.Ссылка,
        |   ВЫБОР
        |       КОГДА ПредметыПапкиВзаимодействий.Предмет ССЫЛКА Документ.Обращение
        |           ТОГДА ПредметыПапкиВзаимодействий.Предмет.Сервис
        |       ИНАЧЕ &ПустойСервис
        |   КОНЕЦ КАК Сервис,
        |   ВЫБОР
        |       КОГДА ПредметыПапкиВзаимодействий.Предмет ССЫЛКА Документ.Обращение
        |           ТОГДА ПредметыПапкиВзаимодействий.Предмет.Витрина
        |       ИНАЧЕ &ПустаяВитрина
        |   КОНЕЦ КАК Витрина,
        |   КомментарийПользователя.Автор
        |ИЗ
        |   Документ.КомментарийПользователя КАК КомментарийПользователя
        |       ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПредметыПапкиВзаимодействий КАК ПредметыПапкиВзаимодействий
        |       ПО КомментарийПользователя.Ссылка = ПредметыПапкиВзаимодействий.Взаимодействие
        |ГДЕ
        |   КомментарийПользователя.Сервис = &ПустойСервис";
        
    Запрос.УстановитьПараметр("ПустойСервис", Справочники.Сервисы.ПустаяСсылка());
    Запрос.УстановитьПараметр("ПустаяВитрина", Справочники.Витрины.ПустаяСсылка());
    
    Результат = Запрос.Выполнить();
    Выборка = Результат.Выбрать();
    
    Пока Выборка.Следующий() Цикл
        Объект = Выборка.Ссылка.ПолучитьОбъект();
        Объект.ОбменДанными.Загрузка = Истина;
        Объект.Входящий = Истина;
        Объект.Сервис = Выборка.Сервис;
        Объект.Витрина = Выборка.Витрина;
        Объект.АвторПредставление = Строка(Выборка.Автор);
        Объект.Записать();
    КонецЦикла;
    
КонецПроцедуры

#КонецОбласти 

#КонецЕсли

