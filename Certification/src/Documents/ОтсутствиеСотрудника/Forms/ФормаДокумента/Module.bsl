#Область ПРОЦЕДУРЫ_ОБРАБОТЧИКИ_СОБЫТИЙ_ФОРМЫ
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Объект.Ответственный = ПараметрыСеанса.ТекущийПользователь;
КонецПроцедуры
#КонецОбласти

#Область КОМАНДЫ_ФОРМЫ
&НаКлиенте
Процедура ЗаполнитьПоПериоду(Команда)
	Если Объект.ОтсутствияСотрудника.Количество()>0 Тогда
		Режим = РежимДиалогаВопрос.ДаНет;
		Оповещение = Новый ОписаниеОповещения("ЗаполнитьПоПериодуЗавершение", ЭтаФорма);
		ПоказатьВопрос(Оповещение, "Табличная часть будет очищена. Продолжить?", РежимДиалогаВопрос.ДаНет);
	Иначе
		ЗаполнитьПоПериодуОткрытьЗавершение();
	КонецЕсли;
КонецПроцедуры
#КонецОбласти

#Область СЛУЖЕБНЫЕ_ПРОЦЕДУРЫ_И_ФУНКЦИИ
&НаКлиенте
Процедура ЗаполнитьПоПериодуЗавершение(Результат, ДополнительныеПараметры = Неопределено) Экспорт
	Если Результат <> КодВозвратаДиалога.Да Тогда
		ВозВрат;
	КонецЕсли;
	ЗаполнитьПоПериодуОткрытьЗавершение();
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоПериодуОткрытьЗавершение()
	НовОповещение = Новый ОписаниеОповещения("ЗаполнитьПоПериодуОбработка",ЭтаФорма);
	ОткрытьФорму("Документ.ОтсутствиеСотрудника.Форма.ФормаЗаполнениеПоПериоду",,,,,,НовОповещение,РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс)	
КонецПроцедуры	

&НаКлиенте
Процедура ЗаполнитьПоПериодуОбработка(Результат, ДополнительныеПараметры) Экспорт 
	Если Результат = Неопределено Тогда 
		Возврат
	КонецЕсли;
	ЗаполнитьТабличнуюЧастьПоПериодуПоРезультату(Результат); 	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТабличнуюЧастьПоПериодуПоРезультату(СтруктураРезультат)
	Объект.ОтсутствияСотрудника.Очистить();
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	ПраздничныеВыходныеДни.Дата КАК Дата
	                      |ИЗ
	                      |	РегистрСведений.ПраздничныеВыходныеДни КАК ПраздничныеВыходныеДни
	                      |ГДЕ
	                      |	ПраздничныеВыходныеДни.Дата <= &ДатаОкончания
	                      |	И ПраздничныеВыходныеДни.Дата >= &ДатаНачала
	                      |	И ПраздничныеВыходныеДни.ВидДня = ЗНАЧЕНИЕ(Перечисление.ВидыДнейКалендаря.Рабочий)");
	Запрос.УстановитьПараметр("ДатаНачала",СтруктураРезультат.Период.ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания",СтруктураРезультат.Период.ДатаОкончания);
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		СтрокаОтсутствия               = Объект.ОтсутствияСотрудника.Добавить();
		СтрокаОтсутствия.ДатаНачала    = Выборка.Дата;
		СтрокаОтсутствия.ВидОтсутствия = СтруктураРезультат.ВидОтсутствия;
		СтрокаОтсутствия.Часы          = СтруктураРезультат.Часы;
	КонецЦикла;	
КонецПроцедуры	
#КонецОбласти















