#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

// Отправляет письмо о приглашении пользователя в сервис
//
// Параметры:
//	Приглашение - Структура, ДокументСсылка.ПриглашениеДляРегистрации - объект, по которому отправляется письмо.
//
Процедура ОтправитьПисьмоПоПриглашению(Приглашение) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если ОбщегоНазначения.ЭтоСсылка(ТипЗнч(Приглашение)) Тогда
			
		ДанныеСервиса = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Приглашение, 
			Новый Структура("Сервис, Витрина", "Сервис", "Сервис.ОсновнаяВитрина"));
		ДанныеКонтакта = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Приглашение, 
			Новый Структура("Адрес, Представление, Контакт","АдресЭлектроннойПочты", "ПолноеИмяПользователя", "Пользователь")); 
	Иначе
		ДанныеСервиса = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Приглашение.Сервис, 
			Новый Структура("Сервис, Витрина", "Ссылка", "ОсновнаяВитрина"));
			
		ДанныеКонтакта = Новый Структура;
		ДанныеКонтакта.Вставить("Адрес", Приглашение.АдресЭлектроннойПочты);
		ДанныеКонтакта.Вставить("Представление", Приглашение.ПолноеИмяПользователя);
		ДанныеКонтакта.Вставить("Контакт", Приглашение.Пользователь);
		
	КонецЕсли;
    
    УчетнаяЗапись = ВзаимодействияУСП.УчетнаяЗаписьПоддержки(ДанныеСервиса.Сервис, ДанныеСервиса.Витрина);
    
    Если Не ЗначениеЗаполнено(УчетнаяЗапись) Тогда
		ВызватьИсключение НСтр("ru='Не установлена учетная запись для отправки писем.'");
	КонецЕсли; 
    
    ШаблонПисьма = Справочники.ШаблоныТекстов.ШаблонПоТипу(
        Справочники.ТипыШаблоновТекстов.ПисьмоОтправкаПриглашенияНаРегистрацию,
        ДанныеСервиса.Сервис, ДанныеСервиса.Витрина);
	
	// тут нужно программно создать электронное письмо
	ПараметрыОтправителя = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(УчетнаяЗапись, "ИмяПользователя, АдресЭлектроннойПочты");
    ТаблицаКартинок = Новый ТаблицаЗначений; // Для сохранения вложений картинок
	Письмо = ВзаимодействияУСП.ЗаготовкаИсходящегоПисьма(ШаблонПисьма, Приглашение, УчетнаяЗапись, ТаблицаКартинок);
	Письмо.СтатусПисьма = Перечисления.СтатусыИсходящегоЭлектронногоПисьма.Исходящее;
	Письмо.УдалятьПослеОтправки = Истина;
    ВзаимодействияУСП.ДобавитьПолучателяПисьма(Письмо, ДанныеКонтакта);	
	Письмо.Записать();
    ВзаимодействияУСП.ДобавитьКартинкиВПисьмо(Письмо, ТаблицаКартинок);
    ВзаимодействияУСП.УстановитьПредметПисьма(Письмо, Приглашение);
    
    ВзаимодействияУСП.ОтправитьПисьмо(Письмо, СобытиеЖурналаРегистрацииОтправкаПисьмаИзПриглашенияНаРегистрацию());
	
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

Функция СобытиеЖурналаРегистрацииОтправкаПисьмаИзПриглашенияНаРегистрацию() Экспорт
    
    Возврат НСтр("ru='Отправка письма из приглашения на регистрацию'", 
                 ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());    
    
КонецФункции

#КонецОбласти 

#КонецЕсли