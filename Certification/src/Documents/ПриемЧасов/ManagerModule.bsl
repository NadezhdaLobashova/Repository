Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПриемЧасовРаботы.Ссылка КАК Ссылка,
	|	ПриемЧасовРаботы.Ссылка.Сотрудник КАК Сотрудник,
	|	ПриемЧасовРаботы.Ссылка.Месяц КАК Месяц,
	|	ПриемЧасовРаботы.Работа КАК Работа,
	|	ПриемЧасовРаботы.Дата КАК Дата,
	|	ПриемЧасовРаботы.ЭтапПроекта КАК ЭтапПроекта,
	|	ПриемЧасовРаботы.НовыйЭтапПроекта КАК НовыйЭтапПроекта,
	|	ПриемЧасовРаботы.Подразделение КАК Подразделение,
	|	ПриемЧасовРаботы.НовоеПодразделение КАК НовоеПодразделение,
	|	ПриемЧасовРаботы.Задача КАК Задача,
	|	ПриемЧасовРаботы.Обращение КАК Обращение,
	|	ПриемЧасовРаботы.ВремяНачала КАК ВремяНачала,
	|	ПриемЧасовРаботы.ВремяОкончания КАК ВремяОкончания,
	|	ПриемЧасовРаботы.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	СУММА(ПриемЧасовРаботы.ЭффективныеЧасы) КАК ЭффективныеЧасы,
	|	СУММА(ПриемЧасовРаботы.Часы) КАК Часы,
	|	ПриемЧасовРаботы.ВидРабочегоВремени КАК ВидРабочегоВремени,
	|	УчетФактическихДанныхВремя.КонтрагентАбонент КАК КонтрагентАбонент,
	|	ПриемЧасовРаботы.ВидДействияСНепринятымиЧасами КАК ВидДействияСНепринятымиЧасами,
	|	ПриемЧасовРаботы.Акт КАК Акт,
	|	ПриемЧасовРаботы.ВСчетБудущегоАкта КАК ВСчетБудущегоАкта,
	|	ПриемЧасовРаботы.КомментарийДляБудущегоАкта КАК КомментарийДляБудущегоАкта
	|ПОМЕСТИТЬ втДокумент
	|ИЗ
	|	Документ.ПриемЧасов.Работы КАК ПриемЧасовРаботы
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.УчетФактическихДанных.Время КАК УчетФактическихДанныхВремя
	|		ПО ПриемЧасовРаботы.ИдентификаторСтроки = УчетФактическихДанныхВремя.ИдентификаторСтроки
	|ГДЕ
	|	ПриемЧасовРаботы.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ПриемЧасовРаботы.ВремяОкончания,
	|	ПриемЧасовРаботы.ВремяНачала,
	|	ПриемЧасовРаботы.Работа,
	|	ПриемЧасовРаботы.Задача,
	|	ПриемЧасовРаботы.Ссылка,
	|	ПриемЧасовРаботы.Дата,
	|	ПриемЧасовРаботы.ЭтапПроекта,
	|	ПриемЧасовРаботы.Ссылка.Сотрудник,
	|	ПриемЧасовРаботы.Ссылка.Месяц,
	|	ПриемЧасовРаботы.ИдентификаторСтроки,
	|	ПриемЧасовРаботы.ВидРабочегоВремени,
	|	УчетФактическихДанныхВремя.КонтрагентАбонент,
	|	ПриемЧасовРаботы.НовыйЭтапПроекта,
	|	ПриемЧасовРаботы.Подразделение,
	|	ПриемЧасовРаботы.НовоеПодразделение,
	|	ПриемЧасовРаботы.ВидДействияСНепринятымиЧасами,
	|	ПриемЧасовРаботы.Обращение,
	|	ПриемЧасовРаботы.Акт,
	|	ПриемЧасовРаботы.ВСчетБудущегоАкта,
	|	ПриемЧасовРаботы.КомментарийДляБудущегоАкта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтавкиСотрудниковПоВидамРаботСрезПоследних.Подразделение КАК Подразделение,
	|	СтавкиСотрудниковПоВидамРаботСрезПоследних.Должность КАК Должность,
	|	СтавкиСотрудниковПоВидамРаботСрезПоследних.Регион КАК Регион,
	|	СтавкиСотрудниковПоВидамРаботСрезПоследних.ВидРаботы КАК ВидРаботы,
	|	СтавкиСотрудниковПоВидамРаботСрезПоследних.Ставка КАК Ставка,
	|	СтавкиСотрудниковПоВидамРаботСрезПоследних.Оклад КАК Оклад
	|ПОМЕСТИТЬ СтавкиСрез
	|ИЗ
	|	РегистрСведений.СтавкиСотрудниковПоВидамРабот.СрезПоследних(&ДатаСреза, ) КАК СтавкиСотрудниковПоВидамРаботСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КадроваяИсторияСрезПоследних.Сотрудник КАК Сотрудник,
	|	МАКСИМУМ(КадроваяИсторияСрезПоследних.Период) КАК Период
	|ПОМЕСТИТЬ ВТ_МаксПоДолжности
	|ИЗ
	|	РегистрСведений.КадроваяИстория.СрезПоследних(&ДатаСреза, ) КАК КадроваяИсторияСрезПоследних
	|
	|СГРУППИРОВАТЬ ПО
	|	КадроваяИсторияСрезПоследних.Сотрудник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_МаксПоДолжности.Сотрудник КАК Сотрудник,
	|	КадроваяИстория.Подразделение КАК Подразделение,
	|	КадроваяИстория.Должность КАК Должность,
	|	КадроваяИстория.Регион КАК Регион
	|ПОМЕСТИТЬ ВТ_Должность
	|ИЗ
	|	ВТ_МаксПоДолжности КАК ВТ_МаксПоДолжности
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КадроваяИстория КАК КадроваяИстория
	|		ПО ВТ_МаксПоДолжности.Сотрудник = КадроваяИстория.Сотрудник
	|			И ВТ_МаксПоДолжности.Период = КадроваяИстория.Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Должность.Сотрудник КАК Сотрудник,
	|	СтавкиСрез.Подразделение КАК Подразделение,
	|	СтавкиСрез.Должность КАК Должность,
	|	СтавкиСрез.ВидРаботы КАК ВидРаботы,
	|	СтавкиСрез.Ставка КАК Ставка,
	|	СтавкиСрез.Оклад КАК Оклад
	|ПОМЕСТИТЬ ВТ_СтавкиНов
	|ИЗ
	|	СтавкиСрез КАК СтавкиСрез
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Должность КАК ВТ_Должность
	|		ПО СтавкиСрез.Подразделение = ВТ_Должность.Подразделение
	|			И СтавкиСрез.Должность = ВТ_Должность.Должность
	|			И СтавкиСрез.Регион = ВТ_Должность.Регион
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втДокумент.Ссылка КАК Ссылка,
	|	втДокумент.Сотрудник КАК Сотрудник,
	|	НАЧАЛОПЕРИОДА(втДокумент.Дата, МЕСЯЦ) КАК Месяц,
	|	втДокумент.Работа КАК Работа,
	|	втДокумент.Дата КАК Дата,
	|	втДокумент.ЭтапПроекта КАК ЭтапПроекта,
	|	втДокумент.НовыйЭтапПроекта КАК НовыйЭтапПроекта,
	|	втДокумент.Подразделение КАК Подразделение,
	|	втДокумент.НовоеПодразделение КАК НовоеПодразделение,
	|	втДокумент.Задача КАК Задача,
	|	втДокумент.Обращение КАК Обращение,
	|	втДокумент.ВремяНачала КАК ВремяНачала,
	|	втДокумент.ВремяОкончания КАК ВремяОкончания,
	|	втДокумент.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	втДокумент.ЭффективныеЧасы КАК ЭффективныеЧасы,
	|	втДокумент.Часы КАК Часы,
	|	втДокумент.ВидРабочегоВремени КАК ВидРабочегоВремени,
	|	ВТ_СтавкиНов.Ставка КАК Ставка,
	|	втДокумент.ЭффективныеЧасы * ВТ_СтавкиНов.Ставка КАК Сумма,
	|	втДокумент.КонтрагентАбонент КАК КонтрагентАбонент,
	|	втДокумент.ВидДействияСНепринятымиЧасами КАК ВидДействияСНепринятымиЧасами,
	|	втДокумент.Акт КАК Акт,
	|	втДокумент.ВСчетБудущегоАкта КАК ВСчетБудущегоАкта,
	|	втДокумент.КомментарийДляБудущегоАкта КАК КомментарийДляБудущегоАкта
	|ПОМЕСТИТЬ втИтог
	|ИЗ
	|	втДокумент КАК втДокумент
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтавкиНов КАК ВТ_СтавкиНов
	|		ПО втДокумент.Работа.ВидыРабот = ВТ_СтавкиНов.ВидРаботы
	|			И втДокумент.Сотрудник = ВТ_СтавкиНов.Сотрудник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	""ПринятыеЧасыВСчетБудущихАктов"" КАК ИмяРегистра_,
	|	втИтог.Ссылка КАК Регистратор,
	|	ЗНАЧЕНИЕ(ВидДВиженияНакопления.Приход) КАК ВидДвижения,
	|	втИтог.Ссылка.Проект КАК Проект,
	|	втИтог.НовыйЭтапПроекта КАК ЭтапПроекта,
	|	втИтог.Дата КАК Дата,
	|	втИтог.Сотрудник КАК Сотрудник,
	|	&ДатаСреза КАК Период,
	|	втИтог.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	втИтог.ЭффективныеЧасы КАК Часы,
	|	втИтог.Сумма КАК Сумма,
	|	втИтог.НовоеПодразделение КАК Подразделение,
	|	втИтог.КомментарийДляБудущегоАкта КАК КомментарийДляБудущегоАкта
	|ИЗ
	|	втИтог КАК втИтог
	|ГДЕ
	|	втИтог.Акт = ЗНАЧЕНИЕ(Документ.АктВыполненныхРабот.ПустаяСсылка)
	|	И втИтог.ВСчетБудущегоАкта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	""АктыПринятыхЧасов"" КАК ИмяРегистра_,
	|	втИтог.Ссылка КАК Регистратор,
	|	втИтог.Ссылка.Проект КАК Проект,
	|	втИтог.НовыйЭтапПроекта КАК ЭтапПроекта,
	|	втИтог.Дата КАК Дата,
	|	втИтог.Сотрудник КАК Сотрудник,
	|	&ДатаСреза КАК Период,
	|	втИтог.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	втИтог.ЭффективныеЧасы КАК Часы,
	|	втИтог.Сумма КАК Сумма,
	|	втИтог.НовоеПодразделение КАК Подразделение,
	|	втИтог.Акт КАК Акт
	|ИЗ
	|	втИтог КАК втИтог
	|ГДЕ
	|	втИтог.Акт <> ЗНАЧЕНИЕ(Документ.АктВыполненныхРабот.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	""ПланированиеПроектов_Факт"" КАК ИмяРегистра_,
	|	втИтог.Ссылка КАК Регистратор,
	|	втИтог.Ссылка.Проект КАК Проект,
	|	втИтог.НовыйЭтапПроекта КАК ЭтапПроекта,
	|	втИтог.Дата КАК ПериодПланирования,
	|	ВЫБОР
	|		КОГДА втИтог.Работа.ВидыРабот.ОсновнаяСтатья = ЗНАЧЕНИЕ(Справочник.СтатьиПланирования.Пустаяссылка)
	|			ТОГДА втИтог.Ссылка.Проект.ОсновнаяСтатья
	|		ИНАЧЕ втИтог.Работа.ВидыРабот.ОсновнаяСтатья
	|	КОНЕЦ КАК Статья,
	|	втИтог.Сотрудник КАК Сотрудник,
	|	&ДатаСреза КАК Период,
	|	втИтог.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	втИтог.КонтрагентАбонент КАК КонтрагентАбонент,
	|	втИтог.ЭффективныеЧасы КАК Время,
	|	втИтог.Сумма КАК Сумма,
	|	втИтог.Работа КАК Работа,
	|	втИтог.НовоеПодразделение КАК Подразделение,
	|	ВТ_Должность.Подразделение КАК ПодразделениеСотрудника
	|ИЗ
	|	втИтог КАК втИтог
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Должность КАК ВТ_Должность
	|		ПО втИтог.Сотрудник = ВТ_Должность.Сотрудник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	""ПринятыеЧасы"" КАК ИмяРегистра_,
	|	втИтог.Ссылка КАК Регистратор,
	|	втИтог.НовыйЭтапПроекта КАК ЭтапПроекта,
	|	&ДатаСреза КАК Период,
	|	втИтог.Сотрудник КАК Сотрудник,
	|	втИтог.Ссылка.Проект КАК Проект,
	|	втИтог.НовоеПодразделение КАК Подразделение,
	|	втИтог.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	втИтог.ЭффективныеЧасы КАК Часы,
	|	ЗНАЧЕНИЕ(ВидДВиженияНакопления.Приход) КАК ВидДвижения,
	|	втИтог.Дата КАК Дата,
	|	втИтог.Сумма КАК Сумма
	|ИЗ
	|	втИтог КАК втИтог
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	""ОтработанноеВремя"" КАК ИмяРегистра_,
	|	&ДатаСреза КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	втИтог.ЭтапПроекта КАК ЭтапПроекта,
	|	втИтог.Дата КАК Дата,
	|	втИтог.Сотрудник КАК Сотрудник,
	|	втИтог.Работа КАК Работа,
	|	втИтог.Задача КАК Задача,
	|	втИтог.Обращение КАК Обращение,
	|	втИтог.Подразделение КАК Подразделение,
	|	втИтог.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	втИтог.Месяц КАК Месяц,
	|	втИтог.ВремяНачала КАК ВремяНачала,
	|	втИтог.ВремяОкончания КАК ВремяОкончания,
	|	ВЫБОР
	|		КОГДА втИтог.ВидДействияСНепринятымиЧасами = ЗНАЧЕНИЕ(Перечисление.ВидыДействийСНепринятымиЧасами.ОстатокОтложить)
	|			ТОГДА втИтог.ЭффективныеЧасы
	|		ИНАЧЕ втИтог.Часы
	|	КОНЕЦ КАК Часы,
	|	втИтог.Ссылка.Проект КАК Проект,
	|	втИтог.ВидРабочегоВремени КАК ВидРабочегоВремени
	|ИЗ
	|	втИтог КАК втИтог";
	Запрос.УстановитьПараметр("Ссылка"   , ДокументСсылка);
	Запрос.УстановитьПараметр("ДатаСреза", ДокументСсылка.Дата);
		
	Результат = Запрос.ВыполнитьПакет();
	
	ТаблицыДляДвижений = ДополнительныеСвойства.ТаблицыДляДвижений;
	ТаблицыДляДвижений.Вставить("ТаблицаОтработанноеВремя"				, Результат[Результат.Количество() - 1].Выгрузить());
	ТаблицыДляДвижений.Вставить("ТаблицаПринятыеЧасы"					, Результат[Результат.Количество() - 2].Выгрузить());
	ТаблицыДляДвижений.Вставить("ТаблицаПланированиеПроектов_Факт"		, Результат[Результат.Количество() - 3].Выгрузить());
	ТаблицыДляДвижений.Вставить("ТаблицаАктыПринятыхЧасов"				, Результат[Результат.Количество() - 4].Выгрузить());
	ТаблицыДляДвижений.Вставить("ТаблицаПринятыеЧасыВСчетБудущихАктов"	, Результат[Результат.Количество() - 5].Выгрузить());
КонецПроцедуры

Процедура ВыполнитьКонтрольРезультатовПроведения(Объект, Отказ) Экспорт
	
	Если Ложь Тогда
		Объект = Документы.ПриемЧасов.СоздатьДокумент();
	КонецЕсли;
	
КонецПроцедуры

Процедура ВыполнитьКонтрольРезультатовОтменыПроведения(Объект, Отказ) Экспорт
	
	Если Ложь Тогда
		Объект = Документы.ПриемЧасов.СоздатьДокумент();
	КонецЕсли;
	
КонецПроцедуры

Процедура ВыполнитьКонтрольПередПроведением(Объект, Отказ) Экспорт
	
	Если Ложь Тогда
		Объект = Документы.ПриемЧасов.СоздатьДокумент();
	КонецЕсли;
КонецПроцедуры

