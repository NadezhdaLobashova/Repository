#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ЭтоНовый = ЭтоНовый();
	Если ЭтоНовый() Тогда
		Автор = Пользователи.АвторизованныйПользователь();
	КонецЕсли;
	
	ДополнительныеСвойства.Вставить("ЭтоНовый", ЭтоНовый);
	ДополнительныеСвойства.Вставить("РежимЗаписи", РежимЗаписи);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ДействияСБлокировкойРасширяющейПодписки();
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбщаяПроверкаПриЗаписи(Отказ);
	
	Если ОсновнаяПодписка.Пустая() Тогда // Основная подписка
		
		ОсновнаяПодпискаПроверкаПриЗаписи(Отказ);
		
	Иначе
		
		Если ОсновнаяПодписка.Абонент <> Абонент Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не совпадает Абонент основной подписки и Абонент подписки на тариф'"));
			Отказ = Истина;
		КонецЕсли;

		Если ОсновнаяПодписка.ВедущийАбонент <> ВедущийАбонент Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не совпадает Обслуживающая организация основной подписки и Обслуживающая организация подписки на тариф'"));
			Отказ = Истина;
		КонецЕсли;
		
		Если ТипПодписки = Перечисления.ТипыПодписок.Основная Тогда // Подписка на расширение тарифа
			ПодпискаНаРасширениеПроверкаПриЗаписи(Отказ);
		ИначеЕсли ТипПодписки = Перечисления.ТипыПодписок.Продлевающая Тогда // Продлевающая подписка
			ПродлевающаяПодпискаПроверкаПриЗаписи(Отказ);
		ИначеЕсли ТипПодписки = Перечисления.ТипыПодписок.Расширяющая Тогда // Расширяющая подписка
			РасширяющаяПодпискаПроверкаПриЗаписи(Отказ);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ДействияСБлокировкойРасширяющейПодписки()
	
	Перем ЭтоНовый;
	Перем РежимЗаписи;
	
	ДополнительныеСвойства.Свойство("ЭтоНовый", ЭтоНовый);
	ДополнительныеСвойства.Свойство("РежимЗаписи", РежимЗаписи);
	ЭтоРасширение = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Тариф, "РасширениеТарифа");
	
КонецПроцедуры

Процедура ОбщаяПроверкаПриЗаписи(Отказ)
	
	Если ДатаОтключения < ДатаПодключения Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Дата подключения должна быть меньше даты отключения'"));
		Отказ = Истина;
	КонецЕсли;
	
	//Если (Тариф.ДатаНачалаДействия <> '00010101' И ДатаПодключения < Тариф.ДатаНачалаДействия) Или
	//	(Тариф.ДатаОкончанияДействия <> '00010101' И ДатаОтключения > Тариф.ДатаОкончанияДействия) Тогда
	//	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Истек срок действия тарифа'"));
	//	Отказ = Истина;
	//КонецЕсли;
	
КонецПроцедуры

Процедура ОсновнаяПодпискаПроверкаПриЗаписи(Отказ)
	
	Если Тариф.РасширениеТарифа Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
            СтрШаблон("%1: %2", НСтр("ru = 'Нельзя подписывать на расширение тарифа'"), Тариф.Наименование));
		Отказ = Истина;
	КонецЕсли;
	
	Если ТипПодписки <> Перечисления.ТипыПодписок.Основная Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Тип подписки должен быть Основная'"));
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПодпискаНаРасширениеПроверкаПриЗаписи(Отказ)
	
	Если Не Тариф.РасширениеТарифа Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
            СтрШаблон("%1: %2", НСтр("ru = 'При добавлении расширения тарифа нельзя подписывать на тариф"), Тариф.Наименование));
		Отказ = Истина;
	КонецЕсли;
	
	Если ОсновнаяПодписка.ДатаОтключения < ДатаОтключения Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Дата отключения основной подписки не должна быть меньше даты отключения подписки расширения на тариф'"));
		Отказ = Истина;
	КонецЕсли;
	
	НайденнаяСтрока = Тариф.ДоступныеТарифы.Найти(ОсновнаяПодписка.Тариф, "Тариф");
	Если НайденнаяСтрока = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Данное расширение на тариф не может быть подключено к текущему тарифу'"));
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПродлевающаяПодпискаПроверкаПриЗаписи(Отказ)
	
	Если ЭтоНовый() И Документы.Подписка.ЕстьПродлевающиеПодписки(ОсновнаяПодписка) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Продлеваемая подписка уже была продлена'"));
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Тариф = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОсновнаяПодписка, "Тариф");
	Если Не ЗначениеЗаполнено(Тариф) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не заполнен тариф у продлеваемой подписки'"));
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ПериодДобавленияПродлевающейПодписки = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Тариф, "ПериодДобавленияПродлевающейПодписки");
	Если Не ЗначениеЗаполнено(ПериодДобавленияПродлевающейПодписки) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Нельзя продлевать подписку на тариф'"));
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ДатаОкончанияОсновнойПодписки = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОсновнаяПодписка, "ДатаОтключения");
	Если Не ЗначениеЗаполнено(ДатаОкончанияОсновнойПодписки) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'У продлевающей подписки не установлена Дата отключения'"));
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если ДатаПодключения >= (ДатаОкончанияОсновнойПодписки + ПериодДобавленияПродлевающейПодписки * 86400) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Истекла возможность продления тарифа'"));
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ПериодДействияПродлевающейПодписки = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Тариф, "ПериодДействияПродлевающейПодписки");
	Если Не ЗначениеЗаполнено(ПериодДействияПродлевающейПодписки) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Нельзя продлевать подписку на тариф'"));
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если (ДатаОтключения - ДатаПодключения)/86400 > ПериодДействияПродлевающейПодписки Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Некорректный период продления подписки'"));
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

Процедура РасширяющаяПодпискаПроверкаПриЗаписи(Отказ)
	
	Если ЭтоНовый() И Документы.Подписка.ЕстьРасширяющиеПодпискиПоДате(ОсновнаяПодписка, ДатаПодключения) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'В данный промежуток уже имеется расширяющая подписка'"));
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если ЭтоНовый() И Документы.Подписка.ЕстьРасширяющиеПодписки(ОсновнаяПодписка) И Документы.Подписка.РазрешеноДобавлятьРасширяющуюПодписку(ОсновнаяПодписка) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Абоненту нельзя расширять данную подписку'"));
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Тариф = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОсновнаяПодписка, "Тариф");
	Если Не ЗначениеЗаполнено(Тариф) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не заполнен тариф у расширяемой подписки'"));
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ПериодДействияРасширяющейПодписки = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Тариф, "ПериодДействияРасширяющейПодписки");
	Если Не ЗначениеЗаполнено(ПериодДействияРасширяющейПодписки) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Нельзя расширять подписку на тариф'"));
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если (ДатаОтключения - ДатаПодключения)/86400 > ПериодДействияРасширяющейПодписки Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Некорректный период расширяющей подписки'"));
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ДатаОтключенияОсновной = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОсновнаяПодписка, "ДатаОтключения");
	Если ДатаОтключения > ДатаОтключенияОсновной Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Дата отключения расширяющей подписки не может быть больше даты расширения основной'"));
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


#КонецЕсли