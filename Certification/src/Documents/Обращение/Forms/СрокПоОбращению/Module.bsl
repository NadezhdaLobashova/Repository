
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
    
	Обращение = Параметры.Обращение;
	
	// Рассчитаем длительность по обращению, чтобы отобразить точные данные на форме.
	РегистрыСведений.ДлительностьОбработкиОбращений.ВыполнитьРасчет(ТекущаяУниверсальнаяДата(), Обращение);
	
	СекундДоМестногоВремени = Параметры.СекундДоМестногоВремени;
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = 
		"ВЫБРАТЬ
        |   Сроки.Соглашение,
        |   ДОБАВИТЬКДАТЕ(Сроки.СрокОбработки, СЕКУНДА, &СекундДоМестногоВремени) КАК СрокОбработки,
        |   Сроки.КлючПараметров
        |ИЗ
        |   РегистрСведений.СрокиОбращенийПоСоглашениям.СрезПоследних(, Обращение = &Обращение) КАК Сроки";
		
	Запрос.УстановитьПараметр("Обращение", Обращение);
	Запрос.УстановитьПараметр("СекундДоМестногоВремени", СекундДоМестногоВремени);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Если Выборка.Следующий() Тогда
		КлючПараметров = Выборка.КлючПараметров;
		Соглашение = Выборка.Соглашение;
		Элементы.ДекорацияСрок.Заголовок = СтрШаблон(НСтр("ru='Срок: %1'"), Формат(Выборка.СрокОбработки, "ДФ='dd.MM.yyyy (HH:mm)'")); 
		Элементы.ДекорацияОписание.Заголовок = РегистрыСведений.СрокиОбращенийПоСоглашениям.ОписаниеУсловийУстановкиСрока(Обращение, СекундДоМестногоВремени);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СоглашениеНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПараметрыФормы = Новый Структура("Ключ, КлючПараметров", Соглашение, КлючПараметров);
	ОткрытьФорму("Справочник.Соглашения.ФормаОбъекта", ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти
