#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	ИнициализироватьДокумент();
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
    
    УстановитьФлагСделатьДвижениеВТЧ();
        
КонецПроцедуры


Процедура ОбработкаПроведения(Отказ, РежимПроведения)
    
    Если Отказ Тогда
		Возврат;
    КонецЕсли;
    
	ДанныеДляПроведения = ДанныеДляПроведенияДокумента();
    ПродуктыАбонентаДвижение = Движения.ПродуктыАбонента;
    ПродуктыАбонентаДвижение.Очистить();
	ПродуктыАбонентаДвижение.Записать();
    ПродуктыАбонентаДвижение.Записывать = Истина;
    Пока ДанныеДляПроведения.Следующий() Цикл
         НовоеДвижение = ПродуктыАбонентаДвижение.Добавить();
         ЗаполнитьЗначенияСвойств(НовоеДвижение,ДанныеДляПроведения);
    КонецЦикла;
	ПродуктыАбонентаДвижение.Записать();
    
КонецПроцедуры
 
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура УстановитьФлагСделатьДвижениеВТЧ()
	
    Запрос = Новый Запрос;
    Запрос.УстановитьПараметр("Таблица",ПродуктыАбонентов.Выгрузить());
    Запрос.УстановитьПараметр("Дата",   Дата);
    Запрос.Текст = 
    "ВЫБРАТЬ
    |   ТаблицаДокумента.НомерСтроки КАК НомерСтроки,
    |   ТаблицаДокумента.Абонент КАК Абонент,
    |   ТаблицаДокумента.РегНомер КАК РегНомер,
    |   ТаблицаДокумента.Состояние КАК Состояние,
    |   ТаблицаДокумента.СделатьДвижение КАК СделатьДвижение
    |ПОМЕСТИТЬ ТЧ_Документа
    |ИЗ
    |   &Таблица КАК ТаблицаДокумента
    |
    |ИНДЕКСИРОВАТЬ ПО
    |   НомерСтроки,
    |   Абонент
    |;
    |
    |////////////////////////////////////////////////////////////////////////////////
    |ВЫБРАТЬ
    |   ТЧ_Документа.НомерСтроки КАК НомерСтроки,
    |   ТЧ_Документа.Абонент КАК Абонент,
    |   ТЧ_Документа.РегНомер КАК РегНомер,
    |   ТЧ_Документа.Состояние КАК Состояние,
    |   ТЧ_Документа.СделатьДвижение КАК СделатьДвижение
    |ИЗ
    |   ТЧ_Документа КАК ТЧ_Документа
    |       ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПродуктыАбонента.СрезПоследних(&Дата, ) КАК ПродуктыАбонентаСрезПоследних
    |       ПО ТЧ_Документа.Абонент = ПродуктыАбонентаСрезПоследних.Абонент
    |ГДЕ
    |   ПродуктыАбонентаСрезПоследних.Регистратор ЕСТЬ NULL";
    
    
    РезультатЗапроса = Запрос.Выполнить();
    
    ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
    
    Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
        НайденнаяСтрока = ПродуктыАбонентов.Найти(ВыборкаДетальныеЗаписи.НомерСтроки,"НомерСтроки");
        НайденнаяСтрока.СделатьДвижение = Истина;
    КонецЦикла;

КонецПроцедуры


Функция ДанныеДляПроведенияДокумента()
    
    Запрос = Новый Запрос;
    Запрос.УстановитьПараметр("Ссылка", Ссылка);
    Запрос.УстановитьПараметр("Период", Дата);
    Запрос.Текст = 
    "ВЫБРАТЬ
    |   &Ссылка КАК Регистратор,
    |   &Период КАК Период,
    |   РегистрационнаяАнкетаПродуктыАбонентов.Абонент КАК Абонент,
    |   РегистрационнаяАнкетаПродуктыАбонентов.РегНомер КАК РегНомер,
    |   РегистрационнаяАнкетаПродуктыАбонентов.Состояние КАК Состояние
    |ИЗ
    |   Документ.РегистрационнаяАнкета.ПродуктыАбонентов КАК РегистрационнаяАнкетаПродуктыАбонентов
    |ГДЕ
    |   РегистрационнаяАнкетаПродуктыАбонентов.СделатьДвижение
    |   И РегистрационнаяАнкетаПродуктыАбонентов.Ссылка = &Ссылка";
    
    Результат = Запрос.Выполнить();
    Выборка = Результат.Выбрать();
    
    Возврат Выборка;
 КонецФункции    

#КонецОбласти

#Область ИнициализацияИЗаполнение

Процедура ИнициализироватьДокумент(ДанныеЗаполнения = Неопределено)
    
    Если ЭтоНовый() Тогда
        Ответственный = Пользователи.ТекущийПользователь();
    КонецЕсли;
    
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
		
КонецПроцедуры

#КонецОбласти

#Область Прочее 
 
#КонецОбласти

#КонецЕсли
