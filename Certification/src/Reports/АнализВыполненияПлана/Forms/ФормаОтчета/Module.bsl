&НаСервере
Процедура ПриОбновленииСоставаПользовательскихНастроекНаСервере(СтандартнаяОбработка)
	УстановитьКлючВарианта();
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	УстановитьКлючВарианта();
КонецПроцедуры

&НаСервере
Процедура ПередЗагрузкойВариантаНаСервере(Настройки)
	УстановитьКлючВарианта();
КонецПроцедуры

&НаСервере
Процедура УстановитьКлючВарианта()
	Отчет.КлючВарианта = КлючТекущегоВарианта;
КонецПроцедуры	

&НаКлиенте
Процедура РезультатОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)
	Если Отчет.КлючВарианта = "ЧасыПоПроектам" Тогда
		СтандартнаяОбработка = Ложь;
		
		Если Расшифровка = Неопределено Тогда 
			Возврат 
		КонецЕсли;
		
		ФормаОтчетаРасшифровки = ПолучитьФорму(ЭтаФорма.ИмяФормы, Новый Структура("КлючВарианта", "ЧасыПоСотрудникам"), ЭтаФорма, Истина);
		ПараметрДатыЭтогоОтчета = УправлениеОтчетамиКлиентСервер.ПолучитьПользовательскийПараметр("Период", Отчет.КомпоновщикНастроек);
		ПараметрДатыОтчетаРасшифровки = УправлениеОтчетамиКлиентСервер.ПолучитьПользовательскийПараметр("Период", ФормаОтчетаРасшифровки.Отчет.КомпоновщикНастроек);
		Если ПараметрДатыЭтогоОтчета <> Неопределено И ПараметрДатыОтчетаРасшифровки <> Неопределено Тогда
			ЗаполнитьЗначенияСвойств(ПараметрДатыОтчетаРасшифровки,ПараметрДатыЭтогоОтчета,"Значение,Использование");
		КонецЕсли;
		Проект = ФормаОтчетаРасшифровки.Отчет.КомпоновщикНастроек.Настройки.Отбор.ДоступныеПоляОтбора.Элементы.Найти("Проект").Поле;	
		Для Каждого ЭлементНастройки из ФормаОтчетаРасшифровки.Отчет.КомпоновщикНастроек.Настройки.Отбор.Элементы Цикл
			Настройка = ФормаОтчетаРасшифровки.Отчет.КомпоновщикНастроек.ПользовательскиеНастройки.Элементы.Найти(ЭлементНастройки.ИдентификаторПользовательскойНастройки);
			Если ТипЗнч(ЭлементНастройки) = Тип("ЭлементОтбораКомпоновкиДанных") Тогда
				Если ЭлементНастройки.ЛевоеЗначение=Проект тогда
					ЭлементНастройки.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно; 
					ЭлементНастройки.Использование = Истина;
					ЭлементНастройки.ПравоеЗначение = Расшифровка;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		ФормаОтчетаРасшифровки.Открыть();
		ФормаОтчетаРасшифровки.СкомпоноватьРезультат();
	КонецЕсли;	
КонецПроцедуры



















