#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
    
#Область СлужебныеПроцедурыИФункции

Процедура НастроитьМакетСхемыКомпоновки()

    // Если соглашения не используются, поля Превышение, ПревышениеВРабочееВремя, ТекущийОстатокПревышение нужно скрыть.
    Если Не ПолучитьФункциональнуюОпцию("ИспользоватьСоглашения") Тогда
        ПолеПревышение = Новый ПолеКомпоновкиДанных("Превышение");
        ПолеПревышениеВРабочееВремя = Новый ПолеКомпоновкиДанных("ПревышениеВРабочееВремя");
        ПолеТекущийОстатокПревышение = Новый ПолеКомпоновкиДанных("ТекущийОстатокПревышение");
        ПоляОграничений = СтрРазделить("Поле, Группировка, Порядок, Условие", ", ", Ложь);
        Для каждого ПолеНабора Из СхемаКомпоновкиДанных.НаборыДанных.Основной.Поля Цикл
            Если ПолеНабора.Поле = "Превышение" 
                 Или ПолеНабора.Поле = "ПревышениеВРабочееВремя" 
                 Или ПолеНабора.Поле = "ТекущийОстатокПревышение" Тогда
                Для Каждого Ограничение Из ПоляОграничений Цикл
                     ПолеНабора.ОграничениеИспользования[Ограничение] = Истина;
                     ПолеНабора.ОграничениеИспользованияРеквизитов[Ограничение] = Истина;
                КонецЦикла; 	
            КонецЕсли;
        КонецЦикла;
        Для каждого ПолеНабора Из СхемаКомпоновкиДанных.ВычисляемыеПоля Цикл
            Если ПолеНабора.ПутьКДанным = "ПревышениеВРабочееВремя" 
                 Или ПолеНабора.ПутьКДанным = "Превышение" 
                 Или ПолеНабора.ПутьКДанным = "ТекущийОстатокПревышение"  Тогда
                Для Каждого Ограничение Из ПоляОграничений Цикл
                     ПолеНабора.ОграничениеИспользования[Ограничение] = Истина;
                КонецЦикла; 	
            КонецЕсли;
        КонецЦикла;
        ВариантыНастроек = СтрРазделить("АнализОбращений, ТекущиеИсполнителиПоОткрытымОбращениям, ОбращенияНаОжидании, РаспределениеОбращенийПоКарточкамБазыЗнаний", ", ", Ложь);
        Для каждого ИмяВарианта Из ВариантыНастроек Цикл
        	ВариантНастроек = СхемаКомпоновкиДанных.ВариантыНастроек[ИмяВарианта];
            Для НомерПоля = -(ВариантНастроек.Настройки.Выбор.Элементы.Количество() - 1) По 0 Цикл
                ПолеВыбора = ВариантНастроек.Настройки.Выбор.Элементы[-НомерПоля];
                Если ПолеВыбора.Поле = ПолеПревышение 
                     Или ПолеВыбора.Поле = ПолеПревышениеВРабочееВремя 
                     Или ПолеВыбора.Поле = ПолеТекущийОстатокПревышение Тогда
                    ВариантНастроек.Настройки.Выбор.Элементы.Удалить(ПолеВыбора);
                КонецЕсли;
            КонецЦикла;
        КонецЦикла; 
    КонецЕсли; 
    
КонецПроцедуры
    
НастроитьМакетСхемыКомпоновки();

#КонецОбласти

#КонецЕсли
