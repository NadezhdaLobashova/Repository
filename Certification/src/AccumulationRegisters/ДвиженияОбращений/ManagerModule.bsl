#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Добавляет движения по обращению.
//
// Параметры:
//  Движения - ДокументОбъект.Обращение.Движения.ДвиженияОбращений - движения документа. 
//
Процедура ДобавитьДвижения(Движения) Экспорт
    
    УстановитьПривилегированныйРежим(Истина);
    
    Запрос = Новый Запрос;
    
    Запрос.Текст = 
        "ВЫБРАТЬ
        |   ДвижениеОбращений.Период КАК Период,
        |   ДвижениеОбращений.ОбслуживающаяОрганизация,
        |   ДвижениеОбращений.ЛинияПоддержки,
        |   ДвижениеОбращений.Исполнитель,
        |   ДвижениеОбращений.Состояние,
        |   ДвижениеОбращений.Обращение
        |ИЗ
        |   РегистрСведений.ДвижениеОбращений КАК ДвижениеОбращений
        |ГДЕ
        |   ДвижениеОбращений.Обращение = &Обращение
        |
        |УПОРЯДОЧИТЬ ПО
        |   Период";
        
    Запрос.УстановитьПараметр("Обращение", Движения.Отбор.Регистратор.Значение);
    
    Результат = Запрос.Выполнить();
    Выборка = Результат.Выбрать();
    
    Движения.Записывать = Истина;
    
    ПредыдущееДвижение = Неопределено;
    Пока Выборка.Следующий() Цикл
        Если ПредыдущееДвижение <> Неопределено Тогда
            Сторно = Движения.Добавить();
            ЗаполнитьЗначенияСвойств(Сторно, ПредыдущееДвижение, 
                "ОбслуживающаяОрганизация, ЛинияПоддержки, Исполнитель, Состояние, Обращение");
            Сторно.Период = Выборка.Период;
            Сторно.Движение = 1;
            Сторно.ВидДвижения = ВидДвиженияНакопления.Расход;
        КонецЕсли;
        Движение = Движения.Добавить();
        ЗаполнитьЗначенияСвойств(Движение, Выборка);
        Движение.Движение = 1;
        Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
        ПредыдущееДвижение = Движение;
        Если Движение.Состояние = Перечисления.СостоянияОбращений.Закрыто Тогда
            Движение = Движения.Добавить();
            ЗаполнитьЗначенияСвойств(Движение, Выборка);
            Движение.Движение = 1;
            Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
            ПредыдущееДвижение = Неопределено;
        КонецЕсли;
    КонецЦикла;
    
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Процедура ЗаполнитьДвижения_1_0_8_9() Экспорт
    
    Запрос = Новый Запрос;
    
    Запрос.Текст = 
    	"ВЫБРАТЬ
        |	ДвиженияОбращенийОбороты.Обращение
        |ИЗ
        |	РегистрНакопления.ДвиженияОбращений.Обороты КАК ДвиженияОбращенийОбороты";
    
    Результат = Запрос.Выполнить();
    Выборка = Результат.Выбрать();
    
    Пока Выборка.Следующий() Цикл
		Движения = РегистрыНакопления.ДвиженияОбращений.СоздатьНаборЗаписей();
        Движения.Отбор.Регистратор.Установить(Выборка.Обращение, Истина);
        ДобавитьДвижения(Движения);
    	Движения.Записать();
    КонецЦикла;
    
КонецПроцедуры

#КонецОбласти

#КонецЕсли
 