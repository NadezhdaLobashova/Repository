
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ЭтотОбъект.ТолькоПросмотр = Параметры.ТолькоПросмотр;
	
	КлючПараметров = Параметры.КлючПараметров;
	Получатель = Параметры.Получатель;
	ТипСрываСроков = Параметры.ТипСрываСроков;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	ПроверяемыеРеквизиты.Добавить("Получатель");
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ПолучательНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	СписокВыбора = Новый СписокЗначений;
	СписокВыбора.Добавить(ПредопределенноеЗначение("Перечисление.ВыраженияПолучателей.РуководительЛинииПоддержки"));
	СписокВыбора.Добавить(ПредопределенноеЗначение("Перечисление.ВыраженияПолучателей.ПерваяЛинияПоддержки"));
	СписокВыбора.Добавить(ПредопределенноеЗначение("Перечисление.ВыраженияПолучателей.ЛинияПоддержкиИсполнителя"));
	СписокВыбора.Добавить("ВыбратьЛиниюПоддержки", НСтр("ru='Выбрать линию поддержки'"));
	СписокВыбора.Добавить("ВыбратьПользователя", НСтр("ru='Выбрать пользователя'"));
	
	ОписаниеОповещенияОВыборе = Новый ОписаниеОповещения("ПолучательНачалоВыбораПродолжениеВыбораИзСписка", ЭтотОбъект);
		
	ПоказатьВыборИзСписка(ОписаниеОповещенияОВыборе, СписокВыбора, Элемент);
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	ПараметрыЗакрытия = Новый Структура;
	ПараметрыЗакрытия.Вставить("КлючПараметров", КлючПараметров);
	ПараметрыЗакрытия.Вставить("Получатель", Получатель);
	ПараметрыЗакрытия.Вставить("ТипСрываСроков", ТипСрываСроков);
	
	Закрыть(ПараметрыЗакрытия);
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПолучательНачалоВыбораПродолжениеВыбораИзСписка(ВыбранноеЗначение, ПараметрыОповещения) Экспорт
	
	Оповещение = Новый ОписаниеОповещения("ПолучательНачалоВыбораПродолжениеВыбораИзСпискаЗавершение", ЭтотОбъект, ПараметрыОповещения);
	
	Если ВыбранноеЗначение = Неопределено Тогда
		Возврат;
	ИначеЕсли ТипЗнч(ВыбранноеЗначение.Значение) = Тип("ПеречислениеСсылка.ВыраженияПолучателей") Тогда
		Получатель = ВыбранноеЗначение.Значение;
	ИначеЕсли ВыбранноеЗначение.Значение = "ВыбратьЛиниюПоддержки" Тогда
		ОткрытьФорму("Справочник.ЛинииПоддержки.ФормаВыбора",, ЭтаФорма,,,, Оповещение);
	ИначеЕсли ВыбранноеЗначение.Значение = "ВыбратьПользователя" Тогда
		ОткрытьФорму("Справочник.Пользователи.Форма.ФормаВыбораИсполнителя",, ЭтаФорма,,,, Оповещение);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучательНачалоВыбораПродолжениеВыбораИзСпискаЗавершение(ВыбранноеЗначение, ПараметрыОповещения) Экспорт
	
	Если ВыбранноеЗначение <> Неопределено Тогда
		Получатель = ВыбранноеЗначение;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти 
