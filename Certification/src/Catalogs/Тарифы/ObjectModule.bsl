#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ПометкаУдаления Тогда 
		Возврат;
	КонецЕсли;
	
	ПроверкаДублейВКонфигурациях(Отказ);
	ПроверкаДублейВОграничениях(Отказ);
	
	Если РасширениеТарифа Тогда 
		
		Если ДоступныеТарифы.Количество() = 0 Тогда 
			Отказ = Истина;
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Должен быть заполнен хоть один доступный тариф.'"));
		КонецЕсли;
		
		Для Каждого СтрокаТЧ Из ДоступныеТарифы Цикл 
			Если СтрокаТЧ.Тариф.РасширениеТарифа Тогда 
				Отказ = Истина;
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'В Доступных тарифах не могут быть установлены расширения тарифов.'"));
			КонецЕсли;
		КонецЦикла;
		
		Если ПериодДействияРасширяющейПодписки <> 0 Тогда
			Отказ = Истина;
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Нельзя в расширении тарифа настраивать расширяющую подписку.'"));
		КонецЕсли;
		
	КонецЕсли;
	
	Если ПериодДействияРасширяющейПодписки <> 0 Тогда
		
		Для Каждого Ограничение Из Ограничения Цикл
			
			Если Ограничение.КоличествоЛицензий = 0 И Ограничение.КоличествоДопЛицензийРасширяющейПодписки <> 0 Тогда
				Отказ = Истина;
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Нельзя расширять неограниченный вид ограничений.'"));
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПроверкаДублейВКонфигурациях(Отказ)
	
	Для Каждого ТекущаяКонфигурация Из ЭтотОбъект.Конфигурации Цикл 
		Фильтр = Новый Структура;
		Фильтр.Вставить("Конфигурация", ТекущаяКонфигурация.Конфигурация);
		Если ЭтотОбъект.Конфигурации.НайтиСтроки(Фильтр).Количество() > 1 Тогда
			ИмяКонфигурации = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТекущаяКонфигурация.Конфигурация, "Наименование");
			ТекстИсключения  = НСтр("ru = 'В ограничениях по тарифу конфигурация ""%ИмяКонфигурации%"" не должна быть добавлена больше одного раза.'");
			ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ИмяКонфигурации%", ИмяКонфигурации);
			Отказ = Истина;
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстИсключения);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ПроверкаДублейВОграничениях(Отказ)
	
	Для Каждого ТекущееОграничение Из ЭтотОбъект.Ограничения Цикл 
		Фильтр = Новый Структура;
		Фильтр.Вставить("ВидОграничений", ТекущееОграничение.ВидОграничений);
		Если ЭтотОбъект.Ограничения.НайтиСтроки(Фильтр).Количество() > 1 Тогда 
			ИмяОграничения = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТекущееОграничение.ВидОграничений, "Наименование");
			ТекстИсключения  = НСтр("ru = 'В ограничениях по тарифу ограничение ""%ИмяОграничения%"" не должно быть добавлено больше одного раза.'");
			ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ИмяОграничения%", ИмяОграничения);
			Отказ = Истина;
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстИсключения);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры 

#КонецОбласти

#КонецЕсли