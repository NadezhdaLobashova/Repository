
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.КонтактнаяИнформация
	УправлениеКонтактнойИнформацией.ПриСозданииНаСервере(ЭтаФорма, Объект, "ГруппаКонтактнаяИнформация", ПоложениеЗаголовкаЭлементаФормы.Лево);
	// Конец СтандартныеПодсистемы.КонтактнаяИнформация
	
	РеквизитыОрганизации = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.Владелец, "ДоступныеСервисы, ДоступныеКомпоненты");
	
	Если РеквизитыОрганизации.ДоступныеСервисы <> Неопределено Тогда
		ДоступныеСервисы.ЗагрузитьЗначения(РеквизитыОрганизации.ДоступныеСервисы.Выгрузить().ВыгрузитьКолонку("Сервис"));
	КонецЕсли;
	
	Если РеквизитыОрганизации.ДоступныеКомпоненты <> Неопределено Тогда
		ДоступныеКомпоненты.ЗагрузитьЗначения(РеквизитыОрганизации.ДоступныеКомпоненты.Выгрузить().ВыгрузитьКолонку("Компонент"));
	КонецЕсли;
	
	Если Не Пользователи.ЭтоПолноправныйПользователь() Тогда
		Элементы.СтраницаГруппыПользователей.Видимость = Ложь;
	КонецЕсли;
	
	Если Объект.Владелец <> Справочники.ОбслуживающиеОрганизации.СлужбаПоддержки Тогда
		Элементы.ГруппаЭкспертов.Видимость = Ложь;
	КонецЕсли;
	
	УстановитьПараметрыВыбораКомпонентов();
	ЗагрузитьЧасовыеПояса();
	ПриСозданииПриЧтенииНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.КонтактнаяИнформация
	УправлениеКонтактнойИнформацией.ПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.КонтактнаяИнформация
	ПриСозданииПриЧтенииНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	// СтандартныеПодсистемы.КонтактнаяИнформация
	УправлениеКонтактнойИнформацией.ОбработкаПроверкиЗаполненияНаСервере(ЭтаФорма, Объект, Отказ);
	// Конец СтандартныеПодсистемы.КонтактнаяИнформация
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.КонтактнаяИнформация
	УправлениеКонтактнойИнформацией.ПередЗаписьюНаСервере(ЭтаФорма, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.КонтактнаяИнформация
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ПриИзмененииГруппДоступа();
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.КонтактнаяИнформация
	УправлениеКонтактнойИнформацией.ПослеЗаписиНаСервере(ЭтаФорма, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.КонтактнаяИнформация
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьЗаголовкиСтраниц();
	
КонецПроцедуры

#Область СтандартныеПодсистемы_КонтактнаяИнформация

&НаСервере
Процедура Подключаемый_ОбновитьКонтактнуюИнформацию(Результат)
	УправлениеКонтактнойИнформацией.ОбновитьКонтактнуюИнформацию(ЭтотОбъект, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_КонтактнаяИнформацияПриИзменении(Элемент)
	
	УправлениеКонтактнойИнформациейКлиент.ПредставлениеПриИзменении(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_КонтактнаяИнформацияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Результат = УправлениеКонтактнойИнформациейКлиент.ПредставлениеНачалоВыбора(ЭтотОбъект, Элемент, , СтандартнаяОбработка);
	ОбновитьКонтактнуюИнформацию(Результат);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_КонтактнаяИнформацияОчистка(Элемент, СтандартнаяОбработка)
	
	Результат = УправлениеКонтактнойИнформациейКлиент.ПредставлениеОчистка(ЭтотОбъект, Элемент.Имя);
	ОбновитьКонтактнуюИнформацию(Результат);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_КонтактнаяИнформацияВыполнитьКоманду(Команда)
	
	Результат = УправлениеКонтактнойИнформациейКлиент.ПодключаемаяКоманда(ЭтотОбъект, Команда.Имя);
	ОбновитьКонтактнуюИнформацию(Результат);
	УправлениеКонтактнойИнформациейКлиент.ОткрытьФормуВводаАдреса(ЭтотОбъект, Результат);
	
КонецПроцедуры

&НаСервере
Функция ОбновитьКонтактнуюИнформацию(Результат = Неопределено)
	
	Возврат УправлениеКонтактнойИнформацией.ОбновитьКонтактнуюИнформацию(ЭтотОбъект, Объект, Результат);
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ОграничениеДоступаПоКомпонентамПриИзменении(Элемент)
	
	Если Не Объект.ОграничениеДоступаПоКомпонентам Тогда
		Объект.ДоступныеКомпоненты.Очистить();
	КонецЕсли;
	Элементы.ДоступныеКомпоненты.ТолькоПросмотр = Не Объект.ОграничениеДоступаПоКомпонентам;
	
КонецПроцедуры

&НаКлиенте
Процедура ГруппаМенеджеровПриИзменении(Элемент)
	
	ПриИзмененииГруппДоступа();
	
КонецПроцедуры

&НаКлиенте
Процедура ГруппаКонсультантовПриИзменении(Элемент)
	
	ПриИзмененииГруппДоступа();
	
КонецПроцедуры

&НаКлиенте
Процедура ГруппаЭкспертовПриИзменении(Элемент)
	
	ПриИзмененииГруппДоступа();
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийТаблицыФормыДоступныеКомпоненты

&НаКлиенте
Процедура ДоступныеКомпонентыПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	УстановитьЗаголовкиСтраниц();
	
КонецПроцедуры

&НаКлиенте
Процедура ДоступныеКомпонентыПослеУдаления(Элемент)
	
	УстановитьЗаголовкиСтраниц();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗагрузитьЧасовыеПояса()
	
	Элементы.ЧасовойПояс.СписокВыбора.Очистить();
	ОбщегоНазначенияУСП.ЗаполнитьСписокЧасовыхПоясов(Элементы.ЧасовойПояс.СписокВыбора);
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииПриЧтенииНаСервере()
	
	Если ЗначениеЗаполнено(Объект.ГруппаКонсультантов) 
	 Или ЗначениеЗаполнено(Объект.ГруппаМенеджеров)
	 Или ЗначениеЗаполнено(Объект.ГруппаЭкспертов) Тогда
		Элементы.ОграничениеДоступаПоКомпонентам.ТолькоПросмотр = Истина;
	КонецЕсли;
	
	Если Объект.ОграничениеДоступаПоКомпонентам Тогда
		Элементы.ДоступныеКомпоненты.ТолькоПросмотр = Ложь;
	Иначе
		Элементы.ДоступныеКомпоненты.ТолькоПросмотр = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьЗаголовкиСтраниц()
	
	Если Объект.ДоступныеКомпоненты.Количество() = 0 Тогда
		Элементы.ГруппаДоступныеКомпоненты.Заголовок = НСтр("ru='Доступные компоненты (все разрешены)'");
	Иначе
		Элементы.ГруппаДоступныеКомпоненты.Заголовок = СтрШаблон(НСтр("ru='Доступные компоненты (%1)'"),Объект.ДоступныеКомпоненты.Количество());
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПараметрыВыбораКомпонентов()
	
	СервисыВладельцы = Новый Массив;
	Для каждого Строка Из ДоступныеСервисы Цикл
		СервисыВладельцы.Добавить(Строка.Значение);
	КонецЦикла; 
	
	ДоступныеКомпонентыДляОтбора = Новый Массив;
	Для каждого Строка Из ДоступныеКомпоненты Цикл
		ДоступныеКомпонентыДляОтбора.Добавить(Строка.Значение);
	КонецЦикла; 
	
	ПараметрыДляВыбора = Новый Массив();
	Если СервисыВладельцы.Количество() > 0 Тогда
		ПараметрыДляВыбора.Добавить(Новый ПараметрВыбора("Отбор.Владелец", СервисыВладельцы));
	КонецЕсли;
	Если ДоступныеКомпонентыДляОтбора.Количество() > 0 Тогда
		ПараметрыДляВыбора.Добавить(Новый ПараметрВыбора("Отбор.Ссылка", ДоступныеКомпонентыДляОтбора));
	КонецЕсли;
	
	Элементы.ДоступныеКомпонентыКомпонент.ПараметрыВыбора = Новый ФиксированныйМассив(ПараметрыДляВыбора);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииГруппДоступа()
	
	Если ЗначениеЗаполнено(Объект.ГруппаКонсультантов) 
	 Или ЗначениеЗаполнено(Объект.ГруппаМенеджеров)
	 Или ЗначениеЗаполнено(Объект.ГруппаЭкспертов) Тогда
		Элементы.ОграничениеДоступаПоКомпонентам.ТолькоПросмотр = Истина;
		Объект.ОграничениеДоступаПоКомпонентам = Истина;
		Элементы.ДоступныеКомпоненты.ТолькоПросмотр = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти 
