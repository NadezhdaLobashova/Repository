
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.КонтактнаяИнформация
	УправлениеКонтактнойИнформацией.ПриСозданииНаСервере(ЭтаФорма, Объект, "ПанельКонтактнаяИнформация", ПоложениеЗаголовкаЭлементаФормы.Лево);
	// Конец СтандартныеПодсистемы.КонтактнаяИнформация
	
	ЭтоПолноправныйПользователь = Пользователи.ЭтоПолноправныйПользователь();
	
	// Отображение контактной информации
	Если Не (РольДоступна(Метаданные.Роли.ЧтениеИзменениеКонтактнойИнформации) Или ЭтоПолноправныйПользователь) Тогда
		Элементы.ПанельКонтактнаяИнформация.Видимость = Ложь;
	КонецЕсли;
	
	Если Не ЭтоПолноправныйПользователь Тогда
		Элементы.ГруппаГруппыПользователей.Видимость = Ложь;
	КонецЕсли; 
	
	Если Объект.Ссылка <> Справочники.ОбслуживающиеОрганизации.СлужбаПоддержки Тогда
		Элементы.ГруппаЭкспертов.Видимость = Ложь;
	Иначе
		Элементы.ДоступныеКомпоненты.ТолькоПросмотр = Истина;
		Элементы.ДоступныеСервисы.ТолькоПросмотр = Истина;
	КонецЕсли;
	
	ЗагрузитьЧасовыеПояса();
	УстановитьПараметрыВыбораКомпонентов();
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.КонтактнаяИнформация
	УправлениеКонтактнойИнформацией.ПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.КонтактнаяИнформация
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	// СтандартныеПодсистемы.КонтактнаяИнформация
	УправлениеКонтактнойИнформацией.ОбработкаПроверкиЗаполненияНаСервере(ЭтаФорма, Объект, Отказ);
	// Конец СтандартныеПодсистемы.КонтактнаяИнформация
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.КонтактнаяИнформация
	УправлениеКонтактнойИнформацией.ПередЗаписьюНаСервере(ЭтаФорма, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.КонтактнаяИнформация
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.КонтактнаяИнформация
	УправлениеКонтактнойИнформацией.ПослеЗаписиНаСервере(ЭтаФорма, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.КонтактнаяИнформация
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьЗаголовкиСтраниц();
	
КонецПроцедуры


#Область СтандартныеПодсистемы_КонтактнаяИнформация

&НаСервере
Процедура Подключаемый_ОбновитьКонтактнуюИнформацию(Результат)
	УправлениеКонтактнойИнформацией.ОбновитьКонтактнуюИнформацию(ЭтотОбъект, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_КонтактнаяИнформацияПриИзменении(Элемент)
	
	УправлениеКонтактнойИнформациейКлиент.ПредставлениеПриИзменении(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_КонтактнаяИнформацияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Результат = УправлениеКонтактнойИнформациейКлиент.ПредставлениеНачалоВыбора(ЭтотОбъект, Элемент, , СтандартнаяОбработка);
	ОбновитьКонтактнуюИнформацию(Результат);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_КонтактнаяИнформацияОчистка(Элемент, СтандартнаяОбработка)
	
	Результат = УправлениеКонтактнойИнформациейКлиент.ПредставлениеОчистка(ЭтотОбъект, Элемент.Имя);
	ОбновитьКонтактнуюИнформацию(Результат);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_КонтактнаяИнформацияВыполнитьКоманду(Команда)
	
	Результат = УправлениеКонтактнойИнформациейКлиент.ПодключаемаяКоманда(ЭтотОбъект, Команда.Имя);
	ОбновитьКонтактнуюИнформацию(Результат);
	УправлениеКонтактнойИнформациейКлиент.ОткрытьФормуВводаАдреса(ЭтотОбъект, Результат);
	
КонецПроцедуры

&НаСервере
Функция ОбновитьКонтактнуюИнформацию(Результат = Неопределено)
	
	Возврат УправлениеКонтактнойИнформацией.ОбновитьКонтактнуюИнформацию(ЭтотОбъект, Объект, Результат);
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область ОбработчикиСобытийТаблицыФормыДоступныеСервисы

&НаКлиенте
Процедура ДоступныеСервисыПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	ПриИзмененииСервиса();
	УстановитьЗаголовкиСтраниц();
	
КонецПроцедуры

&НаКлиенте
Процедура ДоступныеСервисыПослеУдаления(Элемент)
	
	ПриИзмененииСервиса();
	УстановитьЗаголовкиСтраниц();
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийТаблицыФормыДоступныеКомпоненты

&НаКлиенте
Процедура ДоступныеКомпонентыПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	УстановитьЗаголовкиСтраниц();
	
КонецПроцедуры
	
&НаКлиенте
Процедура ДоступныеКомпонентыПослеУдаления(Элемент)
	
	УстановитьЗаголовкиСтраниц();
	
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗагрузитьЧасовыеПояса()
	
	Элементы.ЧасовойПояс.СписокВыбора.Очистить();
	ОбщегоНазначенияУСП.ЗаполнитьСписокЧасовыхПоясов(Элементы.ЧасовойПояс.СписокВыбора);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииСервиса()
	
	СервисыВладельцы = Новый Массив;
	Для каждого Строка Из Объект.ДоступныеСервисы Цикл
		СервисыВладельцы.Добавить(Строка.Сервис);
	КонецЦикла; 
	
	ПараметрыДляВыбора = Новый Массив();
	ПараметрыДляВыбора.Добавить(Новый ПараметрВыбора("Отбор.Владелец", СервисыВладельцы));
	Элементы.ДоступныеКомпонентыКомпонент.ПараметрыВыбора = Новый ФиксированныйМассив(ПараметрыДляВыбора);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПараметрыВыбораКомпонентов()
	
	СервисыВладельцы = Новый Массив;
	Для каждого Строка Из Объект.ДоступныеСервисы Цикл
		СервисыВладельцы.Добавить(Строка.Сервис);
	КонецЦикла; 
	
	ПараметрыДляВыбора = Новый Массив();
	ПараметрыДляВыбора.Добавить(Новый ПараметрВыбора("Отбор.Владелец", СервисыВладельцы));
	Элементы.ДоступныеКомпонентыКомпонент.ПараметрыВыбора = Новый ФиксированныйМассив(ПараметрыДляВыбора);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьЗаголовкиСтраниц()
	
	Если Объект.ДоступныеСервисы.Количество() = 0 Тогда
		Элементы.ГруппаДоступныеСервисы.Заголовок = НСтр("ru='Доступные сервисы (все разрешены)'");
	Иначе
		Элементы.ГруппаДоступныеСервисы.Заголовок = СтрШаблон(НСтр("ru='Доступные сервисы (%1)'"),Объект.ДоступныеСервисы.Количество());
	КонецЕсли;
	
	Если Объект.ДоступныеКомпоненты.Количество() = 0 Тогда
		Элементы.ГруппаДоступныеКомпоненты.Заголовок = НСтр("ru='Доступные компоненты (все разрешены)'");
	Иначе
		Элементы.ГруппаДоступныеКомпоненты.Заголовок = СтрШаблон(НСтр("ru='Доступные компоненты (%1)'"),Объект.ДоступныеКомпоненты.Количество());
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЧасовойПоясПриИзменении(Элемент)

    ЧасовойПоясПриИзмененииНаСервере();
    
КонецПроцедуры

&НаСервере
Процедура ЧасовойПоясПриИзмененииНаСервере()
    
	МестноеВремя = ТекущаяДата();
	Объект.ОтклонениеОтУниверсальногоВремениВСекундах = МестноеВремя - УниверсальноеВремя(МестноеВремя, Объект.ЧасовойПояс);
	
КонецПроцедуры

#КонецОбласти 
