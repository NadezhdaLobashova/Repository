#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;  
	КонецЕсли;
    
    Если Ссылка.Пустая() И Не ЗначениеЗаполнено(ПолучитьСсылкуНового()) Тогда
		СсылкаНового = Справочники.ОбслуживающиеОрганизации.ПолучитьСсылку(Новый УникальныйИдентификатор());
		УстановитьСсылкуНового(СсылкаНового);
		СсылкаНаОбъект = ПолучитьСсылкуНового();
	Иначе
		СсылкаНаОбъект = Ссылка;
	КонецЕсли; 
	
	ИсходноеНаименование = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "Наименование");
	ЭтоСлужбаПоддержки = (Ссылка = Справочники.ОбслуживающиеОрганизации.СлужбаПоддержки);
	
	Если Не ЗначениеЗаполнено(ГруппаМенеджеров) Тогда
		ГруппаМенеджеров = НоваяГруппаПользователей(
			СсылкаНаОбъект,
			?(ЭтоСлужбаПоддержки, 
				Справочники.ПрофилиГруппДоступа.МенеджерСлужбыПоддержки,
				Справочники.ПрофилиГруппДоступа.МенеджерОбслуживающейОрганизации),
				СтрШаблон(НСтр("ru='%1\Менеджеры'"), Наименование));
	ИначеЕсли Наименование <> ИсходноеНаименование Тогда
		Если ЗначениеЗаполнено(ГруппаМенеджеров) Тогда
			ГруппаМенеджеровОбъект = ГруппаМенеджеров.ПолучитьОбъект();
			ГруппаМенеджеровОбъект.Наименование = СтрШаблон(НСтр("ru='%1\Менеджеры'"), Наименование); 
			ГруппаМенеджеровОбъект.Записать();
		КонецЕсли; 
	КонецЕсли; 
	
	Если Не ЗначениеЗаполнено(ГруппаКонсультантов) Тогда
		ГруппаКонсультантов = НоваяГруппаПользователей(
			СсылкаНаОбъект,
			?(ЭтоСлужбаПоддержки, 
				Справочники.ПрофилиГруппДоступа.КонсультантСлужбыПоддержки, 
				Справочники.ПрофилиГруппДоступа.КонсультантОбслуживающейОрганизации),
				СтрШаблон(НСтр("ru='%1\Консультанты'"), Наименование));
	ИначеЕсли Наименование <> ИсходноеНаименование Тогда
		Если ЗначениеЗаполнено(ГруппаКонсультантов) Тогда
			ГруппаКонсультантовОбъект = ГруппаКонсультантов.ПолучитьОбъект();
			ГруппаКонсультантовОбъект.Наименование = СтрШаблон(НСтр("ru='%1\Консультанты'"), Наименование);
			ГруппаКонсультантовОбъект.Записать();
		КонецЕсли; 
	КонецЕсли; 
		
	Если (Не ЗначениеЗаполнено(ГруппаЭкспертов)) И ЭтоСлужбаПоддержки Тогда
		ГруппаЭкспертов = НоваяГруппаПользователей(
			СсылкаНаОбъект, Справочники.ПрофилиГруппДоступа.ЭкспертСлужбыПоддержки,
			СтрШаблон(НСтр("ru='%1\Эксперты'"), Наименование));
	ИначеЕсли Наименование <> ИсходноеНаименование Тогда
		Если ЗначениеЗаполнено(ГруппаЭкспертов) Тогда
			ГруппаЭкспертовОбъект = ГруппаЭкспертов.ПолучитьОбъект();
			ГруппаЭкспертовОбъект.Наименование = СтрШаблон(НСтр("ru='%1\Эксперты'"), Наименование);
			ГруппаЭкспертовОбъект.Записать();
		КонецЕсли; 
	КонецЕсли; 
	
	// Перезаполним права групп доступа к сервисам.
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ГруппыДоступаПользователи.Ссылка
	|ИЗ
	|	Справочник.ГруппыДоступа.Пользователи КАК ГруппыДоступаПользователи
	|ГДЕ
	|	ГруппыДоступаПользователи.Пользователь В(&ГруппыПользователейОО)";
	
	ГруппыПользователейОО = Новый Массив;
	
	ГруппыПользователейОО.Добавить(ГруппаКонсультантов);
	ГруппыПользователейОО.Добавить(ГруппаМенеджеров);
	Если ЭтоСлужбаПоддержки Тогда
		ГруппыПользователейОО.Добавить(ГруппаЭкспертов);
	КонецЕсли; 
	
	Запрос.УстановитьПараметр("ГруппыПользователейОО", ГруппыПользователейОО);
	Результат = Запрос.Выполнить();
	
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		ГруппаДоступаОбъект = Выборка.Ссылка.ПолучитьОбъект();
		
		Если Наименование <> ИсходноеНаименование Тогда
			Если ГруппаДоступаОбъект.Пользователи.Найти(ГруппаМенеджеров) <> Неопределено Тогда
				ГруппаДоступаОбъект.Наименование = СтрШаблон(НСтр("ru='%1\Менеджеры'"),Наименование);
			ИначеЕсли ГруппаДоступаОбъект.Пользователи.Найти(ГруппаКонсультантов) <> Неопределено Тогда
				ГруппаДоступаОбъект.Наименование = СтрШаблон(НСтр("ru='%1\Консультанты'"),Наименование);
			ИначеЕсли ГруппаДоступаОбъект.Пользователи.Найти(ГруппаЭкспертов) <> Неопределено Тогда
				ГруппаДоступаОбъект.Наименование = СтрШаблон(НСтр("ru='%1\Эксперты'"),Наименование);
			КонецЕсли;
		КонецЕсли;
		
		Если Ссылка = Справочники.ОбслуживающиеОрганизации.СлужбаПоддержки Тогда
			ГруппаДоступаОбъект.Записать();
			Продолжить;
		КонецЕсли;
			
		// Перезаполним доступные сервисы
		ЗначенияДоступаСервис = ГруппаДоступаОбъект.ЗначенияДоступа.НайтиСтроки(
			Новый Структура("ВидДоступа", Справочники.Сервисы.ПустаяСсылка()));
		
		Для каждого Строка Из ЗначенияДоступаСервис Цикл
			ГруппаДоступаОбъект.ЗначенияДоступа.Удалить(Строка);
		КонецЦикла; 
		
		ВидыДоступаСервис = ГруппаДоступаОбъект.ВидыДоступа.НайтиСтроки(Новый Структура("ВидДоступа", Справочники.Сервисы.ПустаяСсылка()));
		
		Если ДоступныеСервисы.Количество() = 0 Тогда
			Если ВидыДоступаСервис.Количество() > 0 Тогда
				ВидыДоступаСервис[0].ВсеРазрешены = Истина;
			КонецЕсли;
		Иначе
			Если ВидыДоступаСервис.Количество() > 0 Тогда
				ВидыДоступаСервис[0].ВсеРазрешены = Ложь;
			КонецЕсли;
			Для каждого Строка Из ДоступныеСервисы Цикл
				ЗначениеДоступаСервис = ГруппаДоступаОбъект.ЗначенияДоступа.Добавить();
				ЗначениеДоступаСервис.ВидДоступа = Справочники.Сервисы.ПустаяСсылка();
				ЗначениеДоступаСервис.ЗначениеДоступа = Строка.Сервис;
			КонецЦикла;
		КонецЕсли;
		
		// Перезаполним доступные компоненты сервисов
		ЗначенияДоступаКомпоненты = ГруппаДоступаОбъект.ЗначенияДоступа.НайтиСтроки(
			Новый Структура("ВидДоступа", Справочники.КомпонентыСервиса.ПустаяСсылка()));
			
		Для каждого Строка Из ЗначенияДоступаКомпоненты Цикл
			ГруппаДоступаОбъект.ЗначенияДоступа.Удалить(Строка);
		КонецЦикла; 
			
		ВидыДоступаКомпоненты = ГруппаДоступаОбъект.ВидыДоступа.НайтиСтроки(Новый Структура("ВидДоступа", Справочники.КомпонентыСервиса.ПустаяСсылка()));
		
		Если ДоступныеКомпоненты.Количество() = 0 Тогда
			Если ВидыДоступаКомпоненты.Количество() > 0 Тогда
				ВидыДоступаКомпоненты[0].ВсеРазрешены = Истина;
			КонецЕсли;
		Иначе
			Если ВидыДоступаКомпоненты.Количество() > 0 Тогда
				ВидыДоступаКомпоненты[0].ВсеРазрешены = Ложь;
			КонецЕсли;
			Для каждого Строка Из ДоступныеКомпоненты Цикл
				ЗначениеДоступаСервис = ГруппаДоступаОбъект.ЗначенияДоступа.Добавить();
				ЗначениеДоступаСервис.ВидДоступа = Справочники.КомпонентыСервиса.ПустаяСсылка();
				ЗначениеДоступаСервис.ЗначениеДоступа = Строка.Компонент;
			КонецЦикла;
		КонецЕсли;
			
		ГруппаДоступаОбъект.Записать();
		
	КонецЦикла;
	
	Если Не ЗначениеЗаполнено(ПерваяЛинияПоддержки) Тогда
		
		ПерваяЛинияПоддержкиОбъект = Справочники.ЛинииПоддержки.СоздатьЭлемент();
		ПерваяЛинияПоддержкиОбъект.Владелец = СсылкаНаОбъект;
		ПерваяЛинияПоддержкиОбъект.Наименование = НСтр("ru='Первая линия'"); 
		ПерваяЛинияПоддержкиОбъект.Записать();
		ПерваяЛинияПоддержки = ПерваяЛинияПоддержкиОбъект.Ссылка;
		
	КонецЕсли; 
	                                                                                          
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	ГруппаКонсультантов = Неопределено;
	ГруппаМенеджеров = Неопределено;
	ГруппаЭкспертов = Неопределено;
	ПерваяЛинияПоддержки = Неопределено;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция НоваяГруппаПользователей(СсылкаНаОбъект, ПрофильДоступа, НаименованиеГруппы)
	
	ГруппаДоступаОбъект = Справочники.ГруппыДоступа.СоздатьЭлемент();
	ГруппаДоступаОбъект.Наименование = НаименованиеГруппы;
	ГруппаДоступаОбъект.Профиль = ПрофильДоступа;
	ГруппаДоступаОбъект.Ответственный = Пользователи.ТекущийПользователь();
	
	ВидДоступаОО = ГруппаДоступаОбъект.ВидыДоступа.Добавить();
	ВидДоступаОО.ВидДоступа = Справочники.ОбслуживающиеОрганизации.ПустаяСсылка();
	Если СсылкаНаОбъект = Справочники.ОбслуживающиеОрганизации.СлужбаПоддержки Тогда
		ВидДоступаОО.ВсеРазрешены = Истина;
	Иначе
		ВидДоступаОО.ВсеРазрешены = Ложь;
		ЗначениеДоступаОО = ГруппаДоступаОбъект.ЗначенияДоступа.Добавить();
		ЗначениеДоступаОО.ВидДоступа = Справочники.ОбслуживающиеОрганизации.ПустаяСсылка();
		ЗначениеДоступаОО.ЗначениеДоступа = СсылкаНаОбъект;
	КонецЕсли; 
	
	ВидДоступаСервис = ГруппаДоступаОбъект.ВидыДоступа.Добавить();
	ВидДоступаСервис.ВидДоступа = Справочники.Сервисы.ПустаяСсылка();
	Если СсылкаНаОбъект = Справочники.ОбслуживающиеОрганизации.СлужбаПоддержки Тогда
		ВидДоступаСервис.ВсеРазрешены = Истина;
	Иначе
		Если ДоступныеСервисы.Количество() = 0 Тогда
			ВидДоступаСервис.ВсеРазрешены = Истина;
		Иначе
			Для каждого Строка Из ДоступныеСервисы Цикл
				ЗначениеДоступаСервис = ГруппаДоступаОбъект.ЗначенияДоступа.Добавить();
				ЗначениеДоступаСервис.ВидДоступа = Справочники.Сервисы.ПустаяСсылка();
				ЗначениеДоступаСервис.ЗначениеДоступа = Строка.Сервис;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	ВидДоступаКомпонент = ГруппаДоступаОбъект.ВидыДоступа.Добавить();
	ВидДоступаКомпонент.ВидДоступа = Справочники.КомпонентыСервиса.ПустаяСсылка();
	Если СсылкаНаОбъект = Справочники.ОбслуживающиеОрганизации.СлужбаПоддержки Тогда
		ВидДоступаКомпонент.ВсеРазрешены = Истина;
	Иначе
		Если ДоступныеКомпоненты.Количество() = 0 Тогда
			ВидДоступаКомпонент.ВсеРазрешены = Истина;
		Иначе
			Для каждого Строка Из ДоступныеКомпоненты Цикл
				ЗначениеДоступаСервис = ГруппаДоступаОбъект.ЗначенияДоступа.Добавить();
				ЗначениеДоступаСервис.ВидДоступа = Справочники.КомпонентыСервиса.ПустаяСсылка();
				ЗначениеДоступаСервис.ЗначениеДоступа = Строка.Компонент;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли; 
	
	НоваяГруппаПользователей = Справочники.ГруппыПользователей.СоздатьЭлемент();
	НоваяГруппаПользователей.Наименование = НаименованиеГруппы;
	НоваяГруппаПользователей.Записать();
	НоваяГруппаПользователейСсылка = НоваяГруппаПользователей.Ссылка;
	
	ПользователиГруппы = ГруппаДоступаОбъект.Пользователи.Добавить();
	ПользователиГруппы.Пользователь = НоваяГруппаПользователейСсылка;
	
	ГруппаДоступаОбъект.Записать();
	
	Возврат НоваяГруппаПользователейСсылка;
	
КонецФункции 

#КонецОбласти

#КонецЕсли
