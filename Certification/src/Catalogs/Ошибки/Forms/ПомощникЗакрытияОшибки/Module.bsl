
#Область ОбработчикиСобытийФормы
    
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
    
    Параметры.Свойство("КоличествоОбращенийДляОбработки", КоличествоОбращенийДляОбработки);
    ЧислоИСлово = СтроковыеФункцииКлиентСервер.ЧислоЦифрамиПредметИсчисленияПрописью(
        КоличествоОбращенийДляОбработки, НСтр("ru = 'обращение, обращения, обращений,,,,,,0'"));
    
    Элементы.ДействияЗаголовок.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
        НСтр("ru='Обработать %1 по ошибке'"), ЧислоИСлово) + ":"; 
    
    КомментарийЗакрытияОшибки = НСтр("ru='Ошибка исправлена'");
    ДобавитьКомментарийЗакрытияОшибки = Истина;
    ЗакрытьОбращения = Истина;
    ПеревестиНаПервуюЛинию = Истина;
    
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

    Элементы.КомментарийЗакрытияОшибки.Доступность = ДобавитьКомментарийЗакрытияОшибки;
    
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ДобавитьКомментарийПриИзменении(Элемент)
    
    Элементы.КомментарийЗакрытияОшибки.Доступность = ДобавитьКомментарийЗакрытияОшибки;
    
КонецПроцедуры
 
#КонецОбласти 

#Область ОбработчикиКомандФормы
    
&НаКлиенте
Процедура ВыполнитьДействия(Команда)
   
    Если Не ДобавитьКомментарийЗакрытияОшибки И Не ПеревестиНаПервуюЛинию И Не ЗакрытьОбращения Тогда
        ТекстВопроса = НСтр("ru='Не выбрано никаких действий. Продолжить?'");
        Оповещение = Новый ОписаниеОповещения("ВыполнитьДействияЗавершение", ЭтотОбъект);
        Кнопки = Новый СписокЗначений;
        Кнопки.Добавить(КодВозвратаДиалога.Да, НСтр("ru='Продолжить'"));
        Кнопки.Добавить(КодВозвратаДиалога.Отмена, НСтр("ru='Выбрать действия'"));
        ПоказатьВопрос(Оповещение, ТекстВопроса, Кнопки,, КодВозвратаДиалога.Да);
        Возврат; 
    КонецЕсли; 
    
    ЗакрытьФормуСПараметрами();
    
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
    
    Закрыть();
    
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ВыполнитьДействияЗавершение(Результат, ПараметрыОповещения) Экспорт
    
    Если Результат = КодВозвратаДиалога.Да Тогда
        Закрыть();
    КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьФормуСПараметрами()
	
    Если Не ПроверитьЗаполнение() Тогда
        Возврат;
    КонецЕсли; 
    
    ПараметрыЗакрытия = Новый Структура;
    ПараметрыЗакрытия.Вставить("КомментарийЗакрытияОшибки", КомментарийЗакрытияОшибки);
    ПараметрыЗакрытия.Вставить("ДобавитьКомментарийЗакрытияОшибки", ДобавитьКомментарийЗакрытияОшибки);
    ПараметрыЗакрытия.Вставить("ПеревестиНаПервуюЛинию", ПеревестиНаПервуюЛинию);
    ПараметрыЗакрытия.Вставить("ЗакрытьОбращения", ЗакрытьОбращения);
    
    Закрыть(ПараметрыЗакрытия);
	
КонецПроцедуры

#КонецОбласти 


