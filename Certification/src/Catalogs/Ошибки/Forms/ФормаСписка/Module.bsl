
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
    
	УстановитьУсловноеОформление();
	 
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПринятьВРаботу(Команда)
	
    Ссылки = ОбслуживаниеКлиент.СсылкиИзСтрокСписка(Элементы.Список.ВыделенныеСтроки);
    ПринятьВРаботуНаСервере(Ссылки);
    ОбслуживаниеКлиент.ПринятьОшибкиВРаботуЗавершение(Ссылки);
	
КонецПроцедуры

&НаКлиенте
Процедура ПеревестиНаЛинию(Команда)
	
    Ссылки = ОбслуживаниеКлиент.СсылкиИзСтрокСписка(Элементы.Список.ВыделенныеСтроки);
	ПеревестиНаЛиниюНаСервере(Ссылки);
    ОбслуживаниеКлиент.ПеревестиОшибкиНаЛиниюЗавершение(Ссылки);
	
КонецПроцедуры

&НаКлиенте
Процедура Перенаправить(Команда)
	
	Оповещение = Новый ОписаниеОповещения("ПеренаправитьЗавершение", ЭтотОбъект);
    Объекты = Элементы.Список.ВыделенныеСтроки;
    ОбслуживаниеКлиент.ПеренаправитьОбъекты(Объекты, Тип("СправочникСсылка.Ошибки"), Оповещение);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПринятьВРаботуНаСервере(Ссылки)
    
	Обслуживание.ПринятьОбъектыВРаботу(Ссылки, Элементы.Список);
	
КонецПроцедуры

&НаСервере
Процедура ПеревестиНаЛиниюНаСервере(Ссылки)
    
	Обслуживание.ПеревестиОбъектыНаЛинию(Ссылки, Элементы.Список);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
    
    ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "Запланирована", Перечисления.СостоянияОшибок.Запланирована);
    ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "Расследование", Перечисления.СостоянияОшибок.Расследование);
    ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "НаИсправлении", Перечисления.СостоянияОшибок.НаИсправлении);
    ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "Исправлена", Перечисления.СостоянияОшибок.Исправлена);
    
    УсловноеОформление.Элементы.Очистить();
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных("Список");
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Список.Критичная");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветОсобогоТекста);
	
КонецПроцедуры

&НаКлиенте
Процедура ПеренаправитьЗавершение(Результат, ПараметрыОповещения) Экспорт
	
	Если Результат <> Неопределено Тогда
		ОповеститьОбИзменении(Тип("СправочникСсылка.Ошибки"));
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти


